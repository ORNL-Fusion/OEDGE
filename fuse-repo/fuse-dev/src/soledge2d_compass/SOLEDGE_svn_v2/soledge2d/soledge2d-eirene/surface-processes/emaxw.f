C
C
      FUNCTION EIRENE_EMAXW(TI,XMPER,YMPAR)
C
C  MEAN ENERGY OF AN ION, GIVEN A TRUNCATED MAXWELLIAN FLUX DENSITY
C  AT TI (EV) SHIFTED
C  BY A DRIFT WITH NORMALIZED VELOCITY XMPER PERPENDICULAR
C  ONTO AND YMPAR PARALLEL TO THE TARGET SURFACE
C  IF VDRIFT IS A VELOCITY, THEN XMPER (YMPAR) IS
C  VDRIFT/VTERM WITH VTERM=SQRT(2*TI/RMASS)
C  IN EIRENE UNITS: VDRIFT: CM/SEC
C                   VTERM : SQRT(2*TI/RMASS)*CVEL2A (CM/SEC)
C                           (WITH TI (EV), RMASS (AMU) CVEL2A=9.8226 E5)
      USE EIRMOD_PRECISION
      USE EIRMOD_PARMMOD
      USE EIRMOD_CCONA
 
      IMPLICIT NONE
 
      REAL(DP), INTENT(IN) :: TI, XMPER, YMPAR
      REAL(DP) :: XMM, XMM2, YMM2, ER1, ER2, FACTOR, EIRENE_EMAXW, ERF
 
      IF (XMPER.NE.0.D0) THEN
        XMM=XMPER
        XMM2=XMPER*XMPER
        YMM2=YMPAR*YMPAR
        ER1=EXP(-XMM2)/SQRT(PIA)
        ER2=(1.+ERF(XMM))*XMM
        FACTOR=((XMM2+2.+YMM2)*ER1+(XMM2+2.5+YMM2)*ER2)/(ER1+ER2)
      ELSE
        YMM2=YMPAR*YMPAR
        FACTOR=2.+YMM2
      ENDIF
      EIRENE_EMAXW=FACTOR*TI
      RETURN
      END
