C
      SUBROUTINE EIRENE_WRREC
C
C  EVALUATE EIRENE RECOMMENDATIONS FOR A NEXT RUN OF THE SAME MODEL
C
      USE EIRMOD_PRECISION
      USE EIRMOD_PARMMOD
      USE EIRMOD_CAI
      USE EIRMOD_CCONA
      USE EIRMOD_CTRCEI
      USE EIRMOD_COMSOU
      USE EIRMOD_COMPRT, ONLY: IUNOUT
      USE EIRMOD_COUTAU
 
      IMPLICIT NONE
 
      REAL(DP) :: WSUM, FTOT, XNSUM
      INTEGER :: NREQ, ISTRA
      REAL(DP) :: WTOTT(NSTRA),WMEAN(NSTRA),WREC(NSTRA),XNEXP(NSTRA),
     .          CPUFAC(NSTRA)
 
      OPEN (UNIT=14,ACCESS='SEQUENTIAL',FORM='UNFORMATTED')
      REWIND 14
C
C  FIRSTLY: STRATIFIED SOURCE SAMPLING
C
      IF (NSTRAI.EQ.1) THEN
        RATIO(1)=1.
        GOTO 350
      ENDIF
C
      WSUM=0.
      NREQ=0
      FTOT=0.
      DO 100 ISTRA=1,NSTRAI
        IF (XMCP(ISTRA).LE.0.D0) GOTO 100
        WTOTT(ISTRA)=-WTOTP(0,ISTRA)+WTOTA(0,ISTRA)+
     .                WTOTM(0,ISTRA)+WTOTI(0,ISTRA)
        WTOTT(ISTRA)=WTOTT(ISTRA)/(FLXFAC(ISTRA)+EPS60)
        WMEAN(ISTRA)=WTOTT(ISTRA)/(XMCP(ISTRA)+EPS60)
        WSUM=WSUM+WTOTT(ISTRA)
        NREQ=NREQ+NPTS(ISTRA)
        FTOT=FTOT+FLUXT(ISTRA)
100   CONTINUE
C
C  PROPORTIONAL ALLOCATION: RECOMMENDED REL. WEIGHT PER STRATUM: WREC
C                           EXPECTED REL. NO OF PARTICLES NEEDED: XNEXP
C                           RECOMMENDED NO. OF PARTICLES: NRECOM
C  NOT THE NO. OF PARTICLES BUT THE SUM OF BIRTH WEIGHTS PER STRATUM
C  IS ALLOCATED  PROPORTIONAL TO THE RELATIVE STRATUM POPULATION
C
C
      XNSUM=0.
      DO 200 ISTRA=1,NSTRAI
        IF (XMCP(ISTRA).LE.0.D0) GOTO 200
        WREC(ISTRA)=FLUXT(ISTRA)/(FTOT+EPS60)
        XNEXP(ISTRA)=WREC(ISTRA)/(WMEAN(ISTRA)+EPS60)
C  ACCOUNT FOR DIFFERENT COMPUTING SPEED AT DIFFERENT STRATA
C  ASSUME THEREFORE THAT ALLOCATED CPU TIME WAS PROPORTIONAL NPTS(ISTRA)
        CPUFAC(ISTRA)=XMCP(ISTRA)/(DBLE(NPTS(ISTRA))+EPS60)
        XNEXP(ISTRA)=XNEXP(ISTRA)/(CPUFAC(ISTRA)+EPS60)
        XNSUM=XNSUM+XNEXP(ISTRA)
200   CONTINUE
      DO 300 ISTRA=1,NSTRAI
        RATIO(ISTRA)=0.
        IF (XMCP(ISTRA).LE.0.D0) GOTO 300
C  SCALE XNEXP TO CONSERVE TOTAL NUMBER OF REQUESTED TRACKS
        XNEXP(ISTRA)=XNEXP(ISTRA)*DBLE(NREQ)/(XNSUM+EPS60)
C  CONVERT XNEXP TO AN INTERGER
        NRECOM(ISTRA)=IDINT(REAL(XNEXP(ISTRA),KIND(1.D0)))
        IF (XNEXP(ISTRA)-DBLE(NRECOM(ISTRA)).GT.0.5)
     .      NRECOM(ISTRA)=NRECOM(ISTRA)+1
        RATIO(ISTRA)=WREC(ISTRA)/(WTOTT(ISTRA)/(WSUM+EPS60)+EPS60)
300   CONTINUE
C
350   CONTINUE
      IF (TRCFLE) WRITE (iunout,*) 'WRITE 14: RATIO,NRECOM '
      WRITE (14) RATIO,NRECOM
C
      IF (.NOT.TRCREC.OR.NSTRAI.EQ.1) GOTO 1000
      CALL EIRENE_PAGE
      WRITE (iunout,*)
     .  '=================================================='
      WRITE (iunout,*)
     .  '= RECOMMENDED INPUT MODIFICATIONS FOR STRATIFIED ='
      WRITE (iunout,*)
     .  '= SOURCE SAMPLING                                ='
      WRITE (iunout,*)
     .  '=================================================='
      CALL EIRENE_LEER(3)
      WRITE (iunout,*) 'NPTS OLD = OLD INPUT VALUE FOR NPTS '
      WRITE (iunout,*) 'NPTS REC = EIRENE RECOMMENDATION FOR NEXT RUN '
      WRITE (iunout,*)
     .  'RATIO    = RECOM. REL.WEIGHT / ACTUAL REL. WEIGHT'
      WRITE (iunout,*) 'VALUES OF RATIO CLOSE TO ONE MEAN '
      WRITE (iunout,*) '"ALMOST PROPORTIONAL ALLOCATION OF WEIGHTS"'
      CALL EIRENE_LEER(2)
      WRITE (iunout,*) 'STRAT. NO, NPTS OLD, NPTS REC, RATIO '
      DO 400 ISTRA=1,NSTRAI
        WRITE (iunout,'(1X,I2,8X,I6,4X,I6,5X,1P,E12.4)')
     .                ISTRA,NPTS(ISTRA),NRECOM(ISTRA),RATIO(ISTRA)
400   CONTINUE
      CALL EIRENE_LEER(2)
1000  CONTINUE
C
C  STRATIFIED SOURCE SAMPLING ACCESSMENT FINISHED
C
C  SECONDLY: WEIGHT WINDOWS
C
C  TO BE WRITTEN
C
      RETURN
C
      ENTRY EIRENE_RREC
C
      OPEN (UNIT=14,ACCESS='SEQUENTIAL',FORM='UNFORMATTED')
      REWIND 14
      IF (TRCFLE) WRITE (iunout,*) 'READ 14: RATIO,NRECOM '
      READ (14) RATIO,NRECOM
C
      RETURN
      END
