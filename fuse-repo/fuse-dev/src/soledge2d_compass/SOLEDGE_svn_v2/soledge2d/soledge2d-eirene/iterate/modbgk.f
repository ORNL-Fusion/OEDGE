!pb  24.11.06: flag for shifting of first parameter of rate-coeff introduced
!pb  24.11.06: BZIN initialized with 1
C
C
      SUBROUTINE EIRENE_MODBGK

      USE EIRMOD_PRECISION
      USE EIRMOD_PARMMOD
      USE EIRMOD_COMUSR
      USE EIRMOD_CESTIM
      USE EIRMOD_CCONA
      USE EIRMOD_CLOGAU
      USE EIRMOD_CINIT
      USE EIRMOD_CGRID
      USE EIRMOD_CZT1
      USE EIRMOD_CTRCEI
      USE EIRMOD_CGEOM
      USE EIRMOD_CSDVI
      USE EIRMOD_CSDVI_BGK
      USE EIRMOD_CSDVI_COP
      USE EIRMOD_COMPRT
      USE EIRMOD_COMNNL
      USE EIRMOD_COMSOU
      USE EIRMOD_COUTAU
      USE EIRMOD_COMXS
      USE EIRMOD_CSPEI

      IMPLICIT NONE
C
      REAL(DP), ALLOCATABLE :: PDEN(:),  EDEN(:),
     .                       PDEN2(:), EDEN2(:), ENERGY(:,:)
      REAL(DP) :: RATM(3)
      REAL(DP) :: VXIN1, VXIN2, VYIN1, VYIN2, VZIN1, VZIN2, VYMIX, T1,
     .          T2, ED1, ED2, VXMIX, VZMIX, DELX, DELY, DELZ, VX, VY,
     .          VZ, EOLD, ED, RM, FACTKK, TMIX, EBULK, TS1, DS1,
     .          FACT2, RMAS2, A, FACT1, RMAS1, RESE, RESM, TBEL, DOLD,
     .          DEL, PLS, CNDYN, RRN, RATE, RESN, RATN, RRE, RRM,
     .          EIRENE_RATE_COEFF, ERATE
      INTEGER :: ITYP1(NPLS), ITYP2(NPLS), ISPZ1(NPLS), ISPZ2(NPLS),
     .           IREL1(NPLS), INRC1(NPLS)
      INTEGER :: J, IESTM,
     .           ISCDE, IAIN, IPLS1, NRWK1, I, ISP, IPLS2, IATM2, IIEL,
     .           IAEL, IMEL, IUP12, IUP22, IION2, IBGK2, IMOL2, IUP2,
     .           IUP3, IUP1, IBGK1, IP, NRC, IRAD, IR, IT, IREL,
     .           II, KK, NXM, NYM, NZM, IUP32, IPLSTI, IPLSTI1, IPLSTI2,
     .           IPLSV
      INTEGER, EXTERNAL :: EIRENE_IDEZ
      LOGICAL :: LMARK(NPLS)
      LOGICAL :: TRCSAV
C
C
      CALL EIRENE_LEER(3)
      WRITE (iunout,*) 'MODBGK CALLED AFTER ITERATION IITER= ',IITER
      CALL EIRENE_LEER(3)
C
      A=1.D0/REAL(IITER,KIND(1.D0))

      ISTRA=0
      IF (NSTRAI.EQ.1.AND.IESTR.EQ.1) ISTRA=1
      IF (ISTRA.EQ.IESTR) THEN
C  NOTHING TO BE DONE
      ELSEIF (NFILEN.NE.0) THEN
        IESTR=0
        CALL EIRENE_RSTRT(0,NSTRAI,NESTM1,NESTM2,NADSPC,
     .             ESTIMV,ESTIMS,ESTIML,
     .             NSDVI1,SDVI1,NSDVI2,SDVI2,
     .             NSDVC1,SIGMAC,NSDVC2,SGMCS,
     .             NSBGK,SIGMA_BGK,NBGV_STAT,SGMS_BGK,
     .             NSCOP,SIGMA_COP,NCPV_STAT,SGMS_COP,
     .             NSIGI_SPC,TRCFLE)
      ELSE
        WRITE (iunout,*) 'ERROR IN MODBGK: DATA FOR STRATUM ISTRA= ',
     .                    ISTRA
        WRITE (iunout,*) 'ARE NOT AVAILABLE. EXIT CALLED'
        CALL EIRENE_EXIT_OWN(1)
      ENDIF
C
      if (nbmlt.gt.1) then
        write (iunout,*) 'option not ready in modbgk '
        call EIRENE_exit_own(1)
      endif
C
      NBLCKA=0
      ALLOCATE (PDEN(NRAD))
      ALLOCATE (EDEN(NRAD))
      ALLOCATE (PDEN2(NRAD))
      ALLOCATE (EDEN2(NRAD))
      ALLOCATE (ENERGY(NPLS,NRAD))
c
C  LOOP OVER THOSE BACKGROUND ION SPECIES, WHICH ARE ARTIFICIAL
C  SPECIES FOR (NON-LINEAR) ITERATIONS
C .........................................................................
      DO 1000 IPLS=1,NPLSI
C .........................................................................
C
C  IS IPLS AN ARTIFICIAL "BGK BACKGROUND SPECIES"?
C
        ITYP1(IPLS)=-1
        ISPZ1(IPLS)=0

        IPLSTI = MPLSTI(IPLS)
        IPLSV = MPLSV(IPLS)
C
        IF (NPBGKP(IPLS,1).EQ.0) GOTO 1000
C
C  YES. FIND INCIDENT TEST PARTICLE COLLISION PARTNER: ITYP, ISPZ, IREL

        IBGK1=NPBGKP(IPLS,1)
        IUP1=(IBGK1-1)*3+1
        IUP2=(IBGK1-1)*3+2
        IUP3=(IBGK1-1)*3+3
C
C  TRY ATOMS
        DO IATM=1,NATMI
          IF (NPBGKA(IATM).EQ.IBGK1) THEN
            ITYP1(IPLS)=1
            ISPZ1(IPLS)=IATM
            FACT1=CVRSSA(IATM)
            RMAS1=RMASSA(IATM)
            DO IRAD=1,NRAD
              PDEN(IRAD)=PDENA(IATM,IRAD)
              EDEN(IRAD)=EDENA(IATM,IRAD)
            ENDDO
C  FIND INDEX  NRC
            DO NRC=1,NRCA(IATM)
              IP=EIRENE_IDEZ(IBULKA(IATM,NRC),3,3)
              IF (IP.EQ.IPLS) THEN
                INRC1(IPLS)=NRC
                GOTO 10
              ENDIF
            ENDDO
            GOTO 995
C  FIND INDEX IREL
10          DO IAEL=1,NAELI(IATM)
              IF  (LGAEL(IATM,IAEL,1).EQ.IPLS) THEN
                IREL1(IPLS)=LGAEL(IATM,IAEL,0)
                GOTO 50
              ENDIF
            ENDDO
            GOTO 995
          ENDIF
        ENDDO
C  TRY MOLECULES
        DO IMOL=1,NMOLI
          IF (NPBGKM(IMOL).EQ.IBGK1) THEN
            ITYP1(IPLS)=2
            ISPZ1(IPLS)=IMOL
            FACT1=CVRSSM(IMOL)
            RMAS1=RMASSM(IMOL)
            DO IRAD=1,NRAD
              PDEN(IRAD)=PDENM(IMOL,IRAD)
              EDEN(IRAD)=EDENM(IMOL,IRAD)
            ENDDO
C  FIND INDEX  NRC
            DO NRC=1,NRCM(IMOL)
              IP=EIRENE_IDEZ(IBULKM(IMOL,NRC),3,3)
              IF (IP.EQ.IPLS) THEN
                INRC1(IPLS)=NRC
                GOTO 20
              ENDIF
            ENDDO
            GOTO 995
C  FIND INDEX IREL
20          DO IMEL=1,NMELI(IMOL)
              IF  (LGMEL(IMOL,IMEL,1).EQ.IPLS) THEN
                IREL1(IPLS)=LGMEL(IMOL,IMEL,0)
                GOTO 50
              ENDIF
            ENDDO
            GOTO 995
          ENDIF
        ENDDO
C  TRY TEST IONS
        DO IION=1,NIONI
          IF (NPBGKI(IION).EQ.IBGK1) THEN
            ITYP1(IPLS)=3
            ISPZ1(IPLS)=IION
            FACT1=CVRSSI(IION)
            RMAS1=RMASSI(IION)
            DO IRAD=1,NRAD
              PDEN(IRAD)=PDENI(IION,IRAD)
              EDEN(IRAD)=EDENI(IION,IRAD)
            ENDDO
C  FIND INDEX NRC
            DO NRC=1,NRCI(IION)
              IP=EIRENE_IDEZ(IBULKI(IION,NRC),3,3)
              IF (IP.EQ.IPLS) THEN
                INRC1(IPLS)=NRC
                GOTO 30
              ENDIF
            ENDDO
C  FIND INDEX IREL
30          DO IIEL=1,NIELI(IION)
              IF  (LGIEL(IION,IIEL,1).EQ.IPLS) THEN
                IREL1(IPLS)=LGIEL(IION,IIEL,0)
                GOTO 50
              ENDIF
            ENDDO
            GOTO 995
          ENDIF
        ENDDO
        GOTO 995
C
C  SELF COLLISION OR CROSS COLLISION
C
50      CONTINUE
C
        IF (NPBGKP(IPLS,2).EQ.0) THEN
          ITYP2(IPLS)=-1
          ISPZ2(IPLS)=0
C
        ELSEIF (NPBGKP(IPLS,2).NE.0) THEN
C
C  CROSS COLLISION, FIND SECOND COLLISION PARTNER
C  THIS IS NOT THE INGOING COLLIDING TESTPARTICLE,
C  (DETERMINING, E.G., MASS AND DENSITY OF BACKGROUND PARTICLE)
C  BUT THE SECOND, 'ARTIFICIAL', PARTNER
C
          ITYP2(IPLS)=EIRENE_IDEZ(NPBGKP(IPLS,2),1,3)
          ISPZ2(IPLS)=EIRENE_IDEZ(NPBGKP(IPLS,2),3,3)
C
          IF (ITYP2(IPLS).EQ.1) THEN
            IATM2=ISPZ2(IPLS)
            FACT2=CVRSSA(IATM2)
            RMAS2=RMASSA(IATM2)
            DO IRAD=1,NRAD
              PDEN2(IRAD)=PDENA(IATM2,IRAD)
              EDEN2(IRAD)=EDENA(IATM2,IRAD)
            ENDDO
            IBGK2=NPBGKA(IATM2)
          ELSEIF (ITYP2(IPLS).EQ.2) THEN
            IMOL2=ISPZ2(IPLS)
            FACT2=CVRSSM(IMOL2)
            RMAS2=RMASSM(IMOL2)
            DO IRAD=1,NRAD
              PDEN2(IRAD)=PDENM(IMOL2,IRAD)
              EDEN2(IRAD)=EDENM(IMOL2,IRAD)
            ENDDO
            IBGK2=NPBGKM(IMOL2)
          ELSEIF (ITYP2(IPLS).EQ.3) THEN
            IION2=ISPZ2(IPLS)
            FACT2=CVRSSI(IION2)
            RMAS2=RMASSI(IION2)
            DO IRAD=1,NRAD
              PDEN2(IRAD)=PDENI(IION2,IRAD)
              EDEN2(IRAD)=EDENI(IION2,IRAD)
            ENDDO
            IBGK2=NPBGKI(IION2)
          ENDIF
          IUP12=(IBGK2-1)*3+1
          IUP22=(IBGK2-1)*3+2
          IUP32=(IBGK2-1)*3+3
C
        ENDIF
C
        IF (RMASSP(IPLS).NE.RMAS1) THEN
          WRITE (iunout,*) 'INCONSISTENT MASS FOR IPLS= ',IPLS
          WRITE (iunout,*) 'RMASSP(IPLS),RMAS1 ',RMASSP(IPLS),RMAS1
          CALL EIRENE_EXIT_OWN(1)
        ENDIF
C
        CNDYN=AMUA*RMAS1
C
        NXM=MAX(1,NR1STM)
        NYM=MAX(1,NP2NDM)
        NZM=MAX(1,NT3RDM)
C
        RRN=0.
        RRE=0.
        RRM=0.
        RATN=0.
        RATE=0.
        RATM=0.
        RESN=0.
        RESE=0.
        RESM=0.
C
        IREL=IREL1(IPLS)
C
        IF (ITYP2(IPLS).EQ.-1) THEN
C
        WRITE (iunout,*) 'MODBGK: SELF COLLISION, IPLS',IPLS
        WRITE (iunout,*) 'ITYP,ISPZ,IBGK,IREL ',ITYP1(IPLS),ISPZ1(IPLS),
     .                                     IBGK1,IREL1(IPLS)
C
100     CONTINUE
C
        DO 80 IR=1,NXM
          DO 80 IP=1,NYM
            DO 80 IT=1,NZM
              IRAD=IR + ((IP-1)+(IT-1)*NP2T3)*NR1P2 + NBLCKA
C
              TBEL=0.
              IF (LGVAC(IRAD,IPLS)) GOTO 81
              IF (NSTORDR >= NRAD) THEN
                TBEL = TABEL3(IREL,IRAD,1)
              ELSE
                KK=NREAEL(IREL)
                PLS=TIINL(IPLSTI,IRAD)+ADDEL(IREL,IPLS)
                TBEL = EIRENE_RATE_COEFF(KK,PLS,0._DP,.TRUE.,0,ERATE)
     .                 *DIIN(IPLS,IRAD)*FACREA(KK,1)
              END IF
81            CONTINUE
C DELTA_N
              DOLD=DIIN(IPLS,IRAD)
              DEL=DOLD-PDEN(IRAD)
              RATN=RATN+TBEL*DEL*VOL(IRAD)
              IF (TRCMOD) THEN
                WRITE (iunout,*) 'IR,T,TBEL,RATN ',
     .                            IRAD,TIIN(IPLSTI,IRAD),
     .                               TBEL,TBEL*DEL*VOL(IRAD)*ELCHA
                CALL EIRENE_MASR3('DOLD,DNEW,DEL           ',
     .                      DOLD,PDEN(IRAD),DEL)
              ENDIF
              RESN=RESN+TBEL*ABS(DEL)*VOL(IRAD)
C DELTA_E
c             EOLD=(1.5*TIIN(IPLSTI,IRAD)+EDRIFT(IPLS,IRAD))*
c    .             DIIN(IPLS,IRAD)
              EOLD=(1.5*TIIN(IPLSTI,IRAD)+EDRIFT(IPLS,IRAD))*
     .             PDEN(IRAD)
              DEL=EOLD-EDEN(IRAD)
              RATE=RATE+TBEL*DEL*VOL(IRAD)
              RESE=RESE+TBEL*ABS(DEL)*VOL(IRAD)
C DELTA_V
              DELX=BGKV(IUP1,IRAD)-VXIN(IPLSV,IRAD)*DIIN(IPLS,IRAD)
              DELY=BGKV(IUP2,IRAD)-VYIN(IPLSV,IRAD)*DIIN(IPLS,IRAD)
              DELZ=BGKV(IUP3,IRAD)-VZIN(IPLSV,IRAD)*DIIN(IPLS,IRAD)
              RATM(1)=RATM(1)+TBEL*DELX*VOL(IRAD)
              RATM(2)=RATM(2)+TBEL*DELY*VOL(IRAD)
              RATM(3)=RATM(3)+TBEL*DELZ*VOL(IRAD)
C NEW T, NEW V
              VX=BGKV(IUP1,IRAD)/(PDEN(IRAD)+EPS60)
              VY=BGKV(IUP2,IRAD)/(PDEN(IRAD)+EPS60)
              VZ=BGKV(IUP3,IRAD)/(PDEN(IRAD)+EPS60)
              VXIN(IPLSV,IRAD)=VX
              VYIN(IPLSV,IRAD)=VY
              VZIN(IPLSV,IRAD)=VZ
              ED=(VX**2+VY**2+VZ**2)*FACT1
              TIIN(IPLSTI,IRAD)=(EDEN(IRAD)/(PDEN(IRAD)+EPS60)-ED)/1.5
C NEW N
              DIIN(IPLS,IRAD)=PDEN(IRAD)
              RRN=RRN+PDEN(IRAD)*VOL(IRAD)
C             RRM=?
              RRE=RRE+EDEN(IRAD)*VOL(IRAD)
80      CONTINUE
c
c  same as do 80 loop , for additional cell region
c
        DO 90 IRAD=NSURF+1,NSURF+NRADD
C
          TBEL=0.
          IF (LGVAC(IRAD,IPLS)) GOTO 91
          IF (NSTORDR >= NRAD) THEN
            TBEL = TABEL3(IREL,IRAD,1)
          ELSE
            KK=NREAEL(IREL)
            PLS=TIINL(IPLSTI,IRAD)+ADDEL(IREL,IPLS)
            TBEL = EIRENE_RATE_COEFF(KK,PLS,0._DP,.TRUE.,0,ERATE)
     .             *DIIN(IPLS,IRAD)*FACREA(KK,1)
          END IF
91        CONTINUE
C DELTA_N
          DOLD=DIIN(IPLS,IRAD)
          DEL=DOLD-PDEN(IRAD)
          RATN=RATN+TBEL*DEL*VOL(IRAD)
          IF (TRCMOD) THEN
            WRITE (iunout,*) 'IR,T,TBEL,RATN ',IRAD,TIIN(IPLSTI,IRAD),
     .                           TBEL,TBEL*DEL*VOL(IRAD)*ELCHA
            CALL EIRENE_MASR3('DOLD,DNEW,DEL           ',
     .                  DOLD,PDEN(IRAD),DEL)
          ENDIF
          RESN=RESN+TBEL*ABS(DEL)*VOL(IRAD)
C DELTA_E
c         EOLD=(1.5*TIIN(IPLSTI,IRAD)+EDRIFT(IPLS,IRAD))*
c    .          DIIN(IPLS,IRAD)
          EOLD=(1.5*TIIN(IPLSTI,IRAD)+EDRIFT(IPLS,IRAD))*
     .          PDEN(IRAD)
          DEL=EOLD-EDEN(IRAD)
          RATE=RATE+TBEL*DEL*VOL(IRAD)
          RESE=RESE+TBEL*ABS(DEL)*VOL(IRAD)
C DELTA_V
          DELX=BGKV(IUP1,IRAD)-VXIN(IPLSV,IRAD)*DIIN(IPLS,IRAD)
          DELY=BGKV(IUP2,IRAD)-VYIN(IPLSV,IRAD)*DIIN(IPLS,IRAD)
          DELZ=BGKV(IUP3,IRAD)-VZIN(IPLSV,IRAD)*DIIN(IPLS,IRAD)
          RATM(1)=RATM(1)+TBEL*DELX*VOL(IRAD)
          RATM(2)=RATM(2)+TBEL*DELY*VOL(IRAD)
          RATM(3)=RATM(3)+TBEL*DELZ*VOL(IRAD)
C NEW T, NEW V
          VX=BGKV(IUP1,IRAD)/(PDEN(IRAD)+EPS60)
          VY=BGKV(IUP2,IRAD)/(PDEN(IRAD)+EPS60)
          VZ=BGKV(IUP3,IRAD)/(PDEN(IRAD)+EPS60)
          VXIN(IPLSV,IRAD)=VX
          VYIN(IPLSV,IRAD)=VY
          VZIN(IPLSV,IRAD)=VZ
          ED=(VX**2+VY**2+VZ**2)*FACT1
          TIIN(IPLSTI,IRAD)=(EDEN(IRAD)/(PDEN(IRAD)+EPS60)-ED)/1.5
C NEW N
          DIIN(IPLS,IRAD)=PDEN(IRAD)
          RRN=RRN+PDEN(IRAD)*VOL(IRAD)
C         RRM=?
          RRE=RRE+EDEN(IRAD)*VOL(IRAD)
90      CONTINUE
C
        ELSE
C
        WRITE (iunout,*) 'MODBGK: CROSS COLLISION, IPLS ',IPLS
        WRITE (iunout,*) 'ITYP1,ISPZ1,IBGK1,IREL1 ',
     .                    ITYP1(IPLS),ISPZ1(IPLS),
     .                    IBGK1,IREL1(IPLS)
        WRITE (iunout,*) 'ITYP2,ISPZ2,IBGK2       ',
     .                    ITYP2(IPLS),ISPZ2(IPLS),IBGK2
        DO 180 IR=1,NXM
          DO 180 IP=1,NYM
            DO 180 IT=1,NZM
              IRAD=IR + ((IP-1)+(IT-1)*NP2T3)*NR1P2 + NBLCKA
C
              TBEL=0
              IF (LGVAC(IRAD,IPLS)) GOTO 181
              IF (NSTORDR >= NRAD) THEN
                TBEL = TABEL3(IREL,IRAD,1)
              ELSE
                KK=NREAEL(IREL)
                PLS=TIINL(IPLSTI,IRAD)+ADDEL(IREL,IPLS)
                TBEL = EIRENE_RATE_COEFF(KK,PLS,0._DP,.TRUE.,0,ERATE)
     .                 *DIIN(IPLS,IRAD)*FACREA(KK,1)
              END IF
181           CONTINUE
c             EOLD=(1.5*TIIN(IPLSTI,IRAD)+EDRIFT(IPLS,IRAD))*
c    .              DIIN(IPLS,IRAD)
              EOLD=(1.5*TIIN(IPLSTI,IRAD)+EDRIFT(IPLS,IRAD))*
     .              PDEN(IRAD)
              DOLD=DIIN(IPLS,IRAD)
              DEL=EOLD-EDEN(IRAD)
              RATE=RATE+TBEL*DEL*VOL(IRAD)
              DELX=BGKV(IUP1,IRAD)-VXIN(IPLSV,IRAD)*DIIN(IPLS,IRAD)
              DELY=BGKV(IUP2,IRAD)-VYIN(IPLSV,IRAD)*DIIN(IPLS,IRAD)
              DELZ=BGKV(IUP3,IRAD)-VZIN(IPLSV,IRAD)*DIIN(IPLS,IRAD)
              RATM(1)=RATM(1)+TBEL*DELX*VOL(IRAD)
              RATM(2)=RATM(2)+TBEL*DELY*VOL(IRAD)
              RATM(3)=RATM(3)+TBEL*DELZ*VOL(IRAD)
C
              VXIN1=BGKV(IUP1 ,IRAD)/(PDEN (IRAD)+EPS60)
              VXIN2=BGKV(IUP12,IRAD)/(PDEN2(IRAD)+EPS60)
              VXMIX=(RMAS1*VXIN1+RMAS2*VXIN2)/(RMAS1+RMAS2)
              VYIN1=BGKV(IUP2 ,IRAD)/(PDEN (IRAD)+EPS60)
              VYIN2=BGKV(IUP22,IRAD)/(PDEN2(IRAD)+EPS60)
              VYMIX=(RMAS1*VYIN1+RMAS2*VYIN2)/(RMAS1+RMAS2)
              VZIN1=BGKV(IUP3 ,IRAD)/(PDEN (IRAD)+EPS60)
              VZIN2=BGKV(IUP32,IRAD)/(PDEN2(IRAD)+EPS60)
              VZMIX=(RMAS1*VZIN1+RMAS2*VZIN2)/(RMAS1+RMAS2)
              ED1=(VXIN1**2+VYIN1**2+VZIN1**2)*FACT1
              ED2=(VXIN2**2+VYIN2**2+VZIN2**2)*FACT2
              VXIN(IPLSV,IRAD)=VXMIX
              VYIN(IPLSV,IRAD)=VYMIX
              VZIN(IPLSV,IRAD)=VZMIX
              T1=(EDEN(IRAD)/(PDEN(IRAD)+EPS60)-ED1)/1.5
              T2=(EDEN2(IRAD)/(PDEN2(IRAD)+EPS60)-ED2)/1.5
              RM=2.D0*RMAS1*RMAS2/(RMAS1+RMAS2)**2
              TMIX=T1+RM*(T2-T1+
     .             FACT2/3.D0*((VXIN1-VXIN2)**2+(VYIN1-VYIN2)**2+
     .                         (VZIN1-VZIN2)**2))
              TIIN(IPLSTI,IRAD)=TMIX
              DEL=DIIN(IPLS,IRAD)-PDEN(IRAD)
              RATN=RATN+TBEL*DEL*VOL(IRAD)
              RESN=RESN+TBEL*ABS(DEL)*VOL(IRAD)
              DIIN(IPLS,IRAD)=PDEN(IRAD)
              RRN=RRN+PDEN(IRAD)*VOL(IRAD)
C             RRM=?
              RRE=RRE+EDEN(IRAD)*VOL(IRAD)
180     CONTINUE
c
c  same as do 180 loop , for additional cell region
c
        DO 190 IRAD=NSURF+1,NSURF+NRADD
C
          TBEL=0
          IF (LGVAC(IRAD,IPLS)) GOTO 191
          IF (NSTORDR >= NRAD) THEN
            TBEL = TABEL3(IREL,IRAD,1)
          ELSE
            KK=NREAEL(IREL)
            PLS=TIINL(IPLSTI,IRAD)+ADDEL(IREL,IPLS)
            TBEL = EIRENE_RATE_COEFF(KK,PLS,0._DP,.TRUE.,0,ERATE)
     .             *DIIN(IPLS,IRAD)*FACREA(KK,1)
          END IF
191       CONTINUE
C         EOLD=(1.5*TIIN(IPLSTI,IRAD)+EDRIFT(IPLS,IRAD))*
C    .          DIIN(IPLS,IRAD)
          EOLD=(1.5*TIIN(IPLSTI,IRAD)+EDRIFT(IPLS,IRAD))*
     .          PDEN(IRAD)
          DOLD=DIIN(IPLS,IRAD)
          DEL=EOLD-EDEN(IRAD)
          RATE=RATE+TBEL*DEL*VOL(IRAD)
          DELX=BGKV(IUP1,IRAD)-VXIN(IPLSV,IRAD)*DIIN(IPLS,IRAD)
          DELY=BGKV(IUP2,IRAD)-VYIN(IPLSV,IRAD)*DIIN(IPLS,IRAD)
          DELZ=BGKV(IUP3,IRAD)-VZIN(IPLSV,IRAD)*DIIN(IPLS,IRAD)
          RATM(1)=RATM(1)+TBEL*DELX*VOL(IRAD)
          RATM(2)=RATM(2)+TBEL*DELY*VOL(IRAD)
          RATM(3)=RATM(3)+TBEL*DELZ*VOL(IRAD)
C
          VXIN1=BGKV(IUP1 ,IRAD)/(PDEN (IRAD)+EPS60)
          VXIN2=BGKV(IUP12,IRAD)/(PDEN2(IRAD)+EPS60)
          VXMIX=(RMAS1*VXIN1+RMAS2*VXIN2)/(RMAS1+RMAS2)
          VYIN1=BGKV(IUP2 ,IRAD)/(PDEN (IRAD)+EPS60)
          VYIN2=BGKV(IUP22,IRAD)/(PDEN2(IRAD)+EPS60)
          VYMIX=(RMAS1*VYIN1+RMAS2*VYIN2)/(RMAS1+RMAS2)
          VZIN1=BGKV(IUP3 ,IRAD)/(PDEN (IRAD)+EPS60)
          VZIN2=BGKV(IUP32,IRAD)/(PDEN2(IRAD)+EPS60)
          VZMIX=(RMAS1*VZIN1+RMAS2*VZIN2)/(RMAS1+RMAS2)
          ED1=(VXIN1**2+VYIN1**2+VZIN1**2)*FACT1
          ED2=(VXIN2**2+VYIN2**2+VZIN2**2)*FACT2
          VXIN(IPLSV,IRAD)=VXMIX
          VYIN(IPLSV,IRAD)=VYMIX
          VZIN(IPLSV,IRAD)=VZMIX
          T1=(EDEN(IRAD)/(PDEN(IRAD)+EPS60)-ED1)/1.5
          T2=(EDEN2(IRAD)/(PDEN2(IRAD)+EPS60)-ED2)/1.5
          RM=2.D0*RMAS1*RMAS2/(RMAS1+RMAS2)**2
          TMIX=T1+RM*(T2-T1+
     .         FACT2/3.D0*((VXIN1-VXIN2)**2+(VYIN1-VYIN2)**2+
     .                     (VZIN1-VZIN2)**2))
          TIIN(IPLSTI,IRAD)=TMIX
          DEL=DIIN(IPLS,IRAD)-PDEN(IRAD)
          RATN=RATN+TBEL*DEL*VOL(IRAD)
          RESN=RESN+TBEL*ABS(DEL)*VOL(IRAD)
          DIIN(IPLS,IRAD)=PDEN(IRAD)
          RRN=RRN+PDEN(IRAD)*VOL(IRAD)
C         RRM=?
          RRE=RRE+EDEN(IRAD)*VOL(IRAD)
C
190     CONTINUE
c
        ENDIF
c
        CALL EIRENE_LEER(2)
        WRITE (iunout,*) 'PARTICLE, MOMENTUM AND ENERGY EXCHANGE RATES '
        CALL EIRENE_LEER(1)
        WRITE (iunout,'(1X,A8,1X,1PE12.4)') 'RATN=   ',RATN*ELCHA
        WRITE (iunout,'(1X,A8,1X,3(1PE12.4))') 'RATM=   ',
     .                    RATM(1)*ELCHA*CNDYN,
     .                    RATM(2)*ELCHA*CNDYN,RATM(3)*ELCHA*CNDYN
        WRITE (iunout,'(1X,A8,1X,1PE12.4)') 'RATE=   ',RATE*ELCHA
        CALL EIRENE_LEER(2)
        WRITE (iunout,*) 'RESIDUA (1/SEC)'
        CALL EIRENE_LEER(1)
        CALL EIRENE_MASR1('RESN=   ',RESN/(RRN+EPS60))
C       CALL EIRENE_MASR3('RESM=                   ',RATM(1)/(RRM+EPS60),
C    .                   ,RATM(2)/(RRM+EPS60),RATM(3)/(RRM+EPS60))
        CALL EIRENE_MASR1('RESE=   ',RESE/(RRE+EPS60))
        CALL EIRENE_LEER(2)
C
C.................................................................
1000  CONTINUE
C.................................................................

      CALL EIRENE_LEER(2)
C
C  SAVE OVERHEAD, IF GEOMETRY DATA ALREADY AVAILABLE ON FILE
C
      IF (NFILEM.EQ.1) NFILEM=2
C
C  SET INDPRO=7, AND
C  WRITE PLASMA DATA ONTO PLASMA_BCKGRND FOR CALL TO SUBR. PLASMA BELOW
C  TI,NI AND (VX,VY,VZ) FOR IPLS=1,NPLSI
C  PLAY SAVE: WRITE WHOLE PLASMA_BCKGRND ARRAY.
C
      DO 500 I=1,5
        INDPRO(I)=7
500   CONTINUE
      NRWK1=6+5*NPLS+NAIN
      IF (NIDV < NRWK1) THEN
        WRITE (iunout,*) ' PLASMA_BCKGRND-ARRAY IS TOO SMALL TO HOLD '
        WRITE (iunout,*) ' PLASMA-DATA '
        WRITE (iunout,*) ' CHECK PARAMETER NSMSTRA '
        CALL EIRENE_EXIT_OWN(1)
      END IF
      CALL EIRENE_ALLOC_BCKGRND
      PLASMA_BCKGRND(1:NRWK1,:) = 0.D0
!pb initialize BZIN=1
      PLASMA_BCKGRND(3+1*NPLS+NPLSTI+3*NPLSV+1,:)= 1._DP
      DO 550 IR=1,NXM
        DO 550 IP=1,NYM
          DO 550 IT=1,NZM
            IRAD=IR + ((IP-1)+(IT-1)*NP2T3)*NR1P2 + NBLCKA
            PLASMA_BCKGRND  (0+0*NPLS+1,IRAD)= TEIN(IRAD)
            DO IPLSTI=1,NPLSTI
              PLASMA_BCKGRND(1+0*NPLS+IPLSTI,IRAD)= TIIN(IPLSTI,IRAD)
            END DO
            DO 520 IPLS=1,NPLSI
              PLASMA_BCKGRND(1+0*NPLS+NPLSTI+IPLS,IRAD)= DIIN(IPLS,IRAD)
520         CONTINUE
            DO IPLSV=1,NPLSV
              PLASMA_BCKGRND(1+1*NPLS+NPLSTI+0*NPLSV+IPLSV,IRAD)=
     .               VXIN(IPLSV,IRAD)
              PLASMA_BCKGRND(1+1*NPLS+NPLSTI+1*NPLSV+IPLSV,IRAD)=
     .               VYIN(IPLSV,IRAD)
              PLASMA_BCKGRND(1+1*NPLS+NPLSTI+2*NPLSV+IPLSV,IRAD)=
     .               VZIN(IPLSV,IRAD)
            END DO
            PLASMA_BCKGRND(1+1*NPLS+NPLSTI+3*NPLSV+1,IRAD)= BXIN(IRAD)
            PLASMA_BCKGRND(2+1*NPLS+NPLSTI+3*NPLSV+1,IRAD)= BYIN(IRAD)
            PLASMA_BCKGRND(3+1*NPLS+NPLSTI+3*NPLSV+1,IRAD)= BZIN(IRAD)
            PLASMA_BCKGRND(4+1*NPLS+NPLSTI+3*NPLSV+1,IRAD)= BFIN(IRAD)
            PLASMA_BCKGRND(5+1*NPLS+NPLSTI+3*NPLSV+1,IRAD)= VOL(IRAD)
            DO 530 IAIN=1,NAINI
              PLASMA_BCKGRND(6+1*NPLS+NPLSTI+3*NPLSV+IAIN,IRAD)=
     .               ADIN(IAIN,IRAD)
530         CONTINUE
550   CONTINUE
C
c  same as do 550 loop , for additional cell region
c
      DO 570 IRAD=NSURF+1,NSURF+NRADD
        PLASMA_BCKGRND  (0+0*NPLS+1   ,IRAD)= TEIN(IRAD)
            DO IPLSTI=1,NPLSTI
              PLASMA_BCKGRND(1+0*NPLS+IPLSTI,IRAD)= TIIN(IPLSTI,IRAD)
            END DO
            DO 560 IPLS=1,NPLSI
              PLASMA_BCKGRND(1+0*NPLS+NPLSTI+IPLS,IRAD)= DIIN(IPLS,IRAD)
560         CONTINUE
            DO IPLSV=1,NPLSV
              PLASMA_BCKGRND(1+1*NPLS+NPLSTI+0*NPLSV+IPLSV,IRAD)=
     .               VXIN(IPLSV,IRAD)
              PLASMA_BCKGRND(1+1*NPLS+NPLSTI+1*NPLSV+IPLSV,IRAD)=
     .               VYIN(IPLSV,IRAD)
              PLASMA_BCKGRND(1+1*NPLS+NPLSTI+2*NPLSV+IPLSV,IRAD)=
     .               VZIN(IPLSV,IRAD)
            END DO
            PLASMA_BCKGRND(1+1*NPLS+NPLSTI+3*NPLSV+1,IRAD)= BXIN(IRAD)
            PLASMA_BCKGRND(2+1*NPLS+NPLSTI+3*NPLSV+1,IRAD)= BYIN(IRAD)
            PLASMA_BCKGRND(3+1*NPLS+NPLSTI+3*NPLSV+1,IRAD)= BZIN(IRAD)
            PLASMA_BCKGRND(4+1*NPLS+NPLSTI+3*NPLSV+1,IRAD)= BFIN(IRAD)
            PLASMA_BCKGRND(5+1*NPLS+NPLSTI+3*NPLSV+1,IRAD)= VOL(IRAD)
            DO 565 IAIN=1,NAINI
              PLASMA_BCKGRND(6+1*NPLS+NPLSTI+3*NPLSV+IAIN,IRAD)=
     .               ADIN(IAIN,IRAD)
565         CONTINUE
570   CONTINUE
C
      CALL EIRENE_PLASMA_DERIV(0)
C
600   CONTINUE
C
C .........................................................................
C  NOW: NEW COLLISION RATES MUST BE SET FOR THE NEXT ITERATION
C .........................................................................
C
C  IN CASE OF CROSS COLLISION, SOME MODIFICATIONS ON THE
C  BACKGROUND PARAMETERS ARE REQUIRED TEMPORARYLY TO ENFORCE
C  A SPECIFIC RELATION BETWEEN TAU_1,2 AND TAU_2,1
C
C  COMPUTE SOME 'DERIVED' PLASMA DATA PROFILES FROM THE PROFILES
C  E.G.: EDRIFT, NEEDED FOR EPLEL3
C
C
      TRCSAV=TRCAMD
      TRCAMD=.FALSE.
C
      CALL EIRENE_LEER(1)
      DO IPLS=1,NPLSI
        LMARK(IPLS)=.FALSE.
      ENDDO
      DO IPLS1=1,NPLSI
        IF (NPBGKP(IPLS1,2).NE.0) THEN
C  IPLS1 IS A CROSS COLLISION TALLY
C  FIND CORRESPONDING 2ND CROSS COLLISION TALLY
          IPLS2=0
          DO IPLS=1,NPLSI
            IF (ITYP1(IPLS).EQ.ITYP2(IPLS1).AND.
     .          ISPZ1(IPLS).EQ.ISPZ2(IPLS1).AND.
     .          ITYP2(IPLS).EQ.ITYP1(IPLS1).AND.
     .          ISPZ2(IPLS).EQ.ISPZ1(IPLS1)) IPLS2=IPLS
          ENDDO
          IF (IPLS2.EQ.0) GOTO 800
          CALL EIRENE_LEER(1)
          WRITE (iunout,*) 'CORRESPONDING CROSS COLLISION SPECIES '
          WRITE (iunout,*) 'IPLS1,IPLS2 ',IPLS1,IPLS2
          IF (LMARK(IPLS1).OR.LMARK(IPLS2)) GOTO 800
C  IPLS2 IS THE SECOND CROSS COLLISION TALLY
          WRITE (iunout,*) 
     .      'MODIFY PARAMETERS FOR CROSS COLLISIONALITIES '
          WRITE (iunout,*) 'IPLS1,IPLS2 ',IPLS1,IPLS2
          CALL EIRENE_LEER(1)
          LMARK(IPLS1)=.TRUE.
          LMARK(IPLS2)=.TRUE.
          IPLSTI1 = MPLSTI(IPLS1)
          IPLSTI2 = MPLSTI(IPLS2)
          DO IRAD=1,NSBOX
            DS1=DIIN(IPLS1,IRAD)
            DIIN(IPLS1,IRAD)=DIIN(IPLS2,IRAD)
            DIIN(IPLS2,IRAD)=DS1
C
            ENERGY(IPLS1,IRAD)=1.5*TIIN(IPLSTI1,IRAD)+EDRIFT(IPLS1,IRAD)
            ENERGY(IPLS2,IRAD)=1.5*TIIN(IPLSTI2,IRAD)+EDRIFT(IPLS2,IRAD)
C
            TS1=0.5*(TIIN(IPLSTI1,IRAD)+TIIN(IPLSTI2,IRAD))
            TIIN(IPLSTI1,IRAD)=TS1
            TIIN(IPLSTI2,IRAD)=TS1
          ENDDO
800       CONTINUE
        ENDIF
      ENDDO
      CALL EIRENE_LEER(2)
C
C
C  COMPUTE SOME 'DERIVED' PLASMA DATA PROFILES FROM THE MODIFIED PROFILES
C
      CALL EIRENE_PLASMA_DERIV(0)
C
C  RESET BGK-ATOMIC AND MOLECULAR DATA ARRAYS
C
      DO IPLS=1,NPLSI
        IF (NPBGKP(IPLS,1).NE.0) THEN
          ITYP=ITYP1(IPLS)
          ISPZ=ISPZ1(IPLS)
          IREL=IREL1(IPLS)
          NRC=INRC1(IPLS)
          IF (ITYP.EQ.1) THEN
            ISP=NSPH+ISPZ
            KK=IREACA(ISPZ,NRC)
            EBULK=EBULKA(ISPZ,NRC)
            ISCDE=ISCDEA(ISPZ,NRC)
            IESTM=IESTMA(ISPZ,NRC)
            FACTKK=FREACA(ISPZ,NRC)
          ELSEIF (ITYP.EQ.2) THEN
            ISP=NSPA+ISPZ
            KK=IREACM(ISPZ,NRC)
            EBULK=EBULKM(ISPZ,NRC)
            ISCDE=ISCDEM(ISPZ,NRC)
            IESTM=IESTMM(ISPZ,NRC)
            FACTKK=FREACM(ISPZ,NRC)
          ELSEIF (ITYP.EQ.3) THEN
            ISP=NSPAM+ISPZ
            KK=IREACI(ISPZ,NRC)
            EBULK=EBULKI(ISPZ,NRC)
            ISCDE=ISCDEI(ISPZ,NRC)
            IESTM=IESTMI(ISPZ,NRC)
            FACTKK=FREACI(ISPZ,NRC)
          ENDIF
          IF (FACTKK.EQ.0.D0) FACTKK=1.D0
C  BGK COLLISION, RESET TABEL3, EPLEL3
          CALL EIRENE_XSTEL(IREL,ISP,IPLS,EBULK,ISCDE,IESTM,KK,FACTKK)
          IF (NPBGKP(IPLS,2).NE.0) THEN
C  CROSS COLLISION, RESET EPLEL3 FOR TRACKLENGTH ESTIMATOR
            IF (NSTORDR >= NRAD) THEN
              DO J=1,NSBOX
                EPLEL3(IREL,J,1)=ENERGY(IPLS,J)
              ENDDO
            ELSE
              NELREL(IREL)=-3
            END IF
          ENDIF
        ENDIF
      ENDDO
C
      TRCAMD=TRCSAV
C
C
C  RESTOR PLASMA DATA FROM PLASMA_BCKGRND ARRAY
C
      CALL EIRENE_PLASMA
      CALL EIRENE_PLASMA_DERIV(0)
C
C  SAVE PLASMA DATA AND ATOMIC DATA ON FORT.13
C
      NFILEL=3

      CALL EIRENE_WRPLAM(TRCFLE,0)
C
      DEALLOCATE (PDEN)
      DEALLOCATE (EDEN)
      DEALLOCATE (PDEN2)
      DEALLOCATE (EDEN2)
      DEALLOCATE (ENERGY)
C
      RETURN
C
995   CONTINUE
      WRITE (iunout,*) 'SPECIES ERROR IN MODBGK'
      CALL EIRENE_EXIT_OWN(1)
C
999   CONTINUE
      WRITE (iunout,*) 'ERROR IN MODBGK. IPLS,IBGK= ',IPLS,IBGK1,IBGK2
      CALL EIRENE_EXIT_OWN(1)
      END
