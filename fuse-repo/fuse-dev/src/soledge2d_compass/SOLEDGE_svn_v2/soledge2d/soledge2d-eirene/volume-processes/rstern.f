C
C
      FUNCTION EIRENE_RSTERN (ER,B,IFLAG,P)
C
C     IFLAG=1:  H+  + H, PURELY REPULSIVE POTENTIAL
C     IFLAG=2:  MORSE POTENTIAL, He+ + He, H+ + Noble Gases, H+ + H2
C
C               PARAMETERS: P(1):
C                           P(2):
C                           P(3):
C
C  RSTERN(ER,B) IS THE LARGEST ROOT OF THE EQUATION:
C
C     FI(R):=1.-V(R)/ER-(B/R)**2=0.
C
C   HERE:  ER COLLISION ENERGY (EV)
C          B  IMPACT PARAMETER
C          V  INTERACTION POTENTIAL (EV)
C
C     DIMENSION RV0(3),RVM(3),RVW(3),VM(3),VW(3),VSW(3)
 
      USE EIRMOD_PRECISION
      USE EIRMOD_COMPRT, ONLY: IUNOUT
 
      IMPLICIT NONE
 
      REAL(DP), INTENT(IN) :: ER, B, P(*)
      INTEGER, INTENT(IN) :: IFLAG
 
      REAL(DP) :: FIW, RV, EIRENE_RTSAF, BQ, TOL, EIRENE_RSTERN, 
     .            EIRENE_FI, DFI,
     .        FITEST, rup, rlw, vw, rw, r0
      SAVE
C     DATA IFIRST /0/
C     DATA RV0/0.,0.99699,2.18039/
C     DATA RVM/0.,1.4556 ,2.835539/
C     DATA RVW/0.,1.99515,3.490687/
C     DATA VM /0.,-2.     ,-2.7/
C     DATA VW /0.,-1.5    ,-2.025/
C     DATA VSW/0.,1.284688,1.4283/
C
C
      IF (IFLAG.EQ.1) THEN
C  FIND UPPER AND LOWER BOUND FOR NUMERICAL ROOT FINDER RTSAF
C  FOR IFLAG=1, RLW=B IS A LOWER BOUND AND RUP=INF IS AN UPPER BOUND.
C  FIRST TRY TO FIND BETTER BOUNDS:
        RUP=B
C
10      RLW=RUP
        RUP=RLW*2.
        FITEST=EIRENE_FI(RUP,ER,B,IFLAG,P,DFI)
        IF (FITEST.LT.0.D0) GOTO 10
C
        TOL=1.D-7*(RUP-RLW)
        EIRENE_RSTERN=EIRENE_RTSAF(RLW,RUP,TOL,ER,B,IFLAG,P)
C
      ELSEIF (IFLAG.EQ.2) THEN
C  GENERALISED MORSE POTENTIAL
C  FIND UPPER AND LOWER BOUND FOR NUMERICAL ROOT FINDER RTSAFE
C  FOR IFLAG=2 AND IFLAG=3, THE FOLLOWING INTERVAL CAN BE USED:
C  R0 IS THE ROOT OF THE INTERACTION POTENTIAL V(R): V(R0)=0.
C  RM IS THE RADIUS OF THE MINIMUM OF V(R)           V(RM)= VM = -EPS
C  RW IS THE RADIUS OF THE POINT OF INFLECTION       V(RM)= VW
C        RM=P(4)
         R0=P(5)
         RW=P(6)
         VW=P(8)
C
        BQ=B*B
        RV=R0
C
C  CASE 1: B < R0,
C
        IF (RV.GT.B) THEN
          RLW=B
          RUP=RV
        ENDIF
C
C  CASE 2: R0 < B,
C
        IF (RV.LT.B) THEN
          RLW=RV
          RUP=B
C  TRY TO IMPROVE LOWER BOUND RLW
          FIW=1.-VW/ER-BQ/(RW**2)
          IF (FIW.LT.0.D0) RLW=RW
        ENDIF
C
        TOL=1.D-7*(RUP-RLW)
        EIRENE_RSTERN=EIRENE_RTSAF(RLW,RUP,TOL,ER,B,IFLAG,P)
      ELSE
        GOTO 990
      ENDIF
C
      RETURN
990   CONTINUE
      WRITE (iunout,*) 'ERROR IN FUNCTION EIRENE_RSTERN '
      CALL EIRENE_EXIT_OWN(1)
      END
