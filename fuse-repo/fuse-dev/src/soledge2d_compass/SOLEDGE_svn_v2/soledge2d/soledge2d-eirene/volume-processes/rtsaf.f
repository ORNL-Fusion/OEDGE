C
C
      FUNCTION EIRENE_RTSAF(X1,X2,XACC,ER,B,IFLAG,P)
 
C  TAKEN FROM: NUMERICAL RECIPES, W.H.PRESS ET AL.,
C              CAMBRIDGE UNIV. PRESS, 1989, P258
C  MODIFIED, TO FIND LARGEST ROOT, IN CASE MORE THAN ONE ROOTS
C  AND TO SPEED UP
 
      USE EIRMOD_PRECISION
      USE EIRMOD_COMPRT, ONLY: IUNOUT
 
      IMPLICIT NONE
 
      REAL(DP), INTENT(IN) :: X1, X2, XACC, ER, B, P(*)
      INTEGER, INTENT(IN) :: IFLAG
      INTEGER, PARAMETER :: MAXIT=100
      REAL(DP) :: DF, XDIST, F, EIRENE_FI, TEMP, DX, DXOLD, XH,
     .            EIRENE_RTSAF, XL
      INTEGER :: J
 
      XL=X1
      XH=X2
C
      F=EIRENE_FI(XH,ER,B,IFLAG,P,DF)
      XDIST=(XH-XL)
      DX=MIN(XDIST,F/DF)
      DXOLD=DX
      EIRENE_RTSAF=XH-DX
C
      F=EIRENE_FI(EIRENE_RTSAF,ER,B,IFLAG,P,DF)
 
      IF(F.LT.0.D0) THEN
        XL=EIRENE_RTSAF
      ELSE
        XH=EIRENE_RTSAF
      ENDIF
C
      DO 11 J=1,MAXIT
        IF(((EIRENE_RTSAF-XH)*DF-F)*((EIRENE_RTSAF-XL)*DF-F).GT.0.
     *      .OR. ABS(F+F).GT.ABS(DXOLD*DF) ) THEN
          DXOLD=DX
          DX=0.5*(XH-XL)
          EIRENE_RTSAF=XL+DX
        ELSE
          DXOLD=DX
          DX=F/DF
          TEMP=EIRENE_RTSAF
          EIRENE_RTSAF=EIRENE_RTSAF-DX
        ENDIF
C
        IF(ABS(DX).LT.XACC) RETURN
C
        F=EIRENE_FI(EIRENE_RTSAF,ER,B,IFLAG,P,DF)
        IF(F.LT.0.D0) THEN
          XL=EIRENE_RTSAF
        ELSE
          XH=EIRENE_RTSAF
        ENDIF
11    CONTINUE
      WRITE (iunout,*) 'RTSAF EXCEEDING MAXIMUM ITERATIONS'
      RETURN
      END
