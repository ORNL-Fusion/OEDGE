!pb  30.08.06:  data structure for reaction data redefined
!pb  12.10.06:  modcol revised
!pb  22.11.06:  flag for shift of first parameter to rate_coeff introduced
!pb  28.11.06:  initialization of XSTOR reactivated because of trouble in
!pb             BGK iteration
!pb  22.03.07:  PI reactions revised
C
C
      FUNCTION EIRENE_FPATHM (K,CFLAG,JCOU,NCOU)
C
C   CALCULATE MEAN FREE PATH AND REACTION RATES FOR NEUTRAL
C   "BEAM MOLECULES" OF VELOCITY VEL IN DRIFTING MAXWELLIAN PLASMA-BACKGROUND
C   IN CELL K
C   CFLAG:  AS IN FUNCTION FPATHA
C
      USE EIRMOD_PRECISION
      USE EIRMOD_PARMMOD
      USE EIRMOD_COMUSR
      USE EIRMOD_CCONA
      USE EIRMOD_CLOGAU
      USE EIRMOD_CINIT
      USE EIRMOD_CZT1
      USE EIRMOD_COMPRT
      USE EIRMOD_COMXS
      USE EIRMOD_CTRCEI
 
      IMPLICIT NONE
 
      REAL(DP), INTENT(OUT) :: CFLAG(7,3)
      INTEGER, INTENT(IN) :: K, JCOU,NCOU
 
      REAL(DP) :: DENIO(NPLS), ZTI(NPLS)
      REAL(DP) :: PVELQ(NPLSV)
      REAL(DP) :: TBPI3(9), TBCX3(9), TBEL3(9), FP(6)
      REAL(DP) :: EPPI3(9), EPCX3(9), EPEL3(9)
      REAL(DP) :: VREL, VRELQ, TBEL, EIRENE_FEPLCX3, EIRENE_CROSS, 
     .          EXPO, ELB, CEL,
     .          RMN, RLMS, ER, RMI, RMSI, SIG,
     .          SIGMAX, EIRENE_FEPLEL3, VX, VY, VZ, EHEAVY, 
     .          EIRENE_FTABEI1, EIRENE_FPATHM,
     .          PVELQ0, DENEL, VEFF, VEFFQ, CXS, ELAB,
     .          TBCX, PLS, EIRENE_RATE_COEFF, EIRENE_SNGL_POLY, RCMIN, 
     .          RCMAX, ELTHDUM, CTCHDUM,
     .          EIRENE_FEELEI1, EIRENE_FEELPI1,
     .          EIRENE_FEHVDS1, EIRENE_FEHVPI3,
     .          EIRENE_ENERGY_RATE_COEFF, EIRENE_FTABPI3, CII, 
     .          EIRENE_FEPLPI3, ERATE
      INTEGER :: IBGK, IREL, IMEL, J, JAN, IF8, IML, IPL, IMDS,
     .           II, IREAC, IMCX, IMPI, KK, IRCX, I1, I2, IPLSTI, IPLSV,
     .           IREI, IRPI
C
C
C  SET DEFAULTS: NO REACTIONS
C
      XSTORV=0.D0
!pb      IF (NCOU.GT.1) THEN
        XSTOR=0.D0
!pb      ENDIF
      EIRENE_FPATHM=1.D10
      SIGMAX=0.D0
C
      IF (LGVAC(K,0)) RETURN
C
C   LOCAL PLASMA PARAMETERS
C
      DENEL=DEIN(K)
      DO 2 IPLS=1,NPLSI
        ZTI(IPLS)=ZT1(IPLS,K)
2       DENIO(IPLS)=DIIN(IPLS,K)
      PVELQ0=VEL*VEL
      DO 3 IPLS=1,NPLSV
        IF (NLDRFT) THEN
          IF (INDPRO(4) == 8) THEN
            CALL EIRENE_VECUSR (2,VX,VY,VZ,IPLS)
          ELSE
            VX=VXIN(IPLS,K)
            VY=VYIN(IPLS,K)
            VZ=VZIN(IPLS,K)
          END IF
          PVELQ(IPLS)=(VELX*VEL-VX)**2+
     .                (VELY*VEL-VY)**2+
     .                (VELZ*VEL-VZ)**2
        ELSE
          PVELQ(IPLS)=PVELQ0
        ENDIF
3     CONTINUE
C
C
C  ELECTRON IMPACT RATE COEFFICIENT
C
      IF (LGMEI(IMOL,0).EQ.0.OR.LGVAC(K,NPLS+1)) GOTO 30
      DO 10 IMDS=1,NMDSI(IMOL)
        IREI=LGMEI(IMOL,IMDS)
        IF (MODCOL(1,2,IREI).EQ.1) THEN
          IF (NSTORDR >= NRAD) THEN
            SIGVEI(IREI)=TABDS1(IREI,K)
          ELSE
            SIGVEI(IREI)=EIRENE_FTABEI1(IREI,K)
          END IF
        ELSE
          GOTO 990
        ENDIF
C
        IF (NSTORDR >= NRAD) THEN
          ESIGEI(IREI,5)=EELDS1(IREI,K)
          EHEAVY=EHVDS1(IREI,K)
        ELSE
          ESIGEI(IREI,5)=EIRENE_FEELEI1(IREI,K)
          EHEAVY=EIRENE_FEHVDS1(IREI,K)
        ENDIF
C
        ESIGEI(IREI,1)=EATDS(IREI,0,1)*E0+EATDS(IREI,0,2)*EHEAVY
        ESIGEI(IREI,2)=EMLDS(IREI,0,1)*E0+EMLDS(IREI,0,2)*EHEAVY
        ESIGEI(IREI,3)=EIODS(IREI,0,1)*E0+EIODS(IREI,0,2)*EHEAVY
        ESIGEI(IREI,4)=EPLDS(IREI,  1)*E0+EPLDS(IREI,  2)*EHEAVY
C
        SIGMAX=MAX(SIGMAX,SIGVEI(IREI))
        SIGEIT=SIGEIT+SIGVEI(IREI)
10    CONTINUE
C
C  GENERAL ION IMPACT ON MOLECULE IMOL, BULK ION SPEZIES IPLS=1,NPLSI
C  30--->40
C
30    IF (LGMPI(IMOL,0,0).EQ.0) GOTO 40
      DO 36 IMPI=1,NMPII(IMOL)
        IRPI=LGMPI(IMOL,IMPI,0)
        IPLS=LGMPI(IMOL,IMPI,1)
        IPLSV=MPLSV(IPLS)
        IPLSTI=MPLSTI(IPLS)
        IF (LGVAC(K,IPLS)) GOTO 36
C
C  1.) RATE COEFFICIENT
C
        IF (MODCOL(4,2,IRPI).EQ.1) THEN
C  MAXWELL
          IF (NSTORDR >= NRAD) THEN
            SIGVPI(IRPI)=TABPI3(IRPI,K,1)
          ELSE
CDR  SIGVPI(IRPI)=EIRENE_FTABPI3 : NOT READY
!pb            KK=NREAPI(IRPI)
!pb            PLS=TIINL(IPLSTI,K)+ADDPI(IRPI,IPLS)
!pb            TBPI = EIRENE_RATE_COEFF(KK,PLS,0._DP,.TRUE.,0,ERATE)*DIIN(IPLS,K)
!pb            SIGVPI(IRPI)=TBPI
            SIGVPI(IRPI)=EIRENE_FTABPI3(IRPI,K)
          END IF
        ELSEIF (MODCOL(4,2,IRPI).EQ.2) THEN
C  BEAM - MAXWELL
C
C  MINIMUM PROJECTILE ENERGY: 0.1 EV
          ELB=MAX(-2.3_DP,LOG(PVELQ(IPLSV))+EEFPI(IRPI))
          IF (NSTORDR >= NRAD) THEN
            TBPI3(1:NSTORDT) = TABPI3(IRPI,K,1:NSTORDT)
            FP = 0._DP
            RCMIN = -HUGE(1._DP)
            RCMAX = HUGE(1._DP)
            EXPO = EIRENE_SNGL_POLY(TBPI3,ELB,RCMIN,RCMAX,FP,0,0)
          ELSE
! CALCULATE RATE-COEFFICIENT
            KK=NREAPI(IRPI)
            PLS=TIINL(IPLSTI,K)+ADDPI(IRPI,IPLS)
            EXPO = EIRENE_RATE_COEFF(KK,PLS,ELB,.FALSE.,0,ERATE)
     .             + DIINL(IPLS,K) + FACREA(KK,2)
          END IF
          SIGVPI(IRPI)=EXP(EXPO)
        ELSEIF (MODCOL(4,2,IRPI).EQ.3) THEN
C  BEAM - BEAM
          VRELQ=ZTI(IPLS)+PVELQ(IPLSV)
          VREL=SQRT(VRELQ)
          ELAB=LOG(VRELQ)+DEFPI(IRPI)
          IREAC=MODCOL(4,1,IRPI)
          CII=EIRENE_CROSS(ELAB,IREAC,IRPI,'FPATHA II')
          SIGVPI(IRPI)=CII*VREL*DENIO(IPLS)
        ELSE
          GOTO 991
        ENDIF
C
C  2.A ELECTRON ENERGY LOSS / COLLISION (EV)
C
        IF (NSTORDR >= NRAD) THEN
          ESIGPI(IRPI,5)=EELPI1(IRPI,K)
          EHEAVY=EHVPI3(IRPI,K,1)
        ELSE
          ESIGEI(IREI,5)=EIRENE_FEELPI1(IRPI,K)
          EHEAVY=EIRENE_FEHVPI3(IRPI,K)
        ENDIF
C
        ESIGPI(IRPI,1)=EATPI(IRPI,0,1)*E0+EATPI(IRPI,0,2)*EHEAVY
        ESIGPI(IRPI,2)=EMLPI(IRPI,0,1)*E0+EMLPI(IRPI,0,2)*EHEAVY
        ESIGPI(IRPI,3)=EIOPI(IRPI,0,1)*E0+EIOPI(IRPI,0,2)*EHEAVY
        ESIGPI(IRPI,4)=EPLPI(IRPI,  1)*E0+EPLPI(IRPI,  2)*EHEAVY
C
        SIGMAX=MAX(SIGMAX,SIGVPI(IRPI))
        SIGPIT=SIGPIT+SIGVPI(IRPI)
C
C  2.B BULK ION ENERGY LOSS / COLLISION (EV)
C
cdr     IF (NSTORDR >= NRAD) THEN
cdr       ESIGPI(IRPI,4)=EPLPI3(IRPI,K,1)
cdr     ELSE
cdr       ESIGPI(IRPI,4)=EIRENE_FEPLPI3(IRPI,K)
cdr     END IF
        CFLAG(4,1)=2
36    CONTINUE
C
C  CHARGE EXCHANGE RATE COEFFICIENT FOR MOLECULE IMOL
C  WITH BULK IONS OF SPEZIES IPLS=1,NPLSI
C  40--->50
C
40    CONTINUE
      IF (LGMCX(IMOL,0,0).EQ.0.OR.LGVAC(K,0)) GOTO 50
      DO 41 IMCX=1,NMCXI(IMOL)
        IRCX=LGMCX(IMOL,IMCX,0)
        IPLS=LGMCX(IMOL,IMCX,1)
        IPLSTI=MPLSTI(IPLS)
        IPLSV=MPLSV(IPLS)
        IF (LGVAC(K,IPLS)) GOTO 41
C
C  1.) RATE COEFFICIENT
C
        IF (MODCOL(3,2,IRCX).EQ.1) THEN
C  MODEL 1:
C  MAXWELLIAN RATE, IGNORE NEUTRAL VELOCITY
          IF (NSTORDR >= NRAD) THEN
            SIGVCX(IRCX)=TABCX3(IRCX,K,1)
          ELSE
CDR  SIGVCX(IRCX)=FTABCX3 : NOT READY
            KK=NREACX(IRCX)
            PLS=TIINL(IPLSTI,K)+ADDCX(IRCX,IPLS)
            TBCX = EIRENE_RATE_COEFF(KK,PLS,0._DP,.TRUE.,0,ERATE)*
     .             DIIN(IPLS,K)
            SIGVCX(IRCX)=TBCX
          END IF
        ELSEIF (MODCOL(3,2,IRCX).EQ.2) THEN
C  MODEL 2:
C  BEAM - MAXWELLIAN RATE
          IF (TIIN(IPLSTI,K).LT.TVAC) THEN
C     HERE: T_I IS SO LOW, THAT ALL ION ENERGY IS IN DRIFT MOTION.
C           HENCE: USE BEAM-BEAM RATE INSTEAD.
            VRELQ=PVELQ(IPLSV)
            VREL=SQRT(VRELQ)
            ELAB=LOG(VRELQ)+DEFCX(IRCX)
            IREAC=MODCOL(3,1,IRCX)
            CXS=EIRENE_CROSS(ELAB,IREAC,IRCX,'FPATHM CX')
            SIGVCX(IRCX)=CXS*VREL*DENIO(IPLS)
          ELSE
C  MINIMUM PROJECTILE ENERGY: 0.1 EV
            ELB=MAX(-2.3_DP,LOG(PVELQ(IPLSV))+EEFCX(IRCX))
            IF (NSTORDR >= NRAD) THEN
! DOUBLE POLYNOMIAL FIT REDUCED TO SINGLE POLYNOMIAL FIT BY
! PRECALCULATING TEMPERATURE DEPENDENCIES
              TBCX3(1:NSTORDT) = TABCX3(IRCX,K,1:NSTORDT)
              FP = 0._DP
              RCMIN = -HUGE(1._DP)
              RCMAX = HUGE(1._DP)
              EXPO = EIRENE_SNGL_POLY(TBCX3,ELB,RCMIN,RCMAX,FP,0,0)
            ELSE
! CALCULATE RATE-COEFFICIENT
              KK=NREACX(IRCX)
              PLS=TIINL(IPLSTI,K)+ADDCX(IRCX,IPLS)
              EXPO = EIRENE_RATE_COEFF(KK,PLS,ELB,.FALSE.,0,ERATE)
     .               + DIINL(IPLS,K) + FACREA(KK,2)
            END IF
            SIGVCX(IRCX)=EXP(EXPO)
          ENDIF
        ELSEIF (MODCOL(3,2,IRCX).EQ.3) THEN
C  MODEL 3:
C  BEAM - BEAM RATE, BUT WITH EFFECTIVE INTERACTION ENERGY
          VEFFQ=ZTI(IPLS)+PVELQ(IPLSV)
          VEFF=SQRT(VEFFQ)
          ELAB=LOG(VEFFQ)+DEFCX(IRCX)
          IREAC=MODCOL(3,1,IRCX)
          CXS=EIRENE_CROSS(ELAB,IREAC,IRCX,'FPATHM CX')
          SIGVCX(IRCX)=CXS*VEFF*DENIO(IPLS)
        ELSE
          GOTO 992
        ENDIF
C
        SIGMAX=MAX(SIGMAX,SIGVCX(IRCX))
        SIGCXT=SIGCXT+SIGVCX(IRCX)
C
C  2.) BULK ION ENERGY LOSS RATE:
C
        IF (MODCOL(3,4,IRCX).EQ.1) THEN
C  MODEL 1:
C  MEAN ENERGY FROM DRIFTING MAXWELLIAN
C  (ONLY NEEDED FOR TRACKLENGTH ESTIMATOR)
C  ION SAMPLING FROM MAXWELLIAN
          IF (NSTORDR >= NRAD) THEN
            ESIGCX(IRCX,1)=EPLCX3(IRCX,K,1)
          ELSE
            ESIGCX(IRCX,1)=EIRENE_FEPLCX3(IRCX,K)
          END IF
          CFLAG(3,1)=2
        ELSEIF (MODCOL(3,4,IRCX).EQ.2) THEN
C  MODEL 2:
C  MEAN ENERGY FROM EIRENE_CROSS SECTION WEIGHTED DRIFTING MAXWELLIAN
C  (ONLY NEEDED FOR TRACKLENGTH ESTIMATOR)
C  ION SAMPLING FROM WEIGHTED DRIFTING MAXWELLIAN (E.G., BY REJECTION)
          IF (IESTCX(IRCX,3).EQ.0) THEN  ! for tracklength estimator only
C  MINIMUM PROJECTILE ENERGY: 0.1 EV
            ELB=MAX(-2.3_DP,LOG(PVELQ(IPLSV))+EEFCX(IRCX))
            IF (NSTORDR >= NRAD) THEN
! DOUBLE POLYNOMIAL FIT REDUCED TO SINGLE POLYNOMIAL FIT BY
! PRECALCULATING TEMPERATURE DEPENDENCIES
              EPCX3(1:NSTORDT) = EPLCX3(IRCX,K,1:NSTORDT)
              FP = 0._DP
              RCMIN = -HUGE(1._DP)
              RCMAX = HUGE(1._DP)
              EXPO = EIRENE_SNGL_POLY(TBEL3,ELB,RCMIN,RCMAX,FP,0,0)
            ELSE
! CALCULATE ENERGY-WEIGHTED RATE-COEFFICIENT
              KK=NELRCX(IRCX)
              PLS=TIINL(IPLSTI,K)+ADDCX(IRCX,IPLS)
              EXPO = EIRENE_ENERGY_RATE_COEFF(KK,PLS,ELB,.FALSE.,0)
     .               + DIINL(IPLS,K) + FACREA(KK,2)
            END IF
            ESIGCX(IRCX,1)=EXP(EXPO)/SIGVCX(IRCX)
            ESIGCX(IRCX,1)=ESIGCX(IRCX,1)+EDRIFT(IPLS,K)
          ENDIF  ! this was for tracklength estimator only
          CFLAG(3,1)=3
        ELSEIF (MODCOL(3,4,IRCX).EQ.3) THEN
C  MODEL 3:
C  MEAN ENERGY FROM DRIFTING ISOTROPIC ONE SPEED DISTRIBUTION
C  (ONLY NEEDED FOR TRACKLENGTH ESTIMATOR)
C  ION SAMPLING FROM WEIGHTED DRIFTING ISOTROPIC ONE SPEED DISTRIBUTION
          IF (NSTORDR >= NRAD) THEN
            ESIGCX(IRCX,1)=EPLCX3(IRCX,K,1)
          ELSE
            ESIGCX(IRCX,1)=EIRENE_FEPLCX3(IRCX,K)
          END IF
          CFLAG(3,1)=1
        ELSE
          GOTO 992
        ENDIF
41    CONTINUE
C
C  ELASTIC COLLISIONS OF MOLECULE IMOL WITH IONS OF SPEZIES IPLS=1,NPLSI
C  50--->60
C
50    CONTINUE
      IF (LGMEL(IMOL,0,0).EQ.0.OR.LGVAC(K,0)) GOTO 60
      DO 51 IMEL=1,NMELI(IMOL)
        IREL=LGMEL(IMOL,IMEL,0)
        IPLS=LGMEL(IMOL,IMEL,1)
        IPLSTI=MPLSTI(IPLS)
        IPLSV=MPLSV(IPLS)
        IBGK=NPBGKP(IPLS,1)
        IF (LGVAC(K,IPLS)) GOTO 51
C
C  1.) RATE COEFFICIENT
C
        IF (MODCOL(5,2,IREL).EQ.1) THEN
C  MAXWELL
          IF (NSTORDR >= NRAD) THEN
            SIGVEL(IREL)=TABEL3(IREL,K,1)
          ELSE
            KK=NREAEL(IREL)
            PLS=TIINL(IPLSTI,K)+ADDEL(IREL,IPLS)
            TBEL = EIRENE_RATE_COEFF(KK,PLS,0._DP,.TRUE.,0,ERATE)*
     .             DIIN(IPLS,K)
            SIGVEL(IREL)=TBEL
          END IF
        ELSEIF (MODCOL(5,2,IREL).EQ.2) THEN
C  BEAM - MAXWELL
          IF (TIIN(IPLSTI,K).LT.TVAC) THEN
C  TEMPERATURE TOO LOW, USE: BEAM_ATOM - BEAM_DRIFT RATECOEFF.
            VRELQ=PVELQ(IPLSV)
            VREL=SQRT(VRELQ)
            ELAB=LOG(VRELQ)+DEFEL(IREL)
            IREAC=MODCOL(5,1,IREL)
            CEL=EIRENE_CROSS(ELAB,IREAC,IREL,'FPATHM EL1')
            SIGVEL(IREL)=CEL*VREL*DENIO(IPLS)
          ELSE
C  MINIMUM PROJECTILE ENERGY: 0.1 EV
            ELB=MAX(-2.3_DP,LOG(PVELQ(IPLSV))+EEFEL(IREL))
            IF (NSTORDR >= NRAD) THEN
! DOUBLE POLYNOMIAL FIT REDUCED TO SINGLE POLYNOMIAL FIT BY
! PRECALCULATING TEMPERATURE DEPENDENCIES
              TBEL3(1:NSTORDT) = TABEL3(IREL,K,1:NSTORDT)
              FP = 0._DP
              RCMIN = -HUGE(1._DP)
              RCMAX = HUGE(1._DP)
              EXPO = EIRENE_SNGL_POLY(TBEL3,ELB,RCMIN,RCMAX,FP,0,0)
            ELSE
! CALCULATE RATE-COEFFICIENT
              KK=NREAEL(IREL)
              PLS=TIINL(IPLSTI,K)+ADDEL(IREL,IPLS)
              EXPO = EIRENE_RATE_COEFF(KK,PLS,ELB,.FALSE.,0,ERATE)
     .               + DIINL(IPLS,K) + FACREA(KK,2)
            END IF
            SIGVEL(IREL)=EXP(EXPO)
          ENDIF
        ELSEIF (MODCOL(5,2,IREL).EQ.3) THEN
C  BEAM - BEAM RATE, BUT WITH EFFECTIVE INTERACTION ENERGY
          VEFFQ=ZTI(IPLS)+PVELQ(IPLSV)
          VEFF=SQRT(VEFFQ)
          ELAB=LOG(VEFFQ)+DEFEL(IREL)
          IREAC=MODCOL(5,1,IREL)
C  FIND SIGMA FROM OAK RIDGE "ELASTIC" DATA TABLES
          IF (LHABER) THEN
            RMN=RMASSM(IMOL)
            RMI=RMASSP(IPLS)
            RMSI=1./(RMN+RMI)
            RLMS=RMN*RMI*RMSI
            ER=RLMS*VEFFQ*CVELI2
cdr  flag -1.0_DP: only sigma(ER), but no scattering angle evaluated
            CALL EIRENE_SCATANG (ER,-1.0_DP,ELTHDUM,CTCHDUM,SIG)
            CEL= SIG*AU_TO_CM2
          ELSE
C  FIND SIGMA FROM AMJUEL DATA TABLES (BACHMANN ET AL.)
            CEL=EIRENE_CROSS(ELAB,IREAC,IREL,'FPATHM EL2')
          END IF
          SIGVEL(IREL)=CEL*VEFF*DENIO(IPLS)
        ELSEIF (MODCOL(5,2,IREL).EQ.4) THEN
C  MODEL 4
C  BEAM - BEAM RATE, IGNORE THERMAL ION ENERGY
          VRELQ=PVELQ(IPLSV)
          VREL=SQRT(VRELQ)
          ELAB=LOG(VRELQ)+DEFEL(IREL)
          IREAC=MODCOL(5,1,IREL)
          CEL=EIRENE_CROSS(ELAB,IREAC,IREL,'FPATHM EL3')
          SIGVEL(IREL)=CEL*VREL*DENIO(IPLS)
        ELSE
          GOTO 995
        ENDIF
 
        SIGMAX=MAX(SIGMAX,SIGVEL(IREL))
        SIGELT=SIGELT+SIGVEL(IREL)
 
        IF (IBGK.NE.0) SIGBGK=SIGBGK+SIGVEL(IREL)
C
C  2.) BULK ION ENERGY LOSS RATE:
C
        IF (MODCOL(5,4,IREL).EQ.1) THEN
C  MODEL 1:
C  MEAN ENERGY FROM DRIFTING MAXWELLIAN
C  (ONLY NEEDED FOR TRACKLENGTH ESTIMATOR)
C  ION SAMPLING FROM MAXWELLIAN
          IF (NSTORDR >= NRAD) THEN
            ESIGEL(IREL,1)=EPLEL3(IREL,K,1)
          ELSE
            ESIGEL(IREL,1)=EIRENE_FEPLEL3(IREL,K)
          END IF
          CFLAG(5,1)=2
        ELSEIF (MODCOL(5,4,IREL).EQ.2) THEN
C  MODEL 2:
C  MEAN ENERGY FROM CROSS SECTION WEIGHTED DRIFTING MAXWELLIAN
C  (ONLY NEEDED FOR TRACKLENGTH ESTIMATOR)
C  ION SAMPLING FROM WEIGHTED DRIFTING MAXWELLIAN (E.G., BY REJECTION)
          IF (IESTEL(IREL,3).EQ.0) THEN  ! for tracklength estimator only
C  MINIMUM PROJECTILE ENERGY: 0.1 EV
            ELB=MAX(-2.3_DP,LOG(PVELQ(IPLSV))+EEFEL(IREL))
            IF (NSTORDR >= NRAD) THEN
! DOUBLE POLYNOMIAL FIT REDUCED TO SINGLE POLYNOMIAL FIT BY
! PRECALCULATING TEMPERATURE DEPENDENCIES
              EPEL3(1:NSTORDT) = EPLEL3(IREL,K,1:NSTORDT)
              FP = 0._DP
              RCMIN = -HUGE(1._DP)
              RCMAX = HUGE(1._DP)
              EXPO = EIRENE_SNGL_POLY(EPEL3,ELB,RCMIN,RCMAX,FP,0,0)
            ELSE
! CALCULATE ENERGY-WEIGHTED RATE-COEFFICIENT
              KK=NELREL(IREL)
              PLS=TIINL(IPLSTI,K)+ADDEL(IREL,IPLS)
              EXPO = EIRENE_ENERGY_RATE_COEFF(KK,PLS,ELB,.FALSE.,0)
     .               + DIINL(IPLS,K) + FACREA(KK,2)
            END IF
            ESIGEL(IREL,1)=EXP(EXPO)/SIGVEL(IREL)
            ESIGEL(IREL,1)=ESIGEL(IREL,1)+EDRIFT(IPLS,K)
          ENDIF  ! this was for tracklength estimator only
          CFLAG(5,1)=3
C       ELSEIF (MODCOL(5,4,IREL).EQ.3) THEN
C  MODEL 3:
C  MEAN ENERGY FROM DRIFTING ISOTROPIC ONE SPEED DISTRIBUTION
C  (ONLY NEEDED FOR TRACKLENGTH ESTIMATOR)
C  ION SAMPLING FROM WEIGHTED DRIFTING ISOTROPIC ONE SPEED DISTRIBUTION
          IF (NSTORDR >= NRAD) THEN
            ESIGEL(IREL,1)=EPLEL3(IREL,K,1)
          ELSE
            ESIGEL(IREL,1)=EIRENE_FEPLEL3(IREL,K)
          END IF
          CFLAG(5,1)=1
        ELSE
          GOTO 995
        ENDIF
51    CONTINUE
C
60    CONTINUE
C
C     TOTAL
C
100   CONTINUE
C
      IF (SIGEIT.GT.0._DP) THEN
        DO IREI=1,NRDS
          IF (SIGVEI(IREI) .LE. SIGMAX*1.D-10) THEN
            SIGEIT=SIGEIT-SIGVEI(IREI)
            SIGVEI(IREI) = 0.D0
          END IF
        END DO
      END IF
 
      IF (SIGPIT.GT.0._DP) THEN
        DO IRPI=1,NRPI
          IF (SIGVPI(IRPI) .LE. SIGMAX*1.D-10) THEN
            SIGPIT=SIGPIT-SIGVPI(IRPI)
            SIGVPI(IRPI) = 0.D0
          END IF
        END DO
      END IF
 
      IF (SIGCXT.GT.0._DP) THEN
        DO IRCX=1,NRCX
          IF (SIGVCX(IRCX) .LE. SIGMAX*1.D-10) THEN
            SIGCXT=SIGCXT-SIGVCX(IRCX)
            SIGVCX(IRCX) = 0.D0
          END IF
        END DO
      END IF
 
      IF (SIGELT.GT.0._DP) THEN
        DO IREL=1,NREL
          IF (SIGVEL(IREL) .LE. SIGMAX*1.D-10) THEN
            SIGELT=SIGELT-SIGVEL(IREL)
            SIGVEL(IREL) = 0.D0
          END IF
        END DO
      END IF
C
      SIGTOT=SIGEIT+SIGPIT+SIGCXT+SIGELT
      IF (SIGTOT.GT.1.D-20) THEN
        EIRENE_FPATHM=VEL/SIGTOT
        ZMFPI=1./EIRENE_FPATHM
      ENDIF
C
      RETURN
990   CONTINUE
      WRITE (iunout,*) 'ERROR IN FPATHM: INCONSISTENT ELEC. IMP. DATA'
      WRITE (iunout,*) 'IMOL,IREI,MODCOL(1,J=1,4,IREI) '
      WRITE (iunout,*) IMOL,IREI,(MODCOL(1,J,IREI),J=1,4)
      CALL EIRENE_EXIT_OWN(1)
991   CONTINUE
      WRITE (iunout,*) 'ERROR IN FPATHM: INCONSISTENT ION IMP. DATA'
      WRITE (iunout,*) 'IMOL,IRPI,MODCOL(4,J,IRPI) '
      WRITE (iunout,*) IMOL,IRPI,(MODCOL(4,J,IRPI),J=1,4)
      CALL EIRENE_EXIT_OWN(1)
992   CONTINUE
      WRITE (iunout,*)
     .  'ERROR IN FPATHM: INCONSISTENT CHARGE EXCHANGE DATA'
      WRITE (iunout,*) 'IMOL,IRCX,(MODCOL(3,J,IRCX),J=1,4) '
      WRITE (iunout,*) IMOL,IRCX,(MODCOL(3,J,IRCX),J=1,4)
      CALL EIRENE_EXIT_OWN(1)
995   CONTINUE
      WRITE (iunout,*)
     .  'ERROR IN FPATHM: INCONSISTENT ELASTIC COLL. DATA'
      WRITE (iunout,*) 'IMOL,IREL,(MODCOL(5,J,IREL),J=1,4) '
      WRITE (iunout,*) IMOL,IREL,(MODCOL(5,J,IREL),J=1,4)
      CALL EIRENE_EXIT_OWN(1)
      END
