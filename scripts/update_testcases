#!/bin/tcsh
#
echo 'UPDATING Testcases'
#
if (! $?DIVIMP_PROGDIR) then
   set progdir = divimp
else
   set progdir = $DIVIMP_PROGDIR
endif 

echo 'DIVIMP directory:' $HOME/$progdir

set scriptdir = $HOME/$progdir/scripts

source $scriptdir/init_testcases
#
# Check to see if testcase directory already exists
#
if ( -e $testdir ) then 

   cd $testdir
#
#  Get date/time and create directory to save old test case files
#
   set dateval = `date +%d-%b-%Y-%T`
#
   set olddir = old.testcases.$dateval
#
   mkdir $olddir
#
   echo "Created Archive directory:" $testdir/$olddir  
#
#  Move files over 
# 
   if (-e  testcases_out.txt) then
      mv testcases_out.txt $olddir
   endif
      
   foreach ext ( $extlist ) 

      echo "---- Moving" $ext "files to" $olddir 
#
#     Loop through list of testcases 
#
      foreach testcase ( $testcaselist )
#        
#
#        Remove diff files
#
         if (-e $testcase$ext.diff) then
	    rm $testcase$ext.diff
	 endif
#        
#        Move the old testcases to the archive directory
#
         if ($ext == '.lim') then  

	    if (-e $testcase$ext.$zext) then
		echo "---- Moving" $testcase$ext.$zext "file to" $olddir 
		mv $testcase$ext.$zext $olddir
	    endif
	    
         else if ($ext == '.out') then    

	    if (-e $testcase$ext.$zext) then
		echo "---- Moving" $testcase$ext.$zext "file to" $olddir 
		mv $testcase$ext.$zext $olddir
	    endif

	 else

	     if (-e $testcase$ext) then
		echo "---- Moving" $testcase$ext "file to" $olddir 
		mv $testcase$ext $olddir
	    endif

	 endif   
      end
   end

#
#  Copy the new testcases from the results directory to the testcase directory
#

   foreach ext ( $extlist ) 

      echo "---- Copying" $ext "files to" $testdir

#
#     Loop through list of testcases 
#
      foreach testcase ( $testcaselist )
#        
#        Copy all the new testcases to the testcases directory
#
      
         if ($ext == '.lim') then  

	    if (-e $resultdir/$testcase$ext.$zext) then
		echo "---- Copying" $testcase$ext.$zext "file to" $testdir 
		cp $resultdir/$testcase$ext.$zext $testdir
	    endif
	    
         else if ($ext == '.out') then    

	    if (-e $resultdir/$testcase$ext.$zext) then
		echo "---- Copying" $testcase$ext.$zext "file to" $testdir 
		cp $resultdir/$testcase$ext.$zext $testdir
	    endif
	    
         else

	    if (-e $resultdir/$testcase$ext) then
		echo "---- Copying" $testcase$ext "file to" $testdir 
		cp $resultdir/$testcase$ext $testdir
	    endif
	    
	 endif   
      end
   end
#
#  Return to the divmain directory
#  
   cd $divmain  
#

else
   echo "ERROR: Test cases have not yet been set up on this machine."
   echo "       Please run setup_testcases."
endif








