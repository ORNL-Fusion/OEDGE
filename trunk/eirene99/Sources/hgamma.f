*DK HGAMMA
      SUBROUTINE HGAMMA (IST,IAD1,IAD2,IAD3,IAD4,IAD5,IADS)
c
C  SUBROUTINE FOR H-GAMMA EMISSIVITY. (BALMER SERIES)
C  TAKEN FROM EIRENE, SECTION DIAGNO, SUBR. SIGAL
C  THE H-GAMMA EMISSIVITY PROFILE (PHOTONS/S/CM**3) IS COMPUTED
C  AND WRITTEN ONTO TALLIES ADDV(IAD1,...),... FOR STRATUM NO. IST
C  IAD1: CONTRIBUTION LINEAR IN H   -ATOM      DENSITY
C  IAD2: CONTRIBUTION LINEAR IN H+  -ION       DENSITY
C  IAD3: CONTRIBUTION LINEAR IN H2  -MOLEC.    DENSITY
C  IAD4: CONTRIBUTION LINEAR IN H2+ -MOLEC.ION DENSITY
C  IAD5: CONTRIBUTION LINEAR IN H-  -NEG. ION  DENSITY
C  IADS: SUM OVER ALL CONTRINUTIONS
c
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      INCLUDE 'PARMMOD'
      INCLUDE 'COMPRT'
      INCLUDE 'CSPEI'
      INCLUDE 'CESTIM'
      INCLUDE 'COMUSR'
      INCLUDE 'COMSOU'
      INCLUDE 'CLOGAU'
      INCLUDE 'COMXS'
      INCLUDE 'COMSIG'
      INCLUDE 'CUPD'
      INCLUDE 'CZT1'
      INCLUDE 'CTRCEI'
      INCLUDE 'CGRID'
      INCLUDE 'CCONA'
      INCLUDE 'CADGEO'
      INCLUDE 'CLGIN'
      INCLUDE 'CSDVI'
      INCLUDE 'CSDVI_BGK'
      INCLUDE 'CSDVI_COP'
      INCLUDE 'COUTAU'
      DIMENSION OUTAU(NOUTAU),ESTIM(NESTIM),SDVI(NSDVI),
     .          SDVI_BGK(NSBGK),SDVI_COP(NSCOP)
      EQUIVALENCE
     .  (OUTAU(1),PDENAI(0,0)),
     .  (ESTIM(1),PDENA(1,1)),
     .  (SDVI(1),SIGMA(1,1)),
     .  (SDVI_BGK(1),SIGMA_BGK(1,1)),
     .  (SDVI_COP(1),SIGMA_COP(1,1))
C
      DIMENSION DA51(0:8,0:8)
      DIMENSION DP51(0:8,0:8)
      DIMENSION DM51(0:8,0:8)
      DIMENSION DI51(0:8,0:8)
      DIMENSION DN51(0:8,0:8)
      DIMENSION RHMH2(0:8),RH2PH2(0:8,0:8)
      CHARACTER FILNAM*8,H123*4,REAC*9,CRC*3
      CHARACTER*6 CISTRA
C
      SAVE
C
      DATA IFIRST/0/
C  RADIATIVE TRANSITION RATES (1/S)
C  BALMER ALPHA
      FAC32=4.410E7
C  BALMER BETA
      FAC42=8.419E6
C  BALMER GAMMA
      FAC52=2.530E6
C
C  LYMAN ALPHA
      FAC21=4.699E9
C  LYMAN BETA
      FAC31=5.575E7
C  LYMAN GAMMA
      FAC41=1.278E7
C  LYMAN DELTA
      FAC51=4.125E6
C
C  PASCHEN ALPHA
      FAC43=8.986E6
C  PASCHEN BETA
      FAC53=2.201E6
C
C  INITIALIZE ATOMIC DATA ARRAYS
C
      IF (IFIRST.EQ.0) THEN
        IFIRST=1
        IERROR=0
C
C  READ REDUCED POPULATION COEFFICIENT FOR HYDR. ATOMS FROM FILE AMJUEL
C  AND PUT THEM FROM CREAC(..,..,IR) ONTO DA,DP,DM,DI, AND DN ARRAY
C
        IR=NREACI+1
        IF (IR.GT.NREAC) THEN
          WRITE (6,*) 'FROM SUBROUTINE HGAMMA: '
          CALL MASPRM('NREAC',5,NREAC,'IR',2,IR,IERROR)
          CALL EXIT
        ENDIF
C
        FILNAM='AMJUEL  '
        H123='H.12'
        CRC='OT '
C
C  H(n=5)/H(n=1)
        REAC='2.1.5d   '
        CALL SLREAC(NREACI+1,FILNAM,H123,REAC,CRC)
        DO J=1,9
          DO I=1,9
            DA51(J-1,I-1)=CREAC(J,I,NREACI+1)
          ENDDO
        ENDDO
C  H(n=5)/H+
        REAC='2.1.8d   '
        CALL SLREAC(NREACI+1,FILNAM,H123,REAC,CRC)
        DO J=1,9
          DO I=1,9
            DP51(J-1,I-1)=CREAC(J,I,NREACI+1)
          ENDDO
        ENDDO
C  H(n=5)/H2(g)
        REAC='2.2.5d   '
        CALL SLREAC(NREACI+1,FILNAM,H123,REAC,CRC)
        DO J=1,9
          DO I=1,9
            DM51(J-1,I-1)=CREAC(J,I,NREACI+1)
          ENDDO
        ENDDO
C  H(n=5)/H2+(g)
        REAC='2.2.14d  '
        CALL SLREAC(NREACI+1,FILNAM,H123,REAC,CRC)
        DO J=1,9
          DO I=1,9
            DI51(J-1,I-1)=CREAC(J,I,NREACI+1)
          ENDDO
        ENDDO
C  H(n=5)/H-
        REAC='7.2d     '
        CALL SLREAC(NREACI+1,FILNAM,H123,REAC,CRC)
        DO J=1,9
          DO I=1,9
            DN51(J-1,I-1)=CREAC(J,I,NREACI+1)
          ENDDO
        ENDDO
C
C  NOW READ RATIO OF DENSITIES:
C
C  FIRST: H-/H2
        FILNAM='AMJUEL  '
        H123='H.11'
        REAC='7.0      '
        CRC='OT '
        CALL SLREAC(NREACI+1,FILNAM,H123,REAC,CRC)
        DO I=1,9
          RHMH2(I-1)=CREAC(I,1,NREACI+1)
        ENDDO

C  NEXT : H2+/H2
        FILNAM='AMJUEL  '
        H123='H.12'
        REAC='2.0c     '
        CRC='OT '
C  2.0c INCLUDES VIBRAT. EQUILIBRIUM AND ION CONVERION (CX) ON H2(V)
C  OLD VERSION (WITHOUT THIS CX) SHOULD BE RECOVERED BY
C  READING 2.0b INSTEAD, AND OMITTING THE H- CHANNEL 5.
C
CDR: IF H2(V) IS NOT INCLUDED IN A SPECIFIC NEUTRAL TRANSPORT EQUATION
CDR  (SEE INPUT BLOCK 4), THEN IT SHOULD NOT BE INCLUDED HERE EITHER.
CDR  2.0b DOES NOT INCLUDE CX ON H2(v=0) EITHER.
c slmod begin - tr
        IF (cxd2opt.EQ.0) THEN
c          WRITE(0,*) 'EXCLUDING CX CONTRIBUTION TO DGAMMA'
          WRITE(6,*) 'EXCLUDING CX CONTRIBUTION TO DGAMMA'

          REAC='2.0b    '
        ENDIF
c slmod end
CDR
        CALL SLREAC(NREACI+1,FILNAM,H123,REAC,CRC)
        DO I=1,9
          DO J=1,9
            RH2PH2(I-1,J-1)=CREAC(I,J,NREACI+1)
          ENDDO
        ENDDO
      ENDIF
C
C  END OF INITIALIZATION
C
      IF ((NFILEN.EQ.1.OR.NFILEN.EQ.2).AND.IESTR.NE.IST) THEN
        IESTR=IST
        CALL RSTRT(IST,NSTRAI,NESTIM,NSDVI,ESTIM,SDVI,
     .             NSBGK,SDVI_BGK,NSCOP,SDVI_COP,TRCFLE)
      ELSEIF ((NFILEN.EQ.6.OR.NFILEN.EQ.7).AND.IST.EQ.0) THEN
        IESTR=IST
        CALL RSTRT(IST,NSTRAI,NESTIM,NSDVI,ESTIM,SDVI,
     .             NSBGK,SDVI_BGK,NSCOP,SDVI_COP,TRCFLE)
      ELSEIF (NFILEN.EQ.0.AND.ISTRA.EQ.IESTR) THEN
C  NOTHING TO BE DONE
      ELSE
c slmod begin - done - tr
c...note: Things should be okay as they are right now (May 19, 1999).  The
c         above IF statements don't make much sense in the context of 3/97,
c         so something isn't correct here.
c
        WRITE (0,*) 'HGAMMA: OKAY'

        WRITE (6,*) 'ERROR IN HGAMMA: DATA FOR STRATUM ISTRA= ',IST
        WRITE (6,*) 'QUESTIONABLE, USE AT OWN RISK'
c
c        WRITE (6,*) 'ERROR IN HGAMMA: DATA FOR STRATUM ISTRA= ',IST
c        WRITE (6,*) 'ARE NOT AVAILABLE. H-ALPHA ABANDONNED'
c        RETURN
c slmod end
      ENDIF
C
C  LOOP OVER 2D MESH
C
      POWALF=0.
      POWALF1=0.
      POWALF2=0.
      POWALF3=0.
      POWALF4=0.
      POWALF5=0.
      DO 1000 NCELL=1,NSBOX
C
C  LOCAL PLASMA DATA
C
        TE=TEIN(NCELL)
        DE=DEIN(NCELL)
        SIGADD1=0.
        SIGADD2=0.
        SIGADD3=0.
        SIGADD4=0.
        SIGADD5=0.
        IF (LGVAC(NCELL,0)) GOTO 500
c slmod begin - tr
c...ion species 1 is D+ for these cases, which may not always be the case...
        IF (LGVAC(NCELL,1)) CYCLE
c slmod end
        DEF=LOG(DE*1.D-8)
        TEF=LOG(TE)
        DATM5=0.
        DPLS5=0.
        DMOL5=0.
        DION5=0.
        DNML5=0.
        DO 150 J=0,8
          DEJ=DEF**J
          DO 150 I=0,8
            TEI=TEF**I
            DATM5=DATM5+DA51(I,J)*TEI*DEJ
            DPLS5=DPLS5+DP51(I,J)*TEI*DEJ
            DMOL5=DMOL5+DM51(I,J)*TEI*DEJ
            DION5=DION5+DI51(I,J)*TEI*DEJ
            DNML5=DNML5+DN51(I,J)*TEI*DEJ
150     CONTINUE
        DATM5=EXP(DATM5)
        DPLS5=EXP(DPLS5)
        DMOL5=EXP(DMOL5)
        DION5=EXP(DION5)
        DNML5=EXP(DNML5)

C  RATIO OF DENSITIES: H- TO H2, COLL. EQUIL. IN VIBRATION
C  (ONLY TE-DEPENDENT)

        RATIO7=0
        DO 160 I=0,8
          TEI=TEF**I
          RATIO7=RATIO7+RHMH2(I)*TEI
160     CONTINUE
        RATIO7=EXP(RATIO7)

C  RATIO OF DENSITIES: H2+ TO H2, INCL. ION CONVERSION
C  RATIO OF DENSITIES: H2+ TO H2, EXCL. ION CONVERSION

        RATIO2=0
        DO 170 J=0,8
          DEJ=DEF**J
          DO 170 I=0,8
            TEI=TEF**I
            RATIO2=RATIO2+RH2PH2(I,J)*TEI*DEJ
170     CONTINUE
        RATIO2=EXP(RATIO2)

C
C  CHANNEL 1
C  H ALPHA SOURCE RATE:  PHOTONS/SEC/CM**3
C  LINEAR IN PDENA (IONIZATION)
C
        DO 200 IATM=1,NATMI
          IF (NCHARA(IATM).NE.1) GOTO 200
          DA=DATM5*PDENA(IATM,NCELL)
C  RADIATIVE TRANSITION PROB. LEVEL 5-->2 (1/SEC)
C  SIGADD: PHOTONS/SEC/CM**3
          SIGADD1=SIGADD1+DA*FAC52
200     CONTINUE
C
C  CHANNEL 2
C  H ALPHA SOURCE RATE:  PHOTONS/SEC/CM**3
C  LINEAR IN DIIN (RECOMBINATION)
C
        DO 205 IPLS=1,NPLSI
          IF (NCHARP(IPLS).NE.1.OR.NCHRGP(IPLS).NE.1) GOTO 205
          DP=DPLS5*DIIN(IPLS,NCELL)
C  RADIATIVE TRANSITION PROB. LEVEL 5-->2 (1/SEC)
C  SIGADD: PHOTONS/SEC/CM**3
c slmod begin - tr
c...      Turn off contribution if recombination is 
c         off:
          IF (tabrcm(1,ncell).LT.1.0D-14) THEN
            IF (ncell.EQ.1) 
     .        WRITE(0,*) 'NO Hgamma CONTRIBUTION FROM RECOMBINATION'
          ELSE
            SIGADD2=SIGADD2+DP*FAC52
          ENDIF
c
c          SIGADD2=SIGADD2+DP*FAC52
c slmod end
205     CONTINUE
C
C  CHANNEL 3
C  H ALPHA SOURCE RATE:  PHOTONS/SEC/CM**3
C  LINEAR IN PDENM (DISSOCIATION OF H2)
C
        DO 210 IMOL=1,NMOLI
          IF (NCHARM(IMOL).NE.2) GOTO 210
          DM=DMOL5*PDENM(IMOL,NCELL)
C  RADIATIVE TRANSITION PROB. LEVEL 5-->2 (1/SEC)
C  SIGADD: PHOTONS/SEC/CM**3
          SIGADD3=SIGADD3+DM*FAC52
210     CONTINUE
C
C  CHANNEL 4
C  H ALPHA SOURCE RATE:  PHOTONS/SEC/CM**3
C  LINEAR IN PDENI: (DISSOCIATION OF H2+)
C
C  REVISED: USE (PDENM * DENSITY RATIO H2+/H2) NOW, INSTEAD OF PDENI
C
C       DO 215 IION=1,NIONI
C         IF (NCHARI(IION).NE.2) GOTO 215
C         DI=DION5*PDENI(IION,NCELL)
C  RADIATIVE TRANSITION PROB. LEVEL 5-->2 (1/SEC)
C  SIGADD: PHOTONS/SEC/CM**3
C         SIGADD4=SIGADD4+DI*FAC52
C215     CONTINUE
        DO 215 IMOL=1,NMOLI
          IF (NCHARM(IMOL).NE.2) GOTO 215
          DI=DION5*PDENM(IMOL,NCELL)*RATIO2
C  RADIATIVE TRANSITION PROB. LEVEL 5-->2 (1/SEC)
C  SIGADD: PHOTONS/SEC/CM**3
          SIGADD4=SIGADD4+DI*FAC52
215     CONTINUE
C
C  CHANNEL 5
C  H ALPHA SOURCE RATE:  PHOTONS/SEC/CM**3
C  LINEAR IN H- DENSITY (CHARGE EXCHANGE RECOMBINATION)
C
C  REVISED: USE (PDENM * DENSITY RATIO H-/H2) NOW, INSTEAD OF PDENI
C
        DO 220 IMOL=1,NMOLI
          IF (NCHARM(IMOL).NE.2) GOTO 220
          DN=DNML5*PDENM(IMOL,NCELL)*RATIO7
C  RADIATIVE TRANSITION PROB. LEVEL 5-->2 (1/SEC)
C  SIGADD: PHOTONS/SEC/CM**3
c slmod begin - tr
          IF (cxd2opt.EQ.0) SIGADD5=0.0D0
c slmod end
          SIGADD5=SIGADD5+DN*FAC52
220     CONTINUE
C
500     CONTINUE
C
        SIGADD=SIGADD1+SIGADD2+SIGADD3+SIGADD4+SIGADD5
C
        IF (IAD1.LE.NADV.AND.IAD1.GT.0)
     .    ADDV(IAD1,NCELL)=ADDV(IAD1,NCELL)+SIGADD1
        IF (IAD2.LE.NADV.AND.IAD2.GT.0)
     .    ADDV(IAD2,NCELL)=ADDV(IAD2,NCELL)+SIGADD2
        IF (IAD3.LE.NADV.AND.IAD3.GT.0)
     .    ADDV(IAD3,NCELL)=ADDV(IAD3,NCELL)+SIGADD3
        IF (IAD4.LE.NADV.AND.IAD4.GT.0)
     .    ADDV(IAD4,NCELL)=ADDV(IAD4,NCELL)+SIGADD4
        IF (IAD5.LE.NADV.AND.IAD5.GT.0)
     .    ADDV(IAD5,NCELL)=ADDV(IAD5,NCELL)+SIGADD5
        IF (IADS.LE.NADV.AND.IADS.GT.0)
     .    ADDV(IADS,NCELL)=ADDV(IADS,NCELL)+SIGADD
C
C
        POWALF1=POWALF1+SIGADD1*3.028E-19*VOL(NCELL)
        POWALF2=POWALF2+SIGADD2*3.028E-19*VOL(NCELL)
        POWALF3=POWALF3+SIGADD3*3.028E-19*VOL(NCELL)
        POWALF4=POWALF4+SIGADD4*3.028E-19*VOL(NCELL)
        POWALF5=POWALF5+SIGADD5*3.028E-19*VOL(NCELL)
C
        POWALF =POWALF +SIGADD *3.028E-19*VOL(NCELL)
C
1000  CONTINUE

      CALL LEER(2)
      CALL FTCRI(IST,CISTRA)
      IF (IST.GT.0)
     .CALL MASBOX('SUBR. HGAMMA CALLED, FOR STRATUM NO. '//CISTRA)
      IF (IST.EQ.0)
     .CALL MASBOX('SUBR. HGAMMA CALLED, FOR SUM OVER STRATA')
      CALL LEER(1)
      WRITE (6,*) ' RADIATED POWER BY HGAMMA:',POWALF
      WRITE (6,*) ' COUPL. TO GROUNDSTATE   :',POWALF1
      WRITE (6,*) ' COUPLING TO CONTINUUM   :',POWALF2
      WRITE (6,*) ' COUPLING TO MOLECULES   :',POWALF3
      WRITE (6,*) ' COUPLING TO MOL.IONS    :',POWALF4
      WRITE (6,*) ' COUPLING TO NEG.IONS    :',POWALF5
      CALL LEER(2)

      IF (IAD1.LE.NADV.AND.IAD1.GT.0)
     .CALL INTTAL (ADDV,VOL,IAD1,NADV,NSBOX,ADDVI(IAD1,IST),
     .             NR1ST,NP2ND,NT3RD,NBMLT)
      IF (IAD2.LE.NADV.AND.IAD2.GT.0)
     .CALL INTTAL (ADDV,VOL,IAD2,NADV,NSBOX,ADDVI(IAD2,IST),
     .             NR1ST,NP2ND,NT3RD,NBMLT)
      IF (IAD3.LE.NADV.AND.IAD3.GT.0)
     .CALL INTTAL (ADDV,VOL,IAD3,NADV,NSBOX,ADDVI(IAD3,IST),
     .             NR1ST,NP2ND,NT3RD,NBMLT)
      IF (IAD4.LE.NADV.AND.IAD4.GT.0)
     .CALL INTTAL (ADDV,VOL,IAD4,NADV,NSBOX,ADDVI(IAD4,IST),
     .             NR1ST,NP2ND,NT3RD,NBMLT)
      IF (IAD5.LE.NADV.AND.IAD5.GT.0)
     .CALL INTTAL (ADDV,VOL,IAD5,NADV,NSBOX,ADDVI(IAD5,IST),
     .             NR1ST,NP2ND,NT3RD,NBMLT)
      IF (IADS.LE.NADV.AND.IADS.GT.0)
     .CALL INTTAL (ADDV,VOL,IADS,NADV,NSBOX,ADDVI(IADS,IST),
     .             NR1ST,NP2ND,NT3RD,NBMLT)

C
      IF (NFILEN.EQ.1.OR.NFILEN.EQ.2) THEN
        IESTR=IST
        CALL WRSTRT(IST,NSTRAI,NESTIM,NSDVI,ESTIM,SDVI,
     .              NSBGK,SDVI_BGK,NSCOP,SDVI_COP,TRCFLE)
C
        IRC=2
        WRITE (11,REC=IRC) OUTAU
        IF (TRCFLE)   WRITE (6,*) 'WRITE 11  IRC= ',IRC
      ELSEIF ((NFILEN.EQ.6.OR.NFILEN.EQ.7).AND.IST.EQ.0) THEN
        IESTR=IST
        CALL WRSTRT(IST,NSTRAI,NESTIM,NSDVI,ESTIM,SDVI,
     .              NSBGK,SDVI_BGK,NSCOP,SDVI_COP,TRCFLE)
C
        IRC=2
        WRITE (11,REC=IRC) OUTAU
        IF (TRCFLE)   WRITE (6,*) 'WRITE 11  IRC= ',IRC
      ENDIF
C
      RETURN
      END
