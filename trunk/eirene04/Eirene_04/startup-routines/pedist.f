C
      SUBROUTINE PEDIST (XTIM)
C
C   SUBROUTINE PEDIST CALCULATES THE DISTRIBUTION OF PROCESSORS TO
C   STRATA IN CASE THAT THERE ARE MORE PE'S THAN STRATA
C   DISTRIBUTION OF PE'S IS DONE ACCORDING TO THE DISTRIBUTION OF
C   COMPUTATION TIME.
C
      USE PRECISION
      USE PARMMOD
      USE CCONA
      USE CPES
      USE COMSOU

      IMPLICIT NONE

      REAL(DP), INTENT(IN) :: XTIM(0:NSTRA)
      REAL(DP) :: TIMPE(0:NSTRA)
      REAL(DP) :: FACP, DELT, SUMTIM, TMEAN
      INTEGER :: IPE, K, I, ISTRA, NPRS_FREE, NPRS_OPT

      SUMTIM=0.
      DO ISTRA=1,NSTRAI
        SUMTIM=SUMTIM+XTIM(ISTRA)
      ENDDO

      sumtim=xtim(nstrai)
      TMEAN=SUMTIM/FLOAT(NPRS)

      WRITE (6,*) ' SUMTIM = ',SUMTIM,' MEAN TIME = ',TMEAN

      NPRS_OPT=0
      NPRS_FREE=NPRS
      DO ISTRA=1,NSTRAI
        delt=xtim(istra)-xtim(istra-1)
        IF (delt.GE.1.E-5) THEN
          NPESTR(ISTRA)=1
          NPRS_FREE=NPRS_FREE-1
        ELSE
          NPESTR(ISTRA)=0
        ENDIF
        TIMPE(ISTRA)=MAX(delt-TMEAN,1.E-5_DP)/TMEAN
        NPRS_OPT=NPRS_OPT+int(TIMPE(ISTRA))
      ENDDO
      WRITE (6,*) ' ISTRA, TIMPE '
      DO ISTRA=1,NSTRAI
        WRITE (6,*) ISTRA,TIMPE(ISTRA)
      ENDDO

      WRITE (6,*) ' NPRS_FREE ',NPRS_FREE

      FACP=MIN(1.D0,REAL(NPRS_FREE,KIND(1.D0))/
     .             (REAL(NPRS_OPT,KIND(1.D0))+eps30))
      write (6,*) ' facp ',facp
      NPESTR(0)=NPRS
      DO ISTRA=1,NSTRAI
        NPESTR(ISTRA)=NPESTR(ISTRA)+int(TIMPE(ISTRA)*FACP)
        NPRS_FREE=NPRS_FREE-int(TIMPE(ISTRA)*FACP)
      ENDDO
      WRITE (6,*) ' NPESTR ',(NPESTR(ISTRA),ISTRA=1,NSTRAI)
      WRITE (6,*) ' NPRS_FREE ',NPRS_FREE

      ISTRA=0
      DO WHILE (NPRS_FREE.GT.0)
        ISTRA=ISTRA+1
        IF (ISTRA.GT.NSTRAI) ISTRA=1
        IF (TIMPE(ISTRA).GT.1.E-10) THEN
          NPESTR(ISTRA)=NPESTR(ISTRA)+1
          NPRS_FREE=NPRS_FREE-1
        ENDIF
      ENDDO
      WRITE (6,*) ' NPESTR '
      WRITE (6,'(12I6)') (NPESTR(ISTRA),ISTRA=1,NSTRAI)
      WRITE (6,*) ' NPRS_FREE ',NPRS_FREE

      IPE=0
      DO ISTRA=1,NSTRAI
        DO K=1,NPESTR(ISTRA)
          IPE=IPE+1
          NSTRPE(IPE-1)=ISTRA
        ENDDO
      ENDDO
      WRITE (6,*) ' IPE, ISTRA '
      WRITE (6,'(12I6)') (I,NSTRPE(I),I=0,NPRS-1)

      NPESTA(0)=0
      NPESTA(1)=0
      DO ISTRA=2,NSTRAI
        NPESTA(ISTRA)=NPESTA(ISTRA-1)+NPESTR(ISTRA-1)
      ENDDO
      WRITE (6,*) ' NPESTA '
      WRITE (6,'(12I6)') (NPESTA(I),I=0,NSTRAI)

      RETURN
      END
