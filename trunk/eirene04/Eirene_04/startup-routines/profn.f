C
C
      SUBROUTINE PROFN(PRO,PRO0,PROS,P,Q,E,SEP,PROVAC)
C
C  PARABOLIC PROFILE, PLUS EXPONENTIAL DECAY BEYOND RHOSEP
C
      USE PRECISION
      USE PARMMOD
      USE CCONA
      USE CGRID

      IMPLICIT NONE

      REAL(DP), INTENT(IN) :: PRO0, PROS, P, Q, E, SEP, PROVAC
      REAL(DP), INTENT(OUT) :: PRO(*)
      REAL(DP) :: EP1R, ELLR, YR, AREAA, ARCA, RHOSEP, RORP, FACT, ZR,
     .          RDI, AR
      INTEGER :: J, JM1, LEARCA, NLOCAL

      IF (LEVGEO.LE.2) THEN
        IF (RRA.GT.RAA) THEN
          NLOCAL=NR1STM
          PRO(NR1STM)=PROVAC
        ELSE
          NLOCAL=NR1ST
        ENDIF
C
C FIND RADIAL SURFACE LABELING CO-ORDINATE AT "SEP"
C
        IF (LEVGEO.EQ.2) THEN
          JM1=LEARCA(SEP,RSURF,1,NLOCAL,1,'PROFN       ')
          AR=AREAA (SEP,JM1,ARCA,YR,EP1R,ELLR)
          RHOSEP=SQRT(AR*PIAI)
        ELSEIF (LEVGEO.EQ.1) THEN
          RHOSEP=SEP
        ENDIF
      ELSE
        WRITE (6,*) 'WARNING: SUBR. PROFN CALLED WITH LEVGEO.GT.2 '
        WRITE (6,*) 'NO PLASMA PARAMETERS RETURNED'
        RETURN
      ENDIF
C
      RDI=1./(RHOSEP-RHOSRF(1))
      DO 20 J=1,NLOCAL-1
        IF (RHOZNE(J).LE.RHOSEP) THEN
          ZR=RHOZNE(J)-RHOSRF(1)
          RORP=ZR*RDI
          IF (RORP.LE.0.D0) THEN
            PRO(J)=PRO0
          ELSE
            FACT=(1.0-RORP**P)**Q
            PRO(J)=(PRO0-PROS)*FACT+PROS
          ENDIF
        ELSE
          PRO(J)=PROS*EXP((RHOSEP-RHOZNE(J))/E)
        ENDIF
20    CONTINUE
      PRO(NR1ST)=0.
      RETURN
      END
