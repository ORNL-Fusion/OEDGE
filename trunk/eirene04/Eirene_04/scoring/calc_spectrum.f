	SUBROUTINE CALC_SPECTRUM (WT,IND)

	USE PRECISION
	USE PARMMOD
	USE CESTIM
	USE COMPRT

	IMPLICIT NONE
	
	INTEGER, INTENT(IN) :: IND
	REAL(DP), INTENT(IN) :: WT
	INTEGER :: ISPC, I, IS
	REAL(DP) :: ADD
	TYPE(EIRENE_SPECTRUM), POINTER :: P

	IF (IND .NE. 1) RETURN

	SELECT CASE (ITYP)
	CASE (0)
	  IS = IPHOT
	CASE (1)
	  IS = IATM
	CASE (2)
	  IS = IMOL
	CASE (3)
	  IS = IION
	CASE (4)
	  IS = IPLS
	END SELECT

	DO ISPC=1,NADSPC

	  P => ESTIML(ISPC)%PSPC
!	  IF ((ESTIML(ISPC)%PSPC%ISPCSRF == MSURF) .AND.
!     .        (ESTIML(ISPC)%PSPC%IPRTYP == ITYP) .AND.
!     .        ((ESTIML(ISPC)%PSPC%IPRSP == IS) .OR. 
!     .         (ESTIML(ISPC)%PSPC%IPRSP == 0))) THEN
	  IF ((P%ISPCSRF == MSURF) .AND.
     .        (P%IPRTYP == ITYP) .AND.
     .        ((P%IPRSP == IS) .OR. 
     .         (P%IPRSP == 0))) THEN

	    SELECT CASE(ESTIML(ISPC)%PSPC%ISPCTYP)
	    CASE (1)
	      ADD = WT
	    CASE (2)
	      ADD = WT*E0
            CASE DEFAULT
	      ADD = 0._DP
	    END SELECT

	    IF (E0 <= ESTIML(ISPC)%PSPC%SPCMIN) THEN
	      I = 0
	    ELSEIF (E0 >= ESTIML(ISPC)%PSPC%SPCMAX) THEN
	      I = ESTIML(ISPC)%PSPC%NSPC + 1
	    ELSE
	      I = (E0 - ESTIML(ISPC)%PSPC%SPCMIN) * 
     .            ESTIML(ISPC)%PSPC%SPCDELI + 1
	    END IF
	    ESTIML(ISPC)%PSPC%SPC(I) = ESTIML(ISPC)%PSPC%SPC(I) + ADD 
	    ESTIML(ISPC)%PSPC%IMETSP = 1
	  END IF	
	END DO
	
	RETURN
	END SUBROUTINE CALC_SPECTRUM				
