


C-----------------------------------------------------------------------
      SUBROUTINE ZERLEG(AUSDRU, AKTLEN, IPART, PART, IARITH, ARITH,
     >                  TEIL, HILFE, ERROR)
C-----------------------------------------------------------------------
C
C     FUNKTION:
C
C     DAS UNTERPROGRAMM UBERPRUEFT DEN AUSDRUCK AUF REGELVERLETZUNG
C     UND ZERLEGT IHN IN EINZELOPERATIONEN, SO DASS DER AUSDRUCK
C     BEI SEQUENTIELLER ABARBEITUNG RICHTIG AUSGEWERTET WIRD
C
C-----------------------------------------------------------------------
      IMPLICIT NONE

C
C     EIN/AUSGABEPARAMETER :
C
         CHARACTER(*), INTENT(INOUT) :: AUSDRU
C           : AUSDRUCK, DER IM UNTERPROGRAMM ZERLEGT WIRD

         INTEGER, INTENT(INOUT) :: AKTLEN
C           : AKTUELLE LAENGE VON AUSDRU

C
C     AUSGABEPARAMETER :
C
         INTEGER, INTENT(OUT) :: TEIL
C           : AKTUELLE ANZAHL DER ZERLEGUNGEN

         CHARACTER(*), INTENT(OUT) :: PART(*)
C           : FELD VON STRINGS, AUF DENEN DIE EINZELNEN
C             ELEMENTARZERLEGUNGEN FESTGEHALTEN WERDEN

         INTEGER, INTENT(OUT) :: IPART(*)
C           : AKTUELLE LAENGEN VON PART(ZMAX)

         CHARACTER(*), INTENT(OUT) :: ARITH(*)
C           : FELD VON STRINGS, AUF DENEN DIE TEIL-TE GENERATION
C             VON AUSDRU FESTGEHALTEN WIRD

         INTEGER, INTENT(OUT) :: IARITH(*)
C           : AKTUELLE LAENGEN VON ARITH(ZMAX)

         CHARACTER(*), INTENT(OUT) :: HILFE
C           : HILFSSTRING, DER FUER ZUWEISUNGEN BENOETIGT WIRD

         INTEGER, INTENT(OUT) :: ERROR
C           : FEHLERVARIABLE: > 0, FALLS EIN FEHLER AUFGETRETEN

C
C     LOKALE VARIABLEN :
C
         INTEGER :: ALPHA
C           : POSITION DES ERSTEN ZEICHENS VOM TEILAUSDRUCK

         INTEGER :: OMEGA
C           : POSITION DES LEZTEN ZEICHENS VOM TEILAUSDRUCK

C
C     HILFSVARIABLEN :
C
      INTEGER :: POS


C
C     ELIMINATION VON BLANKS UND ERSETZEN VON '**' DURCH '^'
C
      CALL SUBTIT(AUSDRU, AKTLEN, HILFE)
C
C     UEBERPRUEFUNG VERSCHIEDENER REGELVERLETZUNGEN
C
      CALL FEHLER(AUSDRU, AKTLEN, ERROR)
      IF (ERROR .EQ. 0) THEN
C
C        BEGINN DER ZERLEGUNG
C
         TEIL=0

         ALPHA=1
         OMEGA=AKTLEN
         POS=INDEX( AUSDRU(1:AKTLEN), ')' )
C
C        WHILE : SOLANGE NOCH KLAMMERN IN AUSDRU VORHANDEN
C
22       IF (POS .GT. 0) THEN
C
C           BESTIMME LETZTES ZEICHEN IM ERSTEN INNERSTEN KLAMMERAUSDRUCK
C
            OMEGA=POS-1
C
C           BESTIMME ERSTES ZEICHEN IM ERSTEN INNERSTEN KLAMMERAUSDRUCK
C
            POS=0
11             POS=INDEX( AUSDRU(POS+1:OMEGA), '(' ) +POS
            IF ( INDEX ( AUSDRU(POS+1:OMEGA), '(' ) .GT. 0 ) GOTO 11
            ALPHA=POS+1
C
C           ZERLEGUNG DES KLAMMERAUSDRUCKS
C
            CALL SCHRIT(AUSDRU, AKTLEN, ALPHA, OMEGA, TEIL,
     >                  IPART, PART, IARITH, ARITH)
C
C           UEBERPRUEFUNG,OB DIE KLAMMERN WEGFALLEN KOENNEN
C
            IF (OMEGA+1-ALPHA .LE. 2
     >          .AND. INDEX('+-',AUSDRU(ALPHA:ALPHA)) .EQ. 0) THEN
C
C              ENTFERNUNG DER KLAMMERN
C
               IF (ALPHA-1 .EQ. 1) THEN
                  IF (OMEGA+1 .EQ. AKTLEN) THEN
                     HILFE=AUSDRU(ALPHA:OMEGA)
                  ELSE
                     HILFE=AUSDRU(ALPHA:OMEGA)//AUSDRU(OMEGA+2:AKTLEN)
                  ENDIF
               ELSEIF (OMEGA+1 .EQ. AKTLEN) THEN
                  HILFE=AUSDRU(1:ALPHA-2)//AUSDRU(ALPHA:OMEGA)
               ELSE
                  HILFE=AUSDRU(1:ALPHA-2)//AUSDRU(ALPHA:OMEGA)
     >                   //AUSDRU(OMEGA+2:AKTLEN)
               ENDIF
               AKTLEN=AKTLEN-2
               AUSDRU=HILFE
               IARITH(TEIL)=AKTLEN
               ARITH(TEIL)=AUSDRU
            ENDIF

            ALPHA=1
            OMEGA=AKTLEN
            POS=INDEX( AUSDRU(1:AKTLEN), ')' )
            GOTO 22
         ENDIF
C
C        ENDWHILE
C
C        AUSDRUCK ENTHAELT KEINE KLAMMERAUSDRUECKE MEHR
C          => ZERLEGUNG DES GESAMMTEN AUSDRUCKS
C
         CALL SCHRIT(AUSDRU, AKTLEN, ALPHA, OMEGA, TEIL,
     >               IPART, PART, IARITH, ARITH)

         IF (TEIL .EQ. 0) THEN
C
C           ES SIND KEINE ZERLEGUNGEN GEMACHT WORDEN, DA
C           DER AUSDRUCK NUR AUS EINEM OPERAND BESTEHT
C
            TEIL = 1
            PART(1) = 'Z01 = ' // AUSDRU
            IPART(1) = 6 + AKTLEN
            ARITH(1) = 'Z01'
            IARITH(1) = 3
         ELSE
C
C           ERSETZEN VON '^' DURCH '**' UND EINFUEGEN VON 'Z' FUER
C           ZERLEGUNG
C
            CALL RUKSUB(TEIL, IPART, PART, IARITH, ARITH, HILFE)
         ENDIF

      ENDIF
C
C     ENDE VON ZERLEG
C
      END
