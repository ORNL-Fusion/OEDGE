C 24.11.05: chrdf0 introduced (called from xsecta, xsectm, xsecti)
C 08.08.06: error exit 991 introduced: charge conservation violation
! 30.08.06: data structure for reaction data redefined
! 12.10.06: modcol revised
! 22.11.06: flag for shift of first parameter to rate_coeff introduced
! 08.01.07: pls = 0.dp, twice, preset.
C
      SUBROUTINE XSTCX(RMASS,IRCX,ISP,IPL,ISCD1,ISCD2,EBULK,
     .                 CHRDF0,ISCDE,IESTM,KK,FACTKK)

      USE PRECISION
      USE PARMMOD
      USE COMUSR
      USE COMPRT, ONLY: IUNOUT
      USE CCONA
      USE CGRID
      USE CZT1
      USE COMXS

      IMPLICIT NONE

      REAL(DP), INTENT(IN) :: RMASS, EBULK, FACTKK, CHRDF0
      INTEGER, INTENT(IN) :: IRCX, ISP, IPL, ISCD1, ISCD2, ISCDE,
     .                       IESTM, KK
      REAL(DP) :: PLS(NSTORDR), CF(9,0:9), CFF(9)
      REAL(DP) :: ADD, ADDL, RMTEST, RMBULK, FCTKKL, ADDTL, CHRDIF,
     .            ADDT, TMASS, PMASS, COU, RATE_COEFF, ENERGY_RATE_COEFF
      INTEGER :: ITYP1, ITYP2, ISPZ1, IERR, ISPZ2, IATM, IPLS, KREAD,
     .           J, NEND, MODC, NSECX4, I, IPL2, IIO2, IPLTI
      INTEGER, EXTERNAL :: IDEZ
      CHARACTER(8) :: TEXTS1, TEXTS2

      SAVE
C
C  SET NON DEFAULT CHARGE EXCHANGE COLLISION PROCESS NO. IRCX
C
      IF (IPL.LE.0.OR.IPL.GT.NPLSI) GOTO 990
      IF (MASSP(KK).LE.0.OR.MASST(KK).LE.0) GOTO 992
      RMBULK=RMASSP(IPL)
      RMTEST=RMASS
      IPLTI=MPLSTI(IPL)
C
C  1ST SECONDARY INDEX
      N1STX(IRCX,1)=IDEZ(ISCD1,1,3)
      N1STX(IRCX,2)=IDEZ(ISCD1,3,3)
      N1STX(IRCX,3)=0
      IF (N1STX(IRCX,1).LT.4) N1STX(IRCX,3)=1
C
      IF (N1STX(IRCX,1).EQ.1) THEN
        IF (RMBULK.NE.RMASSA(N1STX(IRCX,2))) GOTO 992
      ELSEIF (N1STX(IRCX,1).EQ.2) THEN
        IF (RMBULK.NE.RMASSM(N1STX(IRCX,2))) GOTO 992
      ELSEIF (N1STX(IRCX,1).EQ.3) THEN
        IF (RMBULK.NE.RMASSI(N1STX(IRCX,2))) GOTO 992
      ELSEIF (N1STX(IRCX,1).EQ.4) THEN
        IF (RMBULK.NE.RMASSP(N1STX(IRCX,2))) GOTO 992
      ENDIF
C  2ND SECONDARY INDEX
      N2NDX(IRCX,1)=IDEZ(ISCD2,1,3)
      N2NDX(IRCX,2)=IDEZ(ISCD2,3,3)
      N2NDX(IRCX,3)=N1STX(IRCX,3)
      IF (N2NDX(IRCX,1).LT.4) N2NDX(IRCX,3)=N2NDX(IRCX,3)+1
C
      IF (N2NDX(IRCX,1).EQ.1) THEN
        IF (RMTEST.NE.RMASSA(N2NDX(IRCX,2))) GOTO 992
      ELSEIF (N2NDX(IRCX,1).EQ.2) THEN
        IF (RMTEST.NE.RMASSM(N2NDX(IRCX,2))) GOTO 992
      ELSEIF (N2NDX(IRCX,1).EQ.3) THEN
        IF (RMTEST.NE.RMASSI(N2NDX(IRCX,2))) GOTO 992
      ELSEIF (N2NDX(IRCX,1).EQ.4) THEN
        IF (RMTEST.NE.RMASSP(N2NDX(IRCX,2))) GOTO 992
      ENDIF
C
      CHRDIF=CHRDF0-NCHRGP(IPL)
      IF (N1STX(IRCX,1).EQ.3) THEN
        IIO2=N1STX(IRCX,2)
        CHRDIF=CHRDIF+NCHRGI(IIO2)
      ENDIF
      IF (N1STX(IRCX,1).EQ.4) THEN
        IPL2=N1STX(IRCX,2)
        CHRDIF=CHRDIF+NCHRGP(IPL2)
      ENDIF
      IF (N2NDX(IRCX,1).EQ.3) THEN
        IIO2=N2NDX(IRCX,2)
        CHRDIF=CHRDIF+NCHRGI(IIO2)
      ENDIF
      IF (N2NDX(IRCX,1).EQ.4) THEN
        IPL2=N2NDX(IRCX,2)
        CHRDIF=CHRDIF+NCHRGP(IPL2)
      ENDIF
      IF (CHRDIF.NE.0) GOTO 991
C
C  TARGET MASS IN <SIGMA*V> FORMULA: MAXW. BULK PARTICLE
C  (= PROJECTILE MASS IN CROSS SECTION MEASUREMENT: TARGET AT REST)
      PMASS=MASSP(KK)*PMASSA
C  PROJECTILE MASS IN <SIGMA*V> FORMULA: MONOENERG. TEST PARTICLE
C  (= TARGET PARTICLE IN CROSS SECTION MEASUREMENT; TARGET AT REST)
      TMASS=MASST(KK)*PMASSA
      ADDT=PMASS/RMASSP(IPL)
      ADDTL=LOG(ADDT)
      NREACX(IRCX) = KK
      ADDCX(IRCX,IPL) = ADDTL
C
C CROSS SECTION (E-LAB)
      IF (IDEZ(MODCLF(KK),2,5).EQ.1) THEN
        MODCOL(3,1,IRCX)=KK
        MODCOL(3,2,IRCX)=3
        IF (FACTKK.NE.1.D0)
     .  WRITE (iunout,*) 'FREAC NOT READY FOR CROSS SECTION IN XSTCX'
      ENDIF
C
C RATE COEFFICIENT
      MODC=IDEZ(MODCLF(KK),3,5)
      IF (MODC.GE.1.AND.MODC.LE.2) THEN
        MODCOL(3,2,IRCX)=MODC
        IF (MODC.EQ.1) NEND=1
        IF (MODC.EQ.2) NEND=NSTORDT
        IF (NSTORDR >= NRAD) THEN
          PLS=0._DP
          DO 242 J=1,NSBOX
            PLS(J)=TIINL(IPLTI,J)+ADDTL
242       CONTINUE
          IF (MODC.EQ.1) THEN
            DO 245 J=1,NSBOX
              COU = RATE_COEFF(KK,PLS(J),0._DP,.TRUE.,0)
              TABCX3(IRCX,J,1)=COU*DIIN(IPL,J)*FACTKK
245         CONTINUE
          ELSEIF (MODC.EQ.2) THEN
            FCTKKL=LOG(FACTKK)
            DO J=1,NSBOX
              CALL PREP_RTCS (KK,3,1,NEND,PLS(J),CFF)
              TABCX3(IRCX,J,1:NEND) = CFF(1:NEND)
              TABCX3(IRCX,J,1)=TABCX3(IRCX,J,1)+DIINL(IPL,J)+FCTKKL
            END DO
          END IF
        ELSE
          FACREA(KK) = LOG(FACTKK)
        END IF
      ELSE
C  NO RATE COEFFICIENT. IS THERE A CROSS SECTION AT LEAST?
        IF (MODCOL(3,2,IRCX).NE.3) GOTO 996
      ENDIF
      DEFCX(IRCX)=LOG(CVELI2*PMASS)
      EEFCX(IRCX)=LOG(CVELI2*TMASS)
C
C  3. BULK PARTICLE MOMENTUM LOSS RATE
C
C
C  4. BULK PARTICLE ENERGY LOSS RATE
C
      NSECX4=IDEZ(ISCDE,4,5)
      IF (NSECX4.EQ.0) THEN
C  4.A)  ENERGY LOSS RATE OF IMP. BULK PARTICLE = CONST.*RATECOEFF.
C        SAMPLE COLLIDING ION FROM DRIFTING MONOENERGETIC ISOTROPIC DISTRIBUTION
        IF (EBULK.LE.0.D0) THEN
          IF (NSTORDR >= NRAD) THEN
            DO J=1,NSBOX
              EPLCX3(IRCX,J,1)=1.5*TIIN(IPLTI,J)+EDRIFT(IPL,J)
            ENDDO
            NELRCX(IRCX) = -3
          ELSE
            NELRCX(IRCX) = -3
          END IF
        ELSE
          IF (NSTORDR >= NRAD) THEN
            DO 251 J=1,NSBOX
              EPLCX3(IRCX,J,1)=EBULK+EDRIFT(IPL,J)
251         CONTINUE
            NELRCX(IRCX) = -2
          ELSE
            NELRCX(IRCX) = -2
            EPLCX3(IRCX,1,1)=EBULK
          END IF
        ENDIF
        MODCOL(3,4,IRCX)=3
      ELSEIF (NSECX4.EQ.1) THEN
C  4.B) ENERGY LOSS RATE OF IMP. ION = 1.5*TI* RATECOEFF.
C       SAMPLE COLLIDING ION FROM DRIFTING MAXWELLIAN
        IF (EBULK.LE.0.D0) THEN
          IF (NSTORDR >= NRAD) THEN
            DO 252 J=1,NSBOX
              EPLCX3(IRCX,J,1)=1.5*TIIN(IPLTI,J)+EDRIFT(IPL,J)
252         CONTINUE
            NELRCX(IRCX) = -3
          ELSE
            NELRCX(IRCX) = -3
          END IF
        ELSE
          WRITE (iunout,*) 'WARNING FROM SUBR. XSTCX '
          WRITE (iunout,*) 'MODIFIED TREATMENT OF CHARGE EXCHANGE '
          WRITE (iunout,*) 'SAMPLE FROM MAXWELLIAN WITH T = ',EBULK/1.5
          WRITE (iunout,*) 'RATHER THEN WITH T = TIIN '
          CALL LEER(1)
          IF (NSTORDR >= NRAD) THEN
            DO 2511 J=1,NSBOX
              EPLCX3(IRCX,J,1)=EBULK+EDRIFT(IPL,J)
2511        CONTINUE
            NELRCX(IRCX) = -2
          ELSE
            NELRCX(IRCX) = -2
            EPLCX3(IRCX,1,1)=EBULK
          END IF
        ENDIF
        MODCOL(3,4,IRCX)=1
C     ELSEIF (NSECX4.EQ.2) THEN
C  use i-integral expressions. to be written
      ELSEIF (NSECX4.EQ.3) THEN
C  4.B)  ENERGY LOSS RATE OF IMP. ION = EN.WEIGHTED RATE
C  4.C)  ENERGY LOSS RATE OF IMP. ION = EN.WEIGHTED RATE
        KREAD=EBULK
        IF (KREAD.EQ.0) THEN
c  data for mean ion energy loss are not available
c  use collision estimator for energy balance
          IF (IDEZ(IESTM,3,3).NE.1) THEN
            WRITE (iunout,*) 
     .        'COLLISION ESTIMATOR ENFORCED FOR ION ENERGY '
            WRITE (iunout,*) 'IN CX COLLISION IRCX= ',IRCX
            WRITE (iunout,*) 'BECAUSE NO ENERGY WEIGHTED RATE AVAILABLE'
cdr         WRITE (iunout,*) 'OOPS: '
cdr         WRITE (iunout,*) 'COLLISION ESTIMATOR NOT READY '
cdr         WRITE (iunout,*) 'CALL EXIT '
cdr         CALL EXIT_OWN(1)
          ENDIF
          IESTCX(IRCX,3)=1
          MODCOL(3,4,IRCX)=2
        ELSE
C  ION ENERGY AVERAGED RATE AVAILABLE AS REACTION NO. "KREAD"
        NELRCX(IRCX) = KREAD
        MODC=IDEZ(MODCLF(KREAD),5,5)
        IF (MODC.GE.1.AND.MODC.LE.2) THEN
          MODCOL(3,4,IRCX)=MODC
          IF (MODC.EQ.1) NEND=1
          IF (MODC.EQ.2) NEND=NSTORDT
          IF (NSTORDR >= NRAD) THEN
            PLS=0._DP
            DO 253 J=1,NSBOX
              PLS(J)=TIINL(IPLTI,J)+ADDTL
253         CONTINUE
            IF (MODC.EQ.1) THEN
              ADD=FACTKK/ADDT
              DO 254 J=1,NSBOX
                EPLCX3(IRCX,J,1)=ENERGY_RATE_COEFF(KREAD,PLS(J),0._DP,
     .                           .FALSE.,0)*DIIN(IPL,J)*ADD
254           CONTINUE
            ELSEIF (MODC.EQ.2) THEN
              ADDL=LOG(FACTKK)-ADDTL
              DO 257 J=1,NSBOX
                CALL PREP_RTCS (KREAD,5,1,NEND,PLS(J),CFF)
                EPLCX3(IRCX,J,1:NEND) = CFF(1:NEND)
                EPLCX3(IRCX,J,1) = EPLCX3(IRCX,J,1)+DIINL(IPL,J)+ADDL
257           CONTINUE
            ENDIF
          ELSE
            IF (MODC.EQ.1) THEN
              ADD=FACTKK/ADDT
              EPLCX3(IRCX,1,1)=ADD
            ELSEIF (MODC.EQ.2) THEN
              ADDL=LOG(FACTKK)-ADDTL
              FACREA(KREAD) = ADDL
            END IF
          END IF
        ENDIF
        ENDIF
      ELSE
        IERR=5
        GOTO 996
      ENDIF

C  ESTIMATOR FOR CONTRIBUTION TO COLLISION RATES FROM THIS REACTION
      IESTCX(IRCX,1)=IDEZ(IESTM,1,3)
      IESTCX(IRCX,2)=IDEZ(IESTM,2,3)
      IF (IESTCX(IRCX,3).EQ.0) IESTCX(IRCX,3)=IDEZ(IESTM,3,3)
C
      ITYP1=N1STX(IRCX,1)
      ITYP2=N2NDX(IRCX,1)
      IF (IESTCX(IRCX,1).NE.0.AND.(ITYP1.NE.1.OR.ITYP2.NE.4)) THEN
        CALL LEER(1)
        WRITE (iunout,*) 
     .    'WARNING: COLL.EST NOT AVAILABLE FOR PART.-BALANCE '
        WRITE (iunout,*) 'IRCX = ',IRCX
        WRITE (iunout,*) 'AUTOMATICALLY RESET TO TRACKLENGTH ESTIMATOR '
        IESTCX(IRCX,1)=0
      ENDIF
      IF (IESTCX(IRCX,2).NE.0.AND.(ITYP1.NE.1.OR.ITYP2.NE.4)) THEN
        CALL LEER(1)
        WRITE (iunout,*) 
     .    'WARNING: COLL.EST NOT AVAILABLE FOR MOM.-BALANCE '
        WRITE (iunout,*) 'IRCX = ',IRCX
        WRITE (iunout,*) 'AUTOMATICALLY RESET TO TRACKLENGTH ESTIMATOR '
        IESTCX(IRCX,2)=0
      ENDIF
      IF (IESTCX(IRCX,3).NE.0.AND.(ITYP1.NE.1.OR.ITYP2.NE.4)) THEN
        CALL LEER(1)
        WRITE (iunout,*) 
     .    'WARNING: COLL.EST NOT AVAILABLE FOR EN.-BALANCE '
        WRITE (iunout,*) 'IRCX = ',IRCX
        WRITE (iunout,*) 'AUTOMATICALLY RESET TO TRACKLENGTH ESTIMATOR '
        IESTCX(IRCX,3)=0
      ENDIF
      RETURN
C
      ENTRY XSTCX_2(IRCX,IPL)
C
      CALL LEER(1)
      WRITE (iunout,*) 'CHARGE EXCHANGE REACTION NO. IRCX= ',IRCX
      CALL LEER(1)
      WRITE (iunout,*) 'CHARGE EXCHANGE WITH BULK IONS IPLS:'
      WRITE (iunout,*) '1ST AND 2ND NEXT GEN. SPECIES I2ND1, I2ND2:'
      ITYP1=N1STX(IRCX,1)
      ITYP2=N2NDX(IRCX,1)
      ISPZ1=N1STX(IRCX,2)
      ISPZ2=N2NDX(IRCX,2)
      IF (ITYP1.EQ.1) TEXTS1=TEXTS(NSPH+ISPZ1)
      IF (ITYP1.EQ.2) TEXTS1=TEXTS(NSPA+ISPZ1)
      IF (ITYP1.EQ.3) TEXTS1=TEXTS(NSPAM+ISPZ1)
      IF (ITYP1.EQ.4) TEXTS1=TEXTS(NSPAMI+ISPZ1)
      IF (ITYP2.EQ.1) TEXTS2=TEXTS(NSPH+ISPZ2)
      IF (ITYP2.EQ.2) TEXTS2=TEXTS(NSPA+ISPZ2)
      IF (ITYP2.EQ.3) TEXTS2=TEXTS(NSPAM+ISPZ2)
      IF (ITYP2.EQ.4) TEXTS2=TEXTS(NSPAMI+ISPZ2)
      WRITE (iunout,*) 'IPLS= ',TEXTS(NSPAMI+IPL),'I2ND1= ',TEXTS1,
     .                    'I2ND2= ',TEXTS2
      CALL LEER(1)
      RETURN
C
990   CONTINUE
      WRITE (iunout,*) 'ERROR IN XSTCX: EXIT CALLED  '
      WRITE (iunout,*) 'INVALID SPECIES INDEX FOR CHARGE EXCHANGE '
      CALL EXIT_OWN(1)
991   CONTINUE
      WRITE (iunout,*) 'ERROR IN XSTCX: EXIT CALLED  '
      WRITE (iunout,*) 'CHARGE CONSERVATION VIOLATED '
      WRITE (iunout,*) 'IRCX, TEST-SPECIES, BULK SPECIES ',IRCX,
     .                  TEXTS(ISP),TEXTS(NSPAMI+IPL)
      CALL EXIT_OWN(1)
992   CONTINUE
      WRITE (iunout,*) 'ERROR IN XSTCX: EXIT CALLED  '
      WRITE (iunout,*) 
     .  'MASS NUMBERS OF INTERACTING PARTICLES INCONSISTENT'
      WRITE (iunout,*) 'KK ',KK
      CALL EXIT_OWN(1)
993   CONTINUE
      WRITE (iunout,*) 'ERROR IN XSTCX: EXIT CALLED  '
      WRITE (iunout,*) 
     .  'EBULK_ION .LE.0, BUT MONOENERGETIC DISTRIBUTION?'
      WRITE (iunout,*) 'CHECK ENERGY FLAG ISCDEA'
      WRITE (iunout,*) 'KK,ISCDEA ',KK,ISCDEA
      CALL EXIT_OWN(1)
996   CONTINUE
      WRITE (iunout,*) 'ERROR IN XSTCX: EXIT CALLED  '
      WRITE (iunout,*) 'NO CROSS SECTION AVAILABLE FOR NON DEFAULT CX'
      WRITE (iunout,*) 'KK ',KK
      WRITE (iunout,*) 'EITHER PROVIDE CROSS SECTION OR USE DIFFERENT '
      WRITE (iunout,*) 'POST COLLISION SAMPLING FLAG ISCDEA'
      CALL EXIT_OWN(1)
      END
