C
C======================================================================
      SUBROUTINE VCROS( REQ , ACT , IER )
      IMPLICIT REAL*8(A-H,O-Z)
C
      CHARACTER*9 VERSON
      PARAMETER( VERSON = '21-APR-98' )
C
C-----------------------------------------------------------------------
C
C PURPOSE : TO OBTAIN THE VERSION OF THE 'CROSSEC' MODULE.
C
C INPUT   : (C*9) REQ    = REQUIRED VERSION.
C
C OUTPUT  : (C*9) ACT    = ACTUAL VERSION.
C           (I*4) IER    = 0 IF REQ =  ACT
C                        = 1 IF REQ <> ACT
C
C PROGRAM : (C*9) VERSON = VERSION
C
C AUTHOR  : JAMES SPENCE (K1/0/80)  EXT. 4866
C           JET
C
C DATE    : 09-DEC-91
C
C-----------------------------------------------------------------------
C
      CHARACTER*9   REQ , ACT
      INTEGER*4     IER
C
C.......................................................................
C
      ACT = VERSON
C
C.......................................................................
C
      IF( REQ.EQ.ACT ) THEN
          IER = 0
      ELSE
          IER = 1
      END IF
C
C.......................................................................
C
      RETURN
      END
C
C======================================================================
      SUBROUTINE AUROXS(AION,TE,TI,ANEUT,ENEUT, SVION,SVCX,SIGIO,SIGCX,
     &SMION,SMDIS,SVREC)
C
C     CROSS SECTIONS AND REACTION RATES FOR CHARGE-EXCHANGE AND
C     IONISATION BETWEEN H AND H+ AS IMPLEMENTED IN THE 'AURORA' CODE
C     ADDITIONS - JULY 17TH 1989 : H2 IONISATION
C               - JULY 20TH 1989 : H2 DISSOCIATION
C
C     INPUT:
C             AION = (H+)-ION MASS (AMU)
C               TE = (ELECTRON TEMPERATURE (EV)
C               TI = (H+)-PLASMA TEMPERATURE (EV)
C            ANEUT = (H)-NEUTRAL MASS (AMU)
C            ENEUT = (H)-NEUTRAL ENERGY (EV)
C     OUTPUT:
C            SVION = ATOM. IONISATION RATE COEFFICIENT (CM(+3)SEC(-1))
C             SVCX = CHARGE-EXCHANGE RATE COEFFICIENT (CM(+3)SEC(-1))
C            SIGIO = IONISATION CROSS-SECTION (CM(+2))
C            SIGCX = CHARGE EXCHANGE CROSS SECTION (CM(+2))
C            SMION = H2. IONISATION RATE COEFFICIENT (CM(+3)SEC(-1))
C            SMDIS = DISSOCIATION OF H2 RATE COEFF   (CM(+3)SEC(-1))
C            SVREC = H+ -RECOMBINATION  RATE COEFF   (CM(+3)SEC(-1))
C
      IMPLICIT REAL*8(A-H,O-Z)
C
      DATA AMU/1.6726E-24/
      DATA ECUT/0.0E0/
C
      TEMPE=TE
      TEMPI=TI
      E0=ENEUT
      IF(E0.LE.ECUT) E0=ECUT
C
      RMASN=ANEUT*AMU
      VEL=DSQRT(E0*3.2E-12/RMASN)
      IF(VEL.LE.0.0D0) VEL=1.0D0
      RMASI=AION*AMU
      VREL2=1.6021E-12 * 8.0*TEMPI/(RMASI*3.14159) + VEL**2
      VREL=SQRT(VREL2)
C           RELATIVE ENERGY FOR H,H+ REACTIONS
      EREL=5.22E-13 * VREL2
C
C     IONISATION X.S. (DUECHS)
      X=DLOG10(TEMPE)
      IF(TEMPE.GT.20.) GOTO 10
      ZLOG = -3.054 * X
     *       -15.72 * DEXP(-X)
     *       +1.603 * DEXP(-X*X)
      GOTO 20
   10 ZLOG =-0.5151*X-2.563/X-5.231
   20 SVION=10.0**ZLOG
C
C     CHARGE-EXCHANGE X.S.
      X=DLOG(EREL)
      IF(EREL.GT.200.0) GOTO 30
C        GREENLAND, 1984
      SIGX=4.549E-15-0.4486E-15*X
      GOTO 40
C        RIVIERE
   30 SIGX=0.6937 * (1.-0.0673*X)**2 / (1.0E+14 + 0.1112 *DEXP(3.3*X) )
C:
   40 SVCX=SIGX*VREL
C
C     H2 IONISATION (JONES)
   50 CALL MOLION( TEMPE , SMION , IER )
C
C     H2 DISSOCIATION (JONES)
   60 CALL MOLDIS( TEMPE , SMDIS , IER )
C
C
C     RECOMBINATION:
C
      TEMPE1 = TEMPE*1.0E-03
      IF(TEMPE1.LE.4.0) THEN
        SVREC = 1.48E-14/(SQRT(TEMPE1)*(1.0 + 43.4*TEMPE1))
      ELSE
        SVREC = 3.58E-16/TEMPE1**1.388
      ENDIF
C
      IF(TEMPE.LT.ECUT .OR. TEMPI.LT.ECUT) THEN
        SVION = 0.0D0
        SVCX  = 0.0D0
        SMION = 0.0D0
        SMDIS = 0.0D0
        SVREC = 0.0D0
      ENDIF
C
      SIGIO=SVION/VEL
      SIGCX=SVCX/VEL
C
      RETURN
      END
C
C=======================================================================
       SUBROUTINE MOLION( EVR4 , RATER4 , IER )
       IMPLICIT REAL*8(A-H,O-Z)
C
C-----------------------------------------------------------------------
C      PURPOSE : TO CALCULATE THE RATE COEFFICIENT FOR THE REACTION -
C                             H2 + E ---> (H+)2 + 2E
C                I.E THE IONISATION OF H2
C      INPUT   : EVR4   = ELECTRON ENERGY (EV)        (REAL*4)
C      OUTPUT  : RATER4 = RATE COEFFICIENT ( CM3/SEC) (REAL*4)
C      NOTES   : IER  = 0 IF SUCCESSFUL
C                IER  = 1 IF ELECTRON ENERGY < 1.0E+00 EV
C                IER  = 2 IF ELECTRON ENERGY > 1.0E+06 EV
C                RATE = EXP( A0 + A1*LOG(EV) + ... + AN*(LOG(EV)**N) )
C      WARNING : IF IER = 1 THEN RATE = RATE FOR 1.0E+00 EV
C                IF IER = 2 THEN RATE = RATE FOR 1.0E+06 EV
C      REFER.  : E.M.JONES, CULHAM REPORT CUL-R175 (1977)
C      AUTHOR  : J. SPENCE
C      DATE    : 17/07/89
C-----------------------------------------------------------------------
C
       PARAMETER ( IVALUE = 9 )
       DIMENSION AVALUE(IVALUE) , ERANGE(2)
C
C-----------------------------------------------------------------------
C
C *** 'A' COEFFICIENTS FOR RANGE  ***
       DATA AVALUE/ -3.423879725E+01
     1            , +1.614828920E+01
     2            , -7.646064171E+00
     3            , +2.372854440E+00
     4            , -4.906446367E-01
     5            , +6.507807927E-02
     6            , -5.235834368E-03
     7            , +2.302401724E-04
     8            , -4.222021965E-06
     &            /
C *** ENERGY RANGES ***
       DATA ERANGE/ 1.00D+00
     2            , 1.00D+06
     &            /
C
C-----------------------------------------------------------------------
C
       EV  = EVR4
       EEV = EV
       IER = 0
C
C------FIND IN WHICH RANGE THE ENERGY (EEV) LIES------------------------
C
       IF( EEV .LT. ERANGE(1) ) THEN
                                IER  = 1
                                EEV  = ERANGE(1)
       END IF
C
       IF( EEV .GT. ERANGE(2) ) THEN
                                IER  = 2
                                EEV  = ERANGE(2)
       END IF
C
C------CALCULATE THE APPROPRIATE POLYNOMIAL---------------------------
C
C
       EEVJ = 1.0D+00
       SUM  = 0.0D+00
C
       DO 100 I = 1 , IVALUE
          IF( I .GT. 1 )  EEVJ = EEVJ * DLOG(EEV)
          SUM = SUM + AVALUE(I)*EEVJ
  100  CONTINUE
C
C------CALCULATE THE RATE COEFFICIENT --------------------------------
C
       RATE   = EXP(SUM)
       RATER4 = RATE
C
C---------------------------------------------------------------------
C
       RETURN
       END
C
C=======================================================================
       SUBROUTINE MOLDIS( EVR4 , RATER4 , IER )
       IMPLICIT REAL*8(A-H,O-Z)
C
C-----------------------------------------------------------------------
C      PURPOSE : TO CALCULATE THE RATE COEFFICIENT FOR THE REACTION -
C                             H2 + E ---> 2H + E
C                I.E THE DISSOCIATION OF H2
C      INPUT   : EVR4   = ELECTRON ENERGY (EV)        (REAL*4)
C      OUTPUT  : RATER4 = RATE COEFFICIENT ( CM3/SEC) (REAL*4)
C      NOTES   : IER  = 0 IF SUCCESSFUL
C                IER  = 1 IF ELECTRON ENERGY < 1.0E+00 EV
C                IER  = 2 IF ELECTRON ENERGY > 2.0E+03 EV
C                RATE = EXP( A0 + A1*LOG(EV) + ... + AN*(LOG(EV)**N) )
C      WARNING : IF IER = 1 THEN RATE = RATE FOR 1.0E+00 EV
C                IF IER = 2 THEN RATE = RATE FOR 2.0E+03 EV
C      REFER.  : E.M.JONES, CULHAM REPORT CUL-R175 (1977)
C      AUTHOR  : J. SPENCE
C      DATE    : 20/07/89
C-----------------------------------------------------------------------
C
       PARAMETER ( IVALUE = 9 )
       DIMENSION AVALUE(IVALUE) , ERANGE(2)
C
C-----------------------------------------------------------------------
C
C *** 'A' COEFFICIENTS FOR RANGE  ***
       DATA AVALUE/ -2.665989317E+01
     1            , +9.312340730E+00
     2            , -5.315731531E+00
     3            , +2.551587608E+00
     4            , -9.873073379E-01
     5            , +2.490946830E-01
     6            , -3.733208732E-02
     7            , +3.016027718E-03
     8            , -1.015095353E-04
     &            /
C *** ENERGY RANGES ***
       DATA ERANGE/ 1.00D+00
     2            , 2.00D+03
     &            /
C
C-----------------------------------------------------------------------
C
       EV  = EVR4
       EEV = EV
       IER = 0
C
C------FIND IN WHICH RANGE THE ENERGY (EEV) LIES------------------------
C
       IF( EEV .LT. ERANGE(1) ) THEN
                                IER  = 1
                                EEV  = ERANGE(1)
       END IF
C
       IF( EEV .GT. ERANGE(2) ) THEN
                                IER  = 2
                                EEV  = ERANGE(2)
       END IF
C
C------CALCULATE THE APPROPRIATE POLYNOMIAL---------------------------
C
C
       EEVJ = 1.0D+00
       SUM  = 0.0D+00
C
       DO 100 I = 1 , IVALUE
          IF( I .GT. 1 )  EEVJ = EEVJ * DLOG(EEV)
          SUM = SUM + AVALUE(I)*EEVJ
  100  CONTINUE
C
C------CALCULATE THE RATE COEFFICIENT --------------------------------
C
       RATE   = EXP(SUM)
       RATER4 = RATE
C
C---------------------------------------------------------------------
C
       RETURN
       END
C
C=======================================================================
       SUBROUTINE IONIZ( ZMASS , TEMPE , RATCOF , IER )
       IMPLICIT REAL*8(A-H,O-Z)
C
C-----------------------------------------------------------------------
C      PURPOSE : TO CALCULATE THE IONISATION RATE COEFFICIENT OF
C                AN ION SPECIES.
C      INPUT   : ZMASS   = ION SPECIES ATOMIC WEIGHT (G MOL-1)
C                TEMPE   = TEMPERATURE TO BE PROCESSED.
C      OUTPUT  : RATCOF  = IONIZ. RATE COEFF. FOR INPUT TEMPERATURE
C                IER     = 0 IF OPERATION SUCCESSFUL
C                          1 IF ION SPECIES NOT FOUND
C                          2 IF ION SPECIES HAS NO SUB-SHELLS
C                          3 IF ION SPECIES ATOMIC WEIGHTS OVERLAP
C      INDEX   : IZ      =  1 ---- ( NOT AVAILABLE ) -- ATOMWT =   N/A
C                IZ      =  2 ---- H                 -- ATOMWT =   1.01
C                IZ      =  3 ---- D                 -- ATOMWT =   2.02
C                IZ      =  4 ---- DT                -- ATOMWT =   2.53
C                IZ      =  5 ---- T                 -- ATOMWT =   3.03
C                IZ      =  6 ---- ( NOT AVAILABLE ) -- ATOMWT =   N/A
C                IZ      =  7 ---- HE                -- ATOMWT =   4.00
C                IZ      =  8 ---- FE                -- ATOMWT =  55.85
C                IZ      =  9 ---- CU                -- ATOMWT =  63.55
C                IZ      = 10 ---- C                 -- ATOMWT =  12.01
C                IZ      = 11 ---- MO                -- ATOMWT =  95.94
C                IZ      = 12 ---- NI                -- ATOMWT =  58.71
C                IZ      = 13 ---- W                 -- ATOMWT = 183.85
C                IZ      = 14 ---- AL                -- ATOMWT =  26.98
C                IZ      = 15 ---- AU                -- ATOMWT = 196.97
C                IZ      = 16 ---- BE                -- ATOMWT =   9.01
C                IZ      = 17 ---- SI                -- ATOMWT =  28.09
C                IZ      = 18 ---- TA                -- ATOMWT = 180.95
C                IZ      = 19 ---- TI                -- ATOMWT =  47.90
C                IZ      = 20 ---- V                 -- ATOMWT =  50.94
C                IZ      = 21 ---- ZR                -- ATOMWT =  91.22
C                IZ      = 22 ---- CR                -- ATOMWT =  52.00
C                IZ      = 23 ---- O                 -- ATOMWT =  16.00
C                IZ      = 24 ---- ( NOT AVAILABLE ) -- ATOMWT =   N/A
C                IZ      = 25 ---- ( NOT AVAILABLE ) -- ATOMWT =   N/A
C                IZ      = 26 ---- ( NOT AVAILABLE ) -- ATOMWT =   N/A
C                IZ      = 27 ---- ( NOT AVAILABLE ) -- ATOMWT =   N/A
C                IZ      = 28 ---- ( NOT AVAILABLE ) -- ATOMWT =   N/A
C                IZ      = 29 ---- ( NOT AVAILABLE ) -- ATOMWT =   N/A
C                IZ      = 30 ---- ( NOT AVAILABLE ) -- ATOMWT =   N/A
C      SELECT  : IZ IS SELECTED BY COMPARING ATOMWT WITH ZMASS SUCH THAT
C                     (ATOMWT(IZ)-0.2) <= ZMASS <= (ATOMWT(IZ)+0.2)
C      PROGRAM : IZOK()  = 0 THEN ION SPECIES IS NOT AVAILABLE
C                          1 THEN ION SPECIES USES THE BELL FORMULAE
C                          2 THEN ION SPECIES USES THE LOTZ FORMULAE
C      NOTE    : WHERE POSSIBLE THE BELL FORMULAE ARE USED IN PREFERENCE
C                TO THE LOTZ FORMULAE.
C      AUTHOR  : J.SPENCE
C      DATE    : 21/07/89
C-----------------------------------------------------------------------
C
       PARAMETER ( IZMAX = 30 )
C
       DIMENSION IZOK(IZMAX)     , ATOMWT(IZMAX)   , BUFFER(10000)
       DIMENSION TBELL(IZMAX,26) , TLOTZ(IZMAX,26)
C
       LOGICAL LDONE
C
       SAVE
C
       DATA LDONE/.FALSE./
C
       DATA IZOK  /   0    ,   1    ,   1    ,   1    ,   1    ,
     &                0    ,   1    ,   1    ,   2    ,   1    ,
     &                1    ,   1    ,   1    ,   1    ,   2    ,
     &                1    ,   1    ,   1    ,   1    ,   1    ,
     &                1    ,   1    ,   1    ,   0    ,   0    ,
     &                0    ,   0    ,   0    ,   0    ,   0    /
C
       DATA ATOMWT/   0.00 ,   1.01 ,   2.02 ,   2.53 ,   3.03 ,
     &                0.00 ,   4.00 ,  55.85 ,  63.55 ,  12.01 ,
     &               95.94 ,  58.71 , 183.85 ,  26.98 , 196.97 ,
     &                9.01 ,  28.09 , 180.95 ,  47.90 ,  50.94 ,
     &               91.22 ,  52.00 ,  16.00 ,   0.00 ,   0.00 ,
     &                0.00 ,   0.00 ,   0.00 ,   0.00 ,   0.00 /
C
C------INITIALISE RATE COEFFICIENT--------------------------------------
C
       IER = 0
C
       RATCOF = 0.0
C
C------THROW OUT ZMASS <= 0.0-------------------------------------------
C
       IF( ZMASS.LE.0.0 ) THEN
                          IER = 1
                          GOTO 9999
       END IF
C
C------OBTAIN REQUESTED ION SPECIES AND CHECK FOR ATOMWT OVERLAP--------
C
       IZ       = 0
       ATINC    = 0.2
       DO 200 I = 1 , IZMAX
          ATMIN = ATOMWT(I) - ATINC
          ATMAX = ATOMWT(I) + ATINC
          IF( ZMASS.GE.ATMIN .AND. ZMASS.LE.ATMAX ) THEN
                                                    IF( IZ.NE.0 ) THEN
                                                        IER = 3
                                                        GOTO 9999
                                                    ELSE
                                                        IZ = I
                                                    END IF
          END IF
  200  CONTINUE
C
C------CHECK REQUESTED ION SPECIES--------------------------------------
C
       IF( IZ.EQ.0 ) THEN
                     IER = 1
                     GOTO 9999
       END IF
C
       IF( IZOK(IZ).EQ.0 ) THEN
                           IER = 1
                           GOTO 9999
       END IF
C
C------INITIALISE DATA--------------------------------------------------
C
       IF(LDONE) GOTO 10
       LDONE = .TRUE.
C
       DO 400 I = 1,30
          DO 300 K = 1 , 26
             TBELL(I,K) = 0.0
             TLOTZ(I,K) = 0.0
  300     CONTINUE
  400  CONTINUE
C
C------BELL COEFFICIENTS-------------------------------------------
C
C                               H
      TBELL(2,1)=13.6
      TBELL(2,2)= 6.0
      TBELL(2,3)= 2.3742E-08
      TBELL(2,4)=-3.6866E-09
      TBELL(2,5)=-1.0366E-08
      TBELL(2,6)=-3.8010E-09
      TBELL(2,7)= 3.4159E-09
      TBELL(2,8)= 1.6834E-09
      TBELL(2,9)= 2.4617E-08
      TBELL(2,10)= 3.0
      TBELL(2,11)= 9.5987E-08
      TBELL(2,12)=-9.2464E-07
      TBELL(2,13)= 3.9974E-06
C                               D
      TBELL(3,1)=13.6
      TBELL(3,2)= 6.0
      TBELL(3,3)= 2.3742E-08
      TBELL(3,4)=-3.6866E-09
      TBELL(3,5)=-1.0366E-08
      TBELL(3,6)=-3.8010E-09
      TBELL(3,7)= 3.4159E-09
      TBELL(3,8)= 1.6834E-09
      TBELL(3,9)= 2.4617E-08
      TBELL(3,10)= 3.0
      TBELL(3,11)= 9.5987E-08
      TBELL(3,12)=-9.2464E-07
      TBELL(3,13)= 3.9974E-06
C                               DT
      TBELL(4,1)=13.6
      TBELL(4,2)= 6.0
      TBELL(4,3)= 2.3742E-08
      TBELL(4,4)=-3.6866E-09
      TBELL(4,5)=-1.0366E-08
      TBELL(4,6)=-3.8010E-09
      TBELL(4,7)= 3.4159E-09
      TBELL(4,8)= 1.6834E-09
      TBELL(4,9)= 2.4617E-08
      TBELL(4,10)= 3.0
      TBELL(4,11)= 9.5987E-08
      TBELL(4,12)=-9.2464E-07
      TBELL(4,13)= 3.9974E-06
C                               T
      TBELL(5,1)=13.6
      TBELL(5,2)= 6.0
      TBELL(5,3)= 2.3742E-08
      TBELL(5,4)=-3.6866E-09
      TBELL(5,5)=-1.0366E-08
      TBELL(5,6)=-3.8010E-09
      TBELL(5,7)= 3.4159E-09
      TBELL(5,8)= 1.6834E-09
      TBELL(5,9)= 2.4617E-08
      TBELL(5,10)= 3.0
      TBELL(5,11)= 9.5986E-08
      TBELL(5,12)=-9.2464E-07
      TBELL(5,13)= 3.9974E-06
C                               HE
      TBELL(7,1)=24.6
      TBELL(7,2)= 6.0
      TBELL(7,3)= 1.4999E-08
      TBELL(7,4)= 5.6657E-10
      TBELL(7,5)=-6.0822E-09
      TBELL(7,6)=-3.5894E-09
      TBELL(7,7)= 1.5529E-09
      TBELL(7,8)= 1.3207E-09
      TBELL(7,9)= 3.1373E-08
      TBELL(7,10)= 3.0
      TBELL(7,11)= 4.7893E-08
      TBELL(7,12)=-7.7361E-07
      TBELL(7,13)= 3.7367E-06
C                               FE
      TBELL(8,1)= 7.9
      TBELL(8,2)= 6.0
      TBELL(8,3)= 1.4438E-07
      TBELL(8,4)=-8.0018E-08
      TBELL(8,5)=-6.1752E-08
      TBELL(8,6)= 5.6502E-08
      TBELL(8,7)= 1.2350E-08
      TBELL(8,8)=-1.7668E-08
      TBELL(8,9)= 3.4615E-07
      TBELL(8,10)= 3.0
      TBELL(8,11)=-4.3391E-07
      TBELL(8,12)= 1.7618E-06
      TBELL(8,13)=-5.3387E-06
C                               C
      TBELL(10,1)=11.3
      TBELL(10,2)= 6.0
      TBELL(10,3)= 5.9849E-08
      TBELL(10,4)= 1.1903E-08
      TBELL(10,5)=-3.0141E-08
      TBELL(10,6)=-1.3693E-08
      TBELL(10,7)= 8.3749E-09
      TBELL(10,8)= 4.0150E-09
      TBELL(10,9)= 3.7442E-07
      TBELL(10,10)= 3.0
      TBELL(10,11)=-6.5826E-07
      TBELL(10,12)= 2.0520E-06
      TBELL(10,13)=-4.4688E-06
C                               MO
C     WE ASSUME THE RATE COEFFICIENT OF MO TO BE EQUAL TO THAT OF CR
C     (SIMILAR OUTER SHELL CONFIGURATION) MULTIPLIED BY Z(MO)/Z(CR)=1.8
C     TO THIS END BELL'S COEFFICIENTS OF CR WERE MULTIPLIED BY 1.8
      TBELL(11,1)= 6.8
      TBELL(11,2)= 6.0
      TBELL(11,3)= 3.1457E-07
      TBELL(11,4)=-1.7081E-07
      TBELL(11,5)=-7.0447E-08
      TBELL(11,6)= 3.9663E-08
      TBELL(11,7)= 1.8976E-08
      TBELL(11,8)=-8.1922E-09
      TBELL(11,9)= 7.0412E-07
      TBELL(11,10)= 3.0
      TBELL(11,11)=-5.9762E-07
      TBELL(11,12)= 8.3650E-07
      TBELL(11,13)= 1.3167E-06
C                               NI
      TBELL(12,1)= 7.6
      TBELL(12,2)= 6.0
      TBELL(12,3)= 1.1655E-07
      TBELL(12,4)=-3.9394E-08
      TBELL(12,5)=-6.9896E-08
      TBELL(12,6)= 3.4807E-08
      TBELL(12,7)= 1.7806E-08
      TBELL(12,8)=-1.1861E-08
      TBELL(12,9)= 2.6366E-07
      TBELL(12,10)= 3.0
      TBELL(12,11)=-2.0322E-07
      TBELL(12,12)= 2.7985E-07
      TBELL(12,13)= 2.6778E-07
C                               W
C     WE ASSUME THE RATE COEFFICIENT OF W TO BE EQUAL TO THAT OF TI
C     (SIMILAR OUTER SHELL CONFIGURATION) MULTIPLIED BY Z(W)/Z(TI)=3.36
C     TO THIS END BELL'S COEFFICIENTS OF TI WERE MULTIPLIED BY 3.36
      TBELL(13,1)= 6.8
      TBELL(13,2)= 6.0
      TBELL(13,3)= 7.7404E-07
      TBELL(13,4)=-6.1300E-07
      TBELL(13,5)= 1.0011E-09
      TBELL(13,6)= 5.7886E-09
      TBELL(13,7)= 9.9338E-08
      TBELL(13,8)=-2.7487E-08
      TBELL(13,9)= 1.0445E-06
      TBELL(13,10)= 3.0
      TBELL(13,11)=-2.4467E-07
      TBELL(13,12)=-1.5803E-07
      TBELL(13,13)= 2.3679E-06
C                               AL
      TBELL(14,1)= 6.0
      TBELL(14,2)= 6.0
      TBELL(14,3)= 3.0764E-07
      TBELL(14,4)=-2.8504E-07
      TBELL(14,5)= 1.0518E-07
      TBELL(14,6)=-2.1274E-08
      TBELL(14,7)=-2.5008E-08
      TBELL(14,8)= 2.0889E-08
      TBELL(14,9)= 5.3461E-07
      TBELL(14,10)= 3.0
      TBELL(14,11)=-3.3911E-07
      TBELL(14,12)=-8.2783E-08
      TBELL(14,13)= 4.4186E-06
C                               BE
      TBELL(16,1)= 9.3
      TBELL(16,2)= 6.0
      TBELL(16,3)= 7.4206E-08
      TBELL(16,4)=-1.5520E-08
      TBELL(16,5)=-3.9403E-08
      TBELL(16,6)= 7.2155E-09
      TBELL(16,7)= 1.1098E-08
      TBELL(16,8)=-2.5501E-09
      TBELL(16,9)= 2.1732E-07
      TBELL(16,10)= 3.0
      TBELL(16,11)=-2.1648E-07
      TBELL(16,12)= 2.8113E-07
      TBELL(16,13)= 5.3070E-07
C                               SI
      TBELL(17,1)= 8.2
      TBELL(17,2)= 6.0
      TBELL(17,3)= 2.5653E-07
      TBELL(17,4)=-2.1940E-07
      TBELL(17,5)= 5.2700E-08
      TBELL(17,6)=-2.9152E-10
      TBELL(17,7)=-1.2481E-08
      TBELL(17,8)= 9.9184E-09
      TBELL(17,9)= 4.5234E-07
      TBELL(17,10)= 3.0
      TBELL(17,11)=-2.8674E-07
      TBELL(17,12)= 2.6614E-08
      TBELL(17,13)= 3.0515E-06
C                               TA
C     WE ASSUME THE RATE COEFFICIENT OF TA TO BE EQUAL TO THAT OF TI
C     (SIMILAR OUTER SHELL CONFIGURATION)MULTIPLIED BY Z(TA)/Z(TI)=3.32
C     TO THIS END BELL'S COEFFICIENTS OF TI WERE MULTIPLIED BY 3.32
      TBELL(18,1)= 6.8
      TBELL(18,2)= 6.0
      TBELL(18,3)= 7.6483E-07
      TBELL(18,4)=-6.0570E-07
      TBELL(18,5)= 9.8922E-10
      TBELL(18,6)= 5.7197E-09
      TBELL(18,7)= 9.8156E-08
      TBELL(18,8)=-2.7160E-08
      TBELL(18,9)= 1.0321E-06
      TBELL(18,10)= 3.0
      TBELL(18,11)=-2.4200E-07
      TBELL(18,12)=-1.5615E-07
      TBELL(18,13)= 2.3397E-06
C                               TI
      TBELL(19,1)= 6.8
      TBELL(19,2)= 6.0
      TBELL(19,3)= 2.3037E-07
      TBELL(19,4)=-1.8244E-07
      TBELL(19,5)= 2.9796E-10
      TBELL(19,6)= 1.7228E-09
      TBELL(19,7)= 2.9565E-08
      TBELL(19,8)=-8.1807E-09
      TBELL(19,9)= 3.1088E-07
      TBELL(19,10)= 3.0
      TBELL(19,11)=-7.2891E-08
      TBELL(19,12)=-4.7033E-08
      TBELL(19,13)= 7.0474E-07
C                               V
      TBELL(20,1)= 6.7
      TBELL(20,2)= 6.0
      TBELL(20,3)= 6.1981E-07
      TBELL(20,4)=-8.2488E-08
      TBELL(20,5)=-1.1598E-07
      TBELL(20,6)=-1.3021E-07
      TBELL(20,7)=-3.7206E-08
      TBELL(20,8)= 1.6844E-08
      TBELL(20,9)= 1.3253E-06
      TBELL(20,10)= 3.0
      TBELL(20,11)=-6.5540E-07
      TBELL(20,12)=-2.2345E-07
      TBELL(20,13)= 7.8098E-06
C                               ZR
C     WE ASSUME THE RATE COEFFICIENT OF ZR TO BE EQUAL TO THAT OF TI
C     (SIMILAR OUTER SHELL CONFIGURATION)MULTIPLIED BY Z(ZR)/Z(TI)=1.82
C     TO THIS END BELL'S COEFFICIENTS OF TI WERE MULTIPLIED BY 1.82
      TBELL(21,1)= 6.8
      TBELL(21,2)= 6.0
      TBELL(21,3)= 4.1927E-07
      TBELL(21,4)=-3.3204E-07
      TBELL(21,5)= 5.4229E-10
      TBELL(21,6)= 3.1355E-09
      TBELL(21,7)= 5.3808E-08
      TBELL(21,8)=-1.4889E-08
      TBELL(21,9)= 5.6580E-07
      TBELL(21,10)= 3.0
      TBELL(21,11)=-1.3266E-07
      TBELL(21,12)=-8.5600E-08
      TBELL(21,13)= 1.2826E-06
C                               CR
      TBELL(22,1)= 6.8
      TBELL(22,2)= 6.0
      TBELL(22,3)= 1.7476E-07
      TBELL(22,4)=-9.4892E-08
      TBELL(22,5)=-3.9137E-08
      TBELL(22,6)= 2.2035E-08
      TBELL(22,7)= 1.0542E-08
      TBELL(22,8)=-4.5512E-09
      TBELL(22,9)= 3.9118E-07
      TBELL(22,10)= 3.0
      TBELL(22,11)=-3.3201E-07
      TBELL(22,12)= 4.6472E-07
      TBELL(22,13)= 7.3151E-07
C                               O
      TBELL(23,1)=13.62
      TBELL(23,2)= 6.0
      TBELL(23,3)= 3.3559E-08
      TBELL(23,4)= 1.3449E-08
      TBELL(23,5)=-6.7112E-09
      TBELL(23,6)=-1.9976E-08
      TBELL(23,7)= 1.6214E-09
      TBELL(23,8)= 6.5852E-09
      TBELL(23,9)= 3.2721E-07
      TBELL(23,10)= 3.0
      TBELL(23,11)=-6.7484E-07
      TBELL(23,12)= 2.3938E-06
      TBELL(23,13)=-6.0438E-06
C
C------LOTZ COEFFICIENTS-------------------------------------------
C
C                              H+
      TLOTZ(2,1)=1.0
      TLOTZ(2,2)=13.6
      TLOTZ(2,3)=4.0E-14
      TLOTZ(2,4)=0.6
      TLOTZ(2,5)=0.56
      TLOTZ(2,6)=1.0
C                              D
      TLOTZ(3,1)=1.0
      TLOTZ(3,2)=13.6
      TLOTZ(3,3)=4.0E-14
      TLOTZ(3,4)=0.6
      TLOTZ(3,5)=0.56
      TLOTZ(3,6)=1.0
C                              DT
      TLOTZ(4,1)=1.0
      TLOTZ(4,2)=13.6
      TLOTZ(4,3)=4.0E-14
      TLOTZ(4,4)=0.6
      TLOTZ(4,5)=0.56
      TLOTZ(4,6)=1.0
C                              T
      TLOTZ(5,1)=1.0
      TLOTZ(5,2)=13.6
      TLOTZ(5,3)=4.0E-14
      TLOTZ(5,4)=0.6
      TLOTZ(5,5)=0.56
      TLOTZ(5,6)=1.0
C                              HE
      TLOTZ(7,1)=1.0
      TLOTZ(7,2)=24.6
      TLOTZ(7,3)=4.0E-14
      TLOTZ(7,4)=0.75
      TLOTZ(7,5)=0.46
      TLOTZ(7,6)=2.0
C                              FE
      TLOTZ(8,1)=3.0
      TLOTZ(8,2)=7.87
      TLOTZ(8,3)=4.0E-14
      TLOTZ(8,4)=0.4
      TLOTZ(8,5)=0.6
      TLOTZ(8,6)=2.0
      TLOTZ(8,7)=9.0
      TLOTZ(8,8)=2.6E-14
      TLOTZ(8,9)=0.92
      TLOTZ(8,10)=0.19
      TLOTZ(8,11)=6.0
      TLOTZ(8,12)=59.0
      TLOTZ(8,13)=4.0E-14
      TLOTZ(8,14)=0.6
      TLOTZ(8,15)=0.4
      TLOTZ(8,16)=6.0
C                              CU
      TLOTZ(9,1)=3.0
      TLOTZ(9,2)=7.73
      TLOTZ(9,3)=4.0E-14
      TLOTZ(9,4)=0.0
      TLOTZ(9,5)=0.0
      TLOTZ(9,6)=1.0
      TLOTZ(9,7)=10.4
      TLOTZ(9,8)=2.2E-14
      TLOTZ(9,9)=0.95
      TLOTZ(9,10)=0.16
      TLOTZ(9,11)=10.0
      TLOTZ(9,12)=80.0
      TLOTZ(9,13)=4.0E-14
      TLOTZ(9,14)=0.6
      TLOTZ(9,15)=0.4
      TLOTZ(9,16)=6.0
C                              C
      TLOTZ(10,1)=2.0
      TLOTZ(10,2)=11.3
      TLOTZ(10,3)=3.5E-14
      TLOTZ(10,4)=0.7
      TLOTZ(10,5)=0.4
      TLOTZ(10,6)=2.0
      TLOTZ(10,7)=16.6
      TLOTZ(10,8)=4.0E-14
      TLOTZ(10,9)=0.7
      TLOTZ(10,10)=0.5
      TLOTZ(10,11)=2.0
C                              MO
C     WE ASSUME THE RATE COEFFICIENT OF MO TO BE EQUAL TO THAT OF CR
C     (SIMILAR OUTER SHELL CONFIGURATION) MULTIPLIED BY Z(MO)/Z(CR)=1.8.
C     TO THIS END LOTZ'S A(I) COEFFICIENTS OF CR WERE MULTIPLIED BY 1.8.
      TLOTZ(11,1)=3.0
      TLOTZ(11,2)=6.77
      TLOTZ(11,3)=7.2E-14
      TLOTZ(11,4)=0.0
      TLOTZ(11,5)=0.0
      TLOTZ(11,6)=1.0
      TLOTZ(11,7)=8.25
      TLOTZ(11,8)=4.86E-14
      TLOTZ(11,9)=0.9
      TLOTZ(11,10)=0.2
      TLOTZ(11,11)=5.0
      TLOTZ(11,12)=48.0
      TLOTZ(11,13)=7.2E-14
      TLOTZ(11,14)=0.6
      TLOTZ(11,15)=0.4
      TLOTZ(11,16)=6.0
C                              NI
      TLOTZ(12,1)=3.0
      TLOTZ(12,2)=8.68
      TLOTZ(12,3)=4.0E-14
      TLOTZ(12,4)=0.4
      TLOTZ(12,5)=0.6
      TLOTZ(12,6)=2.0
      TLOTZ(12,7)=10.0
      TLOTZ(12,8)=2.4E-14
      TLOTZ(12,9)=0.94
      TLOTZ(12,10)=0.17
      TLOTZ(12,11)=8.0
      TLOTZ(12,12)=73.0
      TLOTZ(12,13)=4.0E-14
      TLOTZ(12,14)=0.6
      TLOTZ(12,15)=0.4
      TLOTZ(12,16)=6.0
C                              W
C     WE ASSUME THE RATE COEFFICIENT OF W TO BE EQUAL TO THAT OF V
C     (SIMILAR OUTER SHELL CONFIGURATION) MULTIPLIED BY MASS(W)/MASS(V)=
C     =3.61. TO THIS END LOTZ'S A(I) COEFFICIENTS OF V WERE MULTIPLIED
C     BY 3.61
      TLOTZ(13,1)=3.0
      TLOTZ(13,2)=7.06
      TLOTZ(13,3)=14.44E-14
      TLOTZ(13,4)=0.4
      TLOTZ(13,5)=0.6
      TLOTZ(13,6)=2.0
      TLOTZ(13,7)=8.0
      TLOTZ(13,8)=11.552E-14
      TLOTZ(13,9)=0.8
      TLOTZ(13,10)=0.25
      TLOTZ(13,11)=3.0
      TLOTZ(13,12)=43.0
      TLOTZ(13,13)=14.44E-14
      TLOTZ(13,14)=0.6
      TLOTZ(13,15)=0.4
      TLOTZ(13,16)=6.0
C                              AL
      TLOTZ(14,1)=2.0
      TLOTZ(14,2)=5.99
      TLOTZ(14,3)=4.0E-14
      TLOTZ(14,4)=0.3
      TLOTZ(14,5)=0.6
      TLOTZ(14,6)=1.0
      TLOTZ(14,7)=10.6
      TLOTZ(14,8)=4.0E-14
      TLOTZ(14,9)=0.4
      TLOTZ(14,10)=0.6
      TLOTZ(14,11)=2.0
C                              AU
C     WE ASSUME THE RATE COEFFICIENT OF AU TO BE EQUAL TO THAT OF CU
C     (SIMILAR OUTER SHELL CONFIGURATION) MULTIPLIED BY Z(AU)/Z(CU)=2.7.
C     TO THIS END LOTZ'S A(I) COEFFICIENTS OF CR WERE MULTIPLIED BY 2.7.
      TLOTZ(15,1)=3.0
      TLOTZ(15,2)=7.73
      TLOTZ(15,3)=10.8E-14
      TLOTZ(15,4)=0.0
      TLOTZ(15,5)=0.0
      TLOTZ(15,6)=1.0
      TLOTZ(15,7)=10.4
      TLOTZ(15,8)=5.94E-14
      TLOTZ(15,9)=0.95
      TLOTZ(15,10)=0.16
      TLOTZ(15,11)=10.0
      TLOTZ(15,12)=80.0
      TLOTZ(15,13)=10.8E-14
      TLOTZ(15,14)=0.6
      TLOTZ(15,15)=0.4
      TLOTZ(15,16)=6.0
C                              BE
      TLOTZ(16,1)=2.0
      TLOTZ(16,2)=9.32
      TLOTZ(16,3)=4.0E-14
      TLOTZ(16,4)=0.7
      TLOTZ(16,5)=0.5
      TLOTZ(16,6)=2.0
      TLOTZ(16,7)=115.0
      TLOTZ(16,8)=4.4E-14
      TLOTZ(16,9)=0.6
      TLOTZ(16,10)=0.6
      TLOTZ(16,11)=2.0
C                              SI
      TLOTZ(17,1)=2.0
      TLOTZ(17,2)=8.15
      TLOTZ(17,3)=4.0E-14
      TLOTZ(17,4)=0.3
      TLOTZ(17,5)=0.6
      TLOTZ(17,6)=2.0
      TLOTZ(17,7)=13.5
      TLOTZ(17,8)=4.0E-14
      TLOTZ(17,9)=0.4
      TLOTZ(17,10)=0.6
      TLOTZ(17,11)=2.0
C                             TA
C     WE ASSUME THE RATE COEFFICIENT OF TA TO BE EQUAL TO THAT OF V
C     (SIMILAR OUTER SHELL CONFIGURATION) MULTIPLIED BY Z(TA)/Z(V)=3.2.
C     TO THIS END LOTZ'S A(I) COEFFICIENTS OF V WERE MULTIPLIED BY 3.2
      TLOTZ(18,1)=3.0
      TLOTZ(18,2)=7.06
      TLOTZ(18,3)=12.8E-14
      TLOTZ(18,4)=0.4
      TLOTZ(18,5)=0.6
      TLOTZ(18,6)=2.0
      TLOTZ(18,7)=8.0
      TLOTZ(18,8)=10.24E-14
      TLOTZ(18,9)=0.8
      TLOTZ(18,10)=0.25
      TLOTZ(18,11)=3.0
      TLOTZ(18,12)=43.0
      TLOTZ(18,13)=12.8E-14
      TLOTZ(18,14)=0.6
      TLOTZ(18,15)=0.4
      TLOTZ(18,16)=6.0
C                              TI
      TLOTZ(19,1)=3.0
      TLOTZ(19,2)=6.82
      TLOTZ(19,3)=4.0E-14
      TLOTZ(19,4)=0.4
      TLOTZ(19,5)=0.6
      TLOTZ(19,6)=2.0
      TLOTZ(19,7)=8.0
      TLOTZ(19,8)=3.5E-14
      TLOTZ(19,9)=0.7
      TLOTZ(19,10)=0.4
      TLOTZ(19,11)=2.0
      TLOTZ(19,12)=38.0
      TLOTZ(19,13)=4.0E-14
      TLOTZ(19,14)=0.6
      TLOTZ(19,15)=0.4
      TLOTZ(19,16)=6.0
C                              V
      TLOTZ(20,1)=3.0
      TLOTZ(20,2)=7.06
      TLOTZ(20,3)=4.0E-14
      TLOTZ(20,4)=0.4
      TLOTZ(20,5)=0.6
      TLOTZ(20,6)=2.0
      TLOTZ(20,7)=8.0
      TLOTZ(20,8)=3.2E-14
      TLOTZ(20,9)=0.8
      TLOTZ(20,10)=0.25
      TLOTZ(20,11)=3.0
      TLOTZ(20,12)=43.0
      TLOTZ(20,13)=4.0E-14
      TLOTZ(20,14)=0.6
      TLOTZ(20,15)=0.4
      TLOTZ(20,16)=6.0
C                              ZR
C     WE ASSUME THE RATE COEFFICIENT OF ZR TO BE EQUAL TO THAT OF V
C     (SIMILAR OUTER SHELL CONFIGURATION) MULTIPLIED BY Z(ZR)/Z(V)=1.74
C     TO THIS END LOTZ'S A(I) COEFFICIENTS OF V WERE MULTIPLIED BY 1.74
      TLOTZ(21,1)=3.0
      TLOTZ(21,2)=7.06
      TLOTZ(21,3)=6.96E-14
      TLOTZ(21,4)=0.4
      TLOTZ(21,5)=0.6
      TLOTZ(21,6)=2.0
      TLOTZ(21,7)=8.0
      TLOTZ(21,8)=5.57E-14
      TLOTZ(21,9)=0.8
      TLOTZ(21,10)=0.25
      TLOTZ(21,11)=3.0
      TLOTZ(21,12)=43.0
      TLOTZ(21,13)=6.96E-14
      TLOTZ(21,14)=0.6
      TLOTZ(21,15)=0.4
      TLOTZ(21,16)=6.0
C
C------READY FOR BELL/LOTZ ROUTINES------------------------------------
C
   10  IF( IZOK(IZ).EQ.1 ) THEN
                           N = TBELL(IZ,2)
                           IF(N.EQ.0)THEN
                                     IER = 2
                                     GOTO 9999
                           END IF
                           BUFFER(1) = TBELL(IZ,1)
                           DO 500 M  = 1 , N
                              BUFFER(M + 1) = TBELL(IZ , M + 2)
  500                      CONTINUE
                           BUFFER(N + 2) = TBELL(IZ , N + 3)
                           M = TBELL(IZ , N + 4)
                           DO 600 II = 1 , M
                              LOC = N + II
                              BUFFER(LOC + 2) = TBELL(IZ , LOC + 4)
  600                      CONTINUE
                           CALL DBELLR( N           , M           ,
     &                                  BUFFER(1)   , BUFFER(2)   ,
     &                                  BUFFER(N+2) , BUFFER(N+3) ,
     &                                  TEMPE       , RATCOF      )
       END IF
C
       IF( IZOK(IZ).EQ.2 ) THEN
                           N = TLOTZ(IZ,1)
                           IF(N.EQ.0)THEN
                                     IER = 2
                                     GOTO 9999
                           END IF
                           DO 800 M = 1 , N
                              LOC = ( ( M - 1 ) * 5 ) + 1
                              BUFFER(     M) = TLOTZ(IZ , LOC + 1)
                              BUFFER( 5 + M) = TLOTZ(IZ , LOC + 2)
                              BUFFER(10 + M) = TLOTZ(IZ , LOC + 3)
                              BUFFER(15 + M) = TLOTZ(IZ , LOC + 4)
                              BUFFER(20 + M) = TLOTZ(IZ , LOC + 5)
  800                      CONTINUE
                           CALL DLOTZ( N          ,
     &                                 BUFFER(1)  , BUFFER(6)  ,
     &                                 BUFFER(11) , BUFFER(16) ,
     &                                 BUFFER(21) , TEMPE      ,
     &                                 RATCOF                  )
       END IF
C
C-----------------------------------------------------------------------
C
 9999  RETURN
       END
C
C=======================================================================
      SUBROUTINE DBELLR(N,M,P,A,ALFA,B,T,RC)
      IMPLICIT REAL*8(A-H,O-Z)
C
C     ELECTRON-IMPACT IONIZATION RATE COEFFICIENT
C     (BELL K.L. ET AL. UKAEA REPORT CLM-R216M(1982)
C      AND UNPUBL. REP. 1986 FOR HEAVY MATERIALS)
C
C     GOLD AND CUPPER ARE MISSING WITH RESPECT TO LOTZ,
C     THEREFORE THE MAIN PROGRAM CONSTRUCTS FIRST LOTZ TABLES
C     AND AFTERWARDS BELL TABLES, OVERLAPPING LOTZ ONLY
C     FOR MATERIALS PRESENT IN BELL.
C
C     N= NUMBER OF "A" COEFFICIENTS
C     M= NUMBER OF "B" COEFFICIENTS
C     P= IONIZATION POTENTIAL
C     A(1),...,A(N),ALFA,B(1),...,B(M)= BELL'S FORMULA COEFFICIENTS
C     T= TEMPERATURE
C     RC= RATE COEFFICIENT <V*SIGMA(V)> (CM**3/SEC)
C
C
      DIMENSION A(*),B(*)
C
      TDP=T/P
      PDT=P/T
      RTDP=DSQRT(TDP)
      IF(T.GT.10.0*P) GO TO 30
      RC=A(1)
      IF(N.EQ.1) GO TO 20
      DO 10 I=2,N
   10 RC=RC+A(I)*DLOG10(TDP)**(I-1)
   20 RC=RC*DEXP(-PDT)*RTDP
      RETURN
   30 RC=B(1)
      IF(M.EQ.1) GO TO 50
      DO 40 I=2,M
   40 RC=RC+B(I)*PDT**(I-1)
   50 RC=(ALFA*DLOG(TDP)+RC)/RTDP
      RETURN
      END
C
C=======================================================================
       SUBROUTINE DLOTZ(N,P,A,B,C,Q,T,RC)
       IMPLICIT REAL*8(A-H,O-Z)
       SAVE
C
C      ELECTRON-IMPACT IONIZATION RATE COEFFICIENT
C      (LOTZ W. ZEITSCHRIFT FUR PHYSIK 216,241-247 (1968))
C
C      N=NUMBER OF SUBSHELLS (=1)
C      P=BINDING ENERGY OF ELECTRONS IN THIS SUBSHELL
C      A,B,C= INDIVIDUAL CONSTANTS
C      Q=NUMBER OF EQUIVALENT ELECTRONS IN THIS SUBSHELL
C      T=TEMPERATURE
C      RC=RATE COEFFICIENT (V*SIGMA(V))
       DIMENSION P(3),A(3),B(3),C(3),Q(3)
C
       RC = 0.0
       DO 10 I= 1 , N
          X  = P(I) / T
          XD = X
          CALL DEXPI(XD,EXP1D,DUMMY)
          EXP1 = EXP1D
          Z    = X + C(I)
          ZD   = Z
          CALL DEXPI(ZD,EXP2D,DUMMY)
          EXP2 = EXP2D
   10     RC=RC+A(I)*Q(I)*T**(-1.5)*(EXP1/X-(B(I)*DEXP(C(I))/Z)*EXP2)
C
C              RATE COEFFICIENT IN CM**3/SEC
       RC=RC*6.7E+7
       RETURN
       END
C
C=======================================================================
       SUBROUTINE DEXPI(X,EXPINT,PIFFO)
       IMPLICIT REAL*8(A-H,O-Z)
       SAVE
       DIMENSION P1(5),Q1(4),P2(7),Q2(7),P3(6),Q3(6),P4(8),Q4(8)
       DIMENSION A1(8),B1(7),A2(8),B2(7),A3(6),B3(5)
       DATA P1
     */-4.34981 43832 95212D+2, +4.25696 82638 59170D+2,
     * +2.92525 18866 92055D+2, +3.98941 53870 32107D+1,
     * +4.29312 52343 20973D+0/
       DATA Q1
     */+7.53585 64359 84293D+2, +5.68052 52718 98696D+2,
     * +1.50950 38744 25131D+2, +1.88992 88395 00297D+1/
       DATA P2
     */+4.65627 10797 50957D-7, +9.99979 57705 15950D-1,
     * +9.04161 55694 63287D+0, +2.43784 08879 13167D+1,
     * +2.30192 55939 13335D+1, +6.90522 52278 44436D+0,
     * +4.30967 83946 93888D-1/
       DATA Q2
     */+1.00000 00000 00000D+0, +1.00411 64382 90545D+1,
     * +3.24264 21069 51381D+1, +4.12807 84189 14243D+1,
     * +2.04494 78501 37942D+1, +3.31909 21359 33016D+0,
     * +1.03400 13040 48740D-1/
       DATA P3
     */-9.99999 99990 36009D-1, -1.96304 08535 93865D+1,
     * -1.19557 61038 37175D+2, -2.54376 33976 88951D+2,
     * -1.47982 19500 50448D+2, -2.39099 64453 13573D+0/
       DATA Q3
     */+1.00000 00000 00000D+0, +2.16304 08494 23774D+1,
     * +1.56818 43364 53856D+2, +4.62230 27156 14783D+2,
     * +5.30685 09610 81167D+2, +1.77600 70940 35063D+2/
       DATA P4
     */-8.66937 33995 10696D+0, -5.49142 26552 10851D+2,
     * -4.21001 61535 70699D+3, -2.49301 39345 86476D+5,
     * -1.19623 66934 92469D+5, -2.21744 62775 88454D+7,
     * +3.89280 42131 12014D+6, -3.91546 07380 90955D+8/
       DATA Q4
     */+3.41718 75000 00000D+1, -1.60708 92658 72209D+3,
     * +3.57300 29805 85081D+4, -4.83547 43616 21635D+5,
     * +4.28559 62461 17490D+6, -2.49033 37574 05403D+7,
     * +8.91925 76757 56121D+7, -1.65254 29972 52109D+8/
       DATA A1
     */+1.00443 10922 80779D+0, -4.32531 13287 81346D+1,
     * +6.01217 99083 00805D+1, -3.31842 53199 72211D+1,
     * +2.50762 81129 35598D+1, +9.30816 38566 21651D+0,
     * -2.19010 23385 48809D+1, -2.18086 38152 07237D+0/
       DATA B1
     */+5.27468 85196 29079D-1, +2.73624 11988 93281D+3,
     * +1.43256 73812 19376D+1, +1.00367 43951 67258D+3,
     * -6.25041 16167 18755D+0, +3.00892 64837 29152D+2,
     * +3.93707 70185 27150D+0/
       DATA A2
     */+9.99994 29607 47083D-1, -1.95022 32128 96598D+0,
     * +1.75656 31546 96144D+0, +1.79601 68876 92516D+1,
     * -3.23467 33030 54035D+1, -8.28561 99414 06413D+0,
     * -1.86545 45488 33988D+1, -3.48334 65360 28526D+0/
       DATA B2
     */+1.00083 86740 26391D+0, -3.43942 26689 98700D+0,
     * +2.89516 72792 51351D+1, +7.60761 14800 77346D+2,
     * +2.57776 38423 84399D+1, +5.72837 19383 73237D+1,
     * +6.95000 65588 74340D+1/
       DATA A3
     */+1.00000 00000 70443D+0, -3.00000 77799 35772D+0,
     * -5.02233 17461 85109D+0, -9.14830 08216 73641D+0,
     * -1.01047 90815 76032D+1, -2.77809 28934 43810D+1/
       DATA B3
     */+1.99999 99428 26009D+0, -2.99901 18065 26193D+0,
     * -7.18975 18395 04450D+0, +2.72761 00778 77917D+0,
     * +1.22399 93926 82269D+2/
       DATA X0 /0.372507410781367/
C
       IF(X .GT. 42.D0)  GO TO 90
C
       IF(X .GT. 4.0) GO TO 80
       IF(X .GT. 1.0) GO TO 70
       IF(X .GT. 0.0) GO TO 60
       IF(X .EQ. 0.0) GO TO 50
       Y=-X
       IF(X .GT. -6.0) GO TO 30
       V=DEXP(Y)/X
       IF(X .GT. -12.0) GO TO 20
       IF(X .GT. -24.0) GO TO 10
      EXPINT=V*(1.0+(A3(1)+B3(1)/(A3(2)+Y+B3(2)/(A3(3)+Y+B3(3)/(A3(4)+Y+
     * B3(4)/(A3(5)+Y+B3(5)/(A3(6)+Y))))))/Y)
       GO TO 130
   10 EXPINT=V*(A2(1)+B2(1)/(A2(2)+Y+B2(2)/(A2(3)+Y+B2(3)/(A2(4)+Y+B2(4)
     * /(A2(5)+Y+B2(5)/(A2(6)+Y+B2(6)/(A2(7)+Y+B2(7)/(A2(8)+Y))))))))
       GO TO 130
   20 EXPINT=V*(A1(1)+B1(1)/(A1(2)+Y+B1(2)/(A1(3)+Y+B1(3)/(A1(4)+Y+B1(4)
     * /(A1(5)+Y+B1(5)/(A1(6)+Y+B1(6)/(A1(7)+Y+B1(7)/(A1(8)+Y))))))))
       GO TO 130
   30  V=0.666666666666667D+0*Y-2.0
       BP=0.
       BQ=0.
       DP=P4(1)
       DQ=Q4(1)
       DO 40 I = 2,8
       AP=BP
       BP=DP
       AQ=BQ
       BQ=DQ
       DP=P4(I)-AP+V*BP
   40  DQ=Q4(I)-AQ+V*BQ
       EXPINT=-DLOG(Y/X0)-(Y-X0)*(DP-AP)/(DQ-AQ)
       GO TO 130
   50  EXPINT=0.
       GO TO 130
   60 EXPINT=-DLOG(X)+
     *       (P1(1)+X*(P1(2)+X*(P1(3)+X*(P1(4)+X*P1(5)))))/
     *       (Q1(1)+X*(Q1(2)+X*(Q1(3)+X*(Q1(4)+X))))
       GO TO 130
   70  Y=1.0/X
      EXPINT=DEXP(-X)*
     *(P2(1)+Y*(P2(2)+Y*(P2(3)+Y*(P2(4)+Y*(P2(5)+Y*(P2(6)+Y*P2(7)))))))/
     *(Q2(1)+Y*(Q2(2)+Y*(Q2(3)+Y*(Q2(4)+Y*(Q2(5)+Y*(Q2(6)+Y*Q2(7)))))))
       GO TO 130
   80  Y=1.0/X
      EXPINT=DEXP(-X)*Y*(1.0+Y*
     *       (P3(1)+Y*(P3(2)+Y*(P3(3)+Y*(P3(4)+Y*(P3(5)+Y*P3(6))))))/
     *       (Q3(1)+Y*(Q3(2)+Y*(Q3(3)+Y*(Q3(4)+Y*(Q3(5)+Y*Q3(6)))))))
       GO TO 130
C
   90  CONTINUE
C
       Y      = - 1.D0 / X
       TERM   = 1.D0
       SUM    = 1.D0
       DIN    = 0.D0
       EPS    = 1.D-14
       DO 100 IN = 1,100
       DIN    = DIN + Y
       TERM   = TERM * DIN
       ASTER  = DABS(TERM/SUM)
       IF (ASTER .LT. EPS)  GO TO 110
       SUM    = SUM + TERM
  100  CONTINUE
  110  CONTINUE
       PIFFO  = SUM
       IF (X .GT. 167.D0)  GO TO 120
       EXPINT = SUM / X * DEXP(-X)
       GO TO 130
  120  CONTINUE
       EXPINT = 0.D0
C
  130  CONTINUE
       IF(EXPINT.LT.0.0) EXPINT=0.0
       RETURN
       END
C
C=======================================================================
      SUBROUTINE ATOMIC(HMASS,TCUTEV)
C
C     ATOMIC PHYSICS
C
C     UNITS:
C            ENERGY - EV
C            MASS   - AMU
C
      IMPLICIT NONE
C
      REAL*8  CSVI1,CSVI2,CSVI3,CSVR1,CSVR2,CSVR3,CSVR4,CSVR5,
     &        CSCX1,CSCX2,CSCX3,CSCX4
      SAVE    CSVI1,CSVI2,CSVI3,CSVR1,CSVR2,CSVR3,CSVR4,CSVR5,
     &        CSCX1,CSCX2,CSCX3,CSCX4
      REAL*8  SVI,DSVI,SVT,SVR,DSVR,SCX,DSCX
      REAL*8  TCUT
      SAVE    TCUT
      REAL*8  TE,ANORM,TI
      REAL*8  E0,YY,XX,XXX,XXXX
      REAL*8  EV,EH,HMASS,AMU,TCUTEV
      REAL*8  SMI,DSMI,SMD,DSMD
      PARAMETER (EV=1.6022D-12,ANORM=1.0D0,AMU=1.6727D-24)
C          CGS
      CSVI1 = 7.35D-07*DSQRT(EV)
      CSVI2 = 81.6D0*EV
      CSVI3 = 13.6D0*EV
      CSVR1 = 1.917D-13*DSQRT(EV)
      CSVR2 = 0.43D0
      CSVR3 = 0.5D0
      CSVR4 = 13.6D0*EV
      CSVR5 = 0.19648D0/EV**(1.0D0/3.0D0)
C         NORMALIZED UNITS (N:NORM, TE:EV, M:AMU)
      CSVI1 = CSVI1*ANORM/DSQRT(EV)
      CSVI2 = CSVI2/EV
      CSVI3 = CSVI3/EV
      CSVR1 = CSVR1*ANORM/DSQRT(EV)
      CSVR2 = CSVR2
      CSVR3 = CSVR3
      CSVR4 = CSVR4/EV
      CSVR5 = CSVR5*EV**(1.0D0/3.0D0)
      CSCX1 = 5.22D-13
      CSCX2 = 1.6021D-12*8.0D0/(HMASS*AMU*3.14159D0)
      CSCX3 = 3.2D-12/(HMASS*AMU)
      CSCX4 = ANORM
C
      TCUT = TCUTEV
C
      RETURN
C
      ENTRY NRLXS(TE,TI,EH,SVI,DSVI,SCX,DSCX,SMI,DSMI,SMD,DSMD,SVR,DSVR)
C
C     SVI(TE)
C     =======
      SVI     = CSVI1*DSQRT(TE)*DEXP(-CSVI3/TE)
     &        / (CSVI2+TE)
C
C     DSVI(TE)
C     ========
      DSVI    = (1.0D0/(CSVI2+TE)**2)
     &        * (-0.5D0*CSVI1*DSQRT(TE)+(0.5D0*CSVI1*CSVI2 +
     &          CSVI1*CSVI3)/DSQRT(TE) + CSVI1*CSVI2*CSVI3*
     &          (1.0D0/TE**1.5D0))*DEXP(-CSVI3/TE)
C
C     SVR(TE)
C     =======
      SVR     = (CSVR1/DSQRT(TE))*(CSVR2+CSVR3*DLOG(CSVR4/TE)
     &                             +CSVR5*TE**(1.0D0/3.0D0))
C
C     DSVR(TE)
C     ========
      DSVR    = -0.5D0*CSVR1*CSVR2*TE**(-1.5D0) -(CSVR1*CSVR5/6.0D0)
     &           *TE**(-7.0D0/6.0D0) -0.5D0*CSVR1*CSVR3*TE**(-1.5D0)
     &          *DLOG(CSVR4/TE) + (CSVR1*CSVR3/CSVR4)*DSQRT(TE)
C
C     SCX(TI,EH)
C     =========
      E0 = EH
      YY = DSQRT(CSCX2*TI + CSCX3*E0)
      XX = CSCX1*(CSCX2*TI + CSCX3*E0)
      XXX= DLOG(XX)
      IF(XX.GT.200.0) THEN
        XXXX=0.6937D0*(1.0D0-0.0673*XXX)**2/
     &       (1.0D14+0.1112D0*DEXP(3.3*XXX))
      ELSE
        XXXX=4.549D-15 - 0.4486D-15*XXX
      ENDIF
      SCX     = CSCX4*XXXX*YY
C
C     DSCX(TI)
C     ========
      IF( XX.GT.200.0 ) THEN
          DSCX    = - ( CSCX1*CSCX2*CSCX4*0.639D0
     &                  * (   2.0D0 * DEXP(3.3D0*XXX)
     &                              * (XXX**2)
     &                              * (0.0673D0**2)*0.1112D0*3.3D0
     &                      -         DEXP(3.3D0*XXX)
     &                              * (XXX**2)
     &                              * (0.0673D0**2)* 0.1112D0
     &                      - 4.0D0 * DEXP(3.3D0*XXX)
     &                              * XXX
     &                              * (0.0673D0**2)*0.1112D0
     &                      - 4.0D0 * DEXP(3.3D0*XXX)
     &                              * XXX
     &                              * 0.0673D0*0.1112D0*3.3D0
     &                      + 2.0D0 * DEXP(3.3D0*XXX)
     &                              * XXX
     &                              * 0.0673D0*0.1112D0
     &                      + 4.0D0 * DEXP(3.3D0*XXX)
     &                              * 0.0673D0*0.1112D0
     &                      + 2.0D0 * DEXP(3.3D0*XXX)
     &                              * 0.1112D0*3.3D0
     &                      -         DEXP(3.3D0*XXX)
     &                              * 0.1112D0
     &                      -         (XXX**2)
     &                              * (0.0673D0**2)*1.0D+14
     &                      - 4.0D0 * XXX
     &                              * (0.0673D0**2)*1.0D+14
     &                      + 2.0D0 * XXX * 0.0673D0*1.0D+14
     &                      + 4.0D0 * 0.0673D0
     &                              * 1.0D+14
     &                      -         1.0D+14
     &                    ) )
     &                /   ( 2.0D0*DSQRT(CSCX1*XX)
     &                      * ( DEXP(2.0D0*XXX*3.3D0)
     &                               *(0.1112D0**2)
     &                         +2.0D0*DEXP(3.3D0*XXX)
     &                               *1.0D+14*0.1112D0
     &                         +1.0D+28 ) )
      ELSE
          DSCX    = - ( CSCX1*CSCX2*CSCX4
     &                  * ( 0.4486D-15*(2.0D0+XXX) - 4.549D-15)
     &                ) / ( 2.0D0*DSQRT(CSCX1*XX) )
      END IF
C
C     MOLECULES
C     =========
      SMI = 0.0D0
      DSMI = 0.0D0
      SMD = 0.0D0
      DSMD = 0.0D0
C
      IF( TE.LT.TCUT .OR. TI.LT.TCUT ) THEN
        SVI = 0.0D0
        DSVI = 0.0D0
        SCX = 0.0D0
        DSCX = 0.0D0
        SMI = 0.0D0
        DSMI = 0.0D0
        SMD = 0.0D0
        DSMD = 0.0D0
        SVR = 0.0D0
        DSVR = 0.0D0
      END IF
C
      RETURN
C
      END
