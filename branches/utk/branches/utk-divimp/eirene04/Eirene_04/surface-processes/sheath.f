C
C
      FUNCTION SHEATH(TE,DPP,VP,NZP,GAMMA,CUR,NP,MS)

C  JAN.93: NEW VERSION, NOW CORRECTLY ACCOUNTING
C          FOR MULTISPECIES PLASMA BACKGROUND
C  SHEATH POTENTIAL -E*DPHI (EV) FOR AN NP COMPONENT
C  PLASMA AS FUNCTION OF:
C  TE ELECTRON TEMPERATUR (EV)
C  DP(J),J=1,NP  BULK ION DENSITIES (#/CM**3)
C  VP(J),J=1,NP  BULK ION DRIFT VELOCITIES (CM/SEC)
C                COMPONENT PARALLEL TO MAGNETIC FIELD B
C                (ASSUME: MOBILITY OF ELECTRONS MUCH LARGER
C                 THEN OF IONS, AND ELECTRONS OBEY A
C                 MAXWELL-BOLTZMANN DISTRIBUTION
C  NZP(J),J+1,NP CHARGE NUMBERS OF BULK IONS
C  GAMMA         SECONDARY ELECTRON EMISSION COEF.
C                SHEATH VOLTAGE
C                "SHEATH" DECREASES WITH INCREASING GAMMA
C  CUR           NET CURRENT DENSITY OF BULK PLASMA TO TARGET
C                CUR=(J-ION) - (J-ELECTR.) (AMP/CM**2)
C                SHEATH DECREASES FOR NEGATIVE CURRENTS, AND
C                       INCREASES FOR POSITIVE CURRENTS
C
      USE PRECISION
      USE PARMMOD
      USE CCONA

      IMPLICIT NONE

      REAL(DP), INTENT(IN) :: DPP(*),VP(*), TE, GAMMA, CUR
      INTEGER, INTENT(IN) :: NZP(*), NP, MS
      REAL(DP) :: SUM, DE, CE, SHEATH
      INTEGER :: J, I, MSS, ICOUNT

      SAVE ICOUNT
      DATA ICOUNT/0/
C
      SHEATH=0.
      DE=0.
      DO 10 J=1,NP
        DE=DE+DPP(J)*NZP(J)
10    CONTINUE
      DE=DE+EPS60
C  UNITS OF SUM: VELOCITY (CM/SEC)
      SUM=0.
      DO 100 J=1,NP
        SUM=SUM+NZP(J)*DPP(J)/DE*VP(J)
100   CONTINUE
      CE=CVEL2A*SQRT(TE/PMASSE)
      SUM=1./CE*SQRT(PI2A)/(1.-GAMMA)*(SUM-CUR/ELCHA/DE)
      IF (SUM.GT.0.D0) THEN
        SHEATH=-TE*LOG(SUM)
      ELSEIF (ICOUNT.LE.10) THEN
        MSS=MS
        IF (MSS.GT.NLIM) MSS=-(MSS-NLIM)
        WRITE (6,*) 'WARNING FROM FCT. SHEATH: INVALID ARGUMENTS '
        WRITE (6,*) 'NP,NZP,DPP,VP ',NP
        WRITE (6,*) 'SHEATH RETURNED FOR SURFACE ', MSS,': 2.8*TE'
        DO I=1,NP
          WRITE (6,*) I,NZP(I),DPP(I),VP(I)
        ENDDO
        WRITE (6,*) 'SUM ',SUM
        ICOUNT=ICOUNT+1
        SHEATH = 2.8*TE
      ELSE
        SHEATH = 2.8*TE
      ENDIF
      RETURN
      END
