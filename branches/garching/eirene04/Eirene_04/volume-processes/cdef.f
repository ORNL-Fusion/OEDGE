C
C
      SUBROUTINE CDEF(AL,JI,JE,K,COU,NTE,CF,LEXP,LTEST,LSUM)
C
C  EIRENE ATOMIC DATA , DEFAULT OR FROM FILE: POLYNOM FIT FORMAT
C
C  INPUT:
C    AL(J),J=1,NTE
C  OUTPUT:
C    MAXWELLIAN RATES, AL=LN(KT), KT IN (EV)
C  K
C       K>0: A&M DATA FROM FILES HYDHEL, METHANE OR AMJUEL
C           K:  NUMBER OF REACTION IN EIRENE "CREAC"-ARRAY
C
C       K<0: HARD WIRED EIRENE DEFAULT ATOMIC AND MOLECULAR DATA PACKAGE
C       EACH NUMBER ABS(K) CORRESPONDS TO ONE SPECIFIC REACTION DATA FIT
C       (SEE COMMENTS BELOW)
C  JI,JE
C  FOR JI<=J<=JE RETURN:
C       1<=J<=9:  JTH ENERGY COEFFICIENT OF TWO PARAM. FITS
C                 AT TEMPERATUR KT (EV)
C                 (E.G.: RATES FOR HEAVY PARTICLE INTERACTIONS)
C                 IF ONLY ONE FIT AVAILABLE, ITS INDEX IS J=1
C                 E.Q. FOR ELECT. IMP. RATES AS FUNCTION OF TE
C
C  FIT FROM JANEV ET AL, PPPL-TM-368, 1985  (PREPRINT) OR:
C           SPRINGER SERIES ON ATOMS AND PLASMAS, VOL 4, 1987
C
      USE PRECISION
      USE PARMMOD
      USE CCONA
      USE COMXS

      IMPLICIT NONE

      REAL(DP), INTENT(IN) :: AL(*)
      REAL(DP), INTENT(OUT) :: COU(0:9,*),CF(9,0:9)
      INTEGER, INTENT(IN) :: JI, JE, K, NTE
      LOGICAL, INTENT(IN) :: LEXP, LTEST, LSUM
      REAL(DP), SAVE :: CFP(9,11)
      REAL(DP), SAVE :: CF1(9),CF2(9),CF3(9),
     .                  CF4(9),CF5(9),CF6(9),CF7(9),
     .                  CF8(9),CF9(9),CF10(9),CF11(9)
      REAL(DP) :: CTEST
      INTEGER :: ICELL, J, II
      INTEGER, SAVE :: IFIRST=0
C
      EQUIVALENCE (CFP(1,1),CF1(1)),(CFP(1,2),CF2(1)),
     .            (CFP(1,3),CF3(1)),(CFP(1,4),CF4(1)),
     .            (CFP(1,5),CF5(1)),(CFP(1,6),CF6(1)),
     .            (CFP(1,7),CF7(1)),(CFP(1,8),CF8(1)),
     .            (CFP(1,9),CF9(1)),(CFP(1,10),CF10(1)),
     .            (CFP(1,11),CF11(1))

      IF (IFIRST == 0) THEN
        IFIRST=1
C
C  K>0:
C  DATA FROM ARRAY CREAC(9,-1:9,K)
C
C -K=1:   E + HE --> 2E + HE+
C  RATE COEFFICIENT, JANEV, 2.3.9
        CF1 = 
     .  (/-4.409865E+01,2.391597E+01,-1.075323E+01,3.058039,
     .    -5.685119E-01,6.795391E-02,-5.009056E-03,2.067236E-04,
     .    -3.649161E-06/)
C -K=2:   FREE
C -K=3:   FREE
C -K=4:   E + H --> H+ + 2E
C  RATE COEFFICIENT, JANEV, 2.1.5
        CF4 =
     .  (/-3.271397E+01,1.353656E+01,-5.739329E 00,1.563155E 00,
     .    -2.877056E-01,3.482560E-02,-2.631976E-03,1.119544E-04,
     .    -2.039150E-06/)
C -K=5:  E + H2 --> H + H + E
C  RATE COEFFICIENT, JANEV, 2.2.5, PREPRINT (CORRECT), NOT "BOOK"
        CF5 =
     .  (/-2.7872175E+01,1.0522527E+01,-4.9732123E+00,
     .     1.4511982E+00,-3.0627906E-01,4.4333795E-02,
     .    -4.0963442E-03, 2.1596703E-04,-4.9285453E-06/)
C -K=6:  E + H2 --> H+ + H + 2E
C  RATE COEFFICIENT, JANEV, 2.2.10
        CF6 =
     .  (/-3.834597E+01,1.426322E+01,-5.826467E+00,
     .     1.727941E+00,-3.598121E-01,4.822199E-02,
     .    -3.909403E-03,1.738777E-04,-3.252845E-06/)
C -K=7: E + H2 --> H2+(VIB) + 2E
C  RATE COEFFICIENT, JANEV, 2.2.9
        CF7 =
     .  (/-3.568640E+01,1.733469E+01,-7.767469E+00,
     .     2.211579E+00,-4.169840E-01,5.088290E-02,
     .    -3.832738E-03,1.612863E-04,-2.893392E-06/)
C -K=8: E + H2+(VIB) --> H + H+ + E
C  RATE COEFFICIENT, JANEV, 2.2.12
        CF8 =
     .  (/-1.781416E+01,2.277799E+00,-1.266868E+00,
     .    4.296170E-01,-9.609908E-02,1.387958E-02,
     .   -1.231349E-03,6.042383E-05,-1.247521E-06/)
C -K=9: E + H2+(VIB) --> H+ + H+ + 2E
C  RATE COEFFICIENT, JANEV, 2.2.11
        CF9 = 
     .  (/-3.746192E+01,1.559355E+01,-6.693238E+00,
     .     1.981700E+00,-4.044820E-01,5.352392E-02,
     .    -4.317452E-03,1.918499E-04,-3.591779E-06/)
C -K=10: E + H2+(VIB) --> H + H(N)
C  RATE COEFFICIENT, JANEV, 2.2.14
        CF10 =
     .  (/-1.670436E+01,-6.035645E-01,-1.942746E-08,
     .    -2.005952E-07,2.962996E-08,2.134293E-08,
     .    -6.353973E-09,6.152557E-10,-2.025362E-11/)
C -K=11:  FREE
C
      END IF  ! IFIRST BLOCK FINISHED

      IF (K.LT.0) THEN
C  EIRENE DEFAULT DATA, JI=JE, LEXP=TRUE,LSUM=FALSE,LTEST=FALSE
        DO J=JI,JE
          DO II=1,9
            CF(II,J)=CFP(II,-K+J-JI)
          END DO
        END DO
C
      ELSEIF (K.GT.0) THEN
C  DATA FROM A&M DATA FILES
!pb     IF (AL.LT.RCMN(K,2).OR.AL.GT.RCMX(K,2)) THEN
!pb       WRITE (6,*) 'WARNING FROM SUBR. CDEF '
!pb       WRITE (6,*) 'PLASMA TEMPERATURE OUT OF SAVE RANGE FOR'
!pb       WRITE (6,*) 'RATE COEFFICIENT NR. ',K, exp(al)
!pb       WRITE (6,*) 'CONSULT D.REITER '
!pb     ENDIF
        DO 11 J=JI,JE
          DO 12 II=1,9
            CF(II,J)=CREAC(II,J,K)
12        CONTINUE
11      CONTINUE
        IF (LTEST) THEN
          DO 13 J=JI,JE
            CTEST=0.
            DO 14 II=1,9
              CTEST=CTEST+ABS(CF(II,J))
14          CONTINUE
            IF (CTEST.LE.EPS60) GOTO 990
13        CONTINUE
        ENDIF
      ENDIF
C
      IF (LSUM) THEN
        DO J=JI,JE
          DO ICELL=1,NTE
            COU(J,ICELL)=CF(9,J)
          END DO
        END DO
C
        DO 21 J=JI,JE
          DO 22 II=8,1,-1
            DO 23 ICELL=1,NTE
              COU(J,ICELL)=COU(J,ICELL)*AL(ICELL)+CF(II,J)
23          CONTINUE
22        CONTINUE
21      CONTINUE
      ENDIF
C
      IF (LEXP) THEN
        DO 24 ICELL=1,NTE
          COU(JE,ICELL)=EXP(MAX(-100._DP,COU(JE,ICELL)))
24      CONTINUE
      ENDIF
C
      RETURN
C
C
990   CONTINUE
      WRITE (6,*) 'ERROR IN SUBROUTINE CDEF: ZERO FIT COEFFICIENTS'
      WRITE (6,*) 'J,K = ',J,K,'  EXIT CALLED!'
      CALL EXIT_OWN(1)
      END
