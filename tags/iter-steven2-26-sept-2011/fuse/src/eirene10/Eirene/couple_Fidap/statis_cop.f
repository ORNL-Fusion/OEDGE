C  STANDARD DEVIATIONS FOR COPV TALLIES, AND FOR SOME SPECIAL
C                      ALGEBRAIC FUNCTIONS (SUMS, ....) AMONGST THEM.
C
C  THIS VERSION:  COUPLING TO FIDAP FOR RADIATION TRANSFER
C                 COPV(1:6), SEE SUBR. UPTCOP

C                 ONE EXTRA COPV-TALLY: COPV(NCPVI+1), WHICH IS AN
C                 ALGEBRAIC TALLY (SUM): COPV(3)+COPV(4)+COPV(5)+COPV(6)
C
      SUBROUTINE STATIS_COP
      USE PRECISION
      USE PARMMOD
      USE COMUSR
      USE CESTIM
      USE CCONA
      USE CGRID
      USE CSDVI
      USE CSDVI_COP
      USE COUTAU

      IMPLICIT NONE

      INTEGER, INTENT(IN) :: NBIN, NRIN, NPIN, NTIN, NSIN
      LOGICAL, INTENT(IN) :: LP, LT
      REAL(DP) :: XNM, SD2, DS, ZFLUXQ, SD2S, SDS, SDI, SDE,
     .          D2S, SG, DSA, DD, D, SG2, DA, FSIG, XN, ZFLUX,
     .          SD1, SD1S , SAV
      INTEGER :: IPLS, NSB, IR, ICO, IPL, NR1, NP2, NT3, ICPV,
     .           I,J,IIN,NRW,IRU,ITS,ITL,ISCO,IGI,IGE,NSYM,NSYH,
     .           IGS,IG,IT,IP,J1,J2
      REAL(DP), ALLOCATABLE, SAVE :: VECTOR(:), 
     .          SD(:)   
      INTEGER, ALLOCATABLE, SAVE ::                               
     .                              IND(:,:),   IIND(:),    INDSS(:,:)
C
      SAVE
C
      ENTRY STATS0_COP

      IMETCL = 0
      NCLMT = 0
      LMETSP = .FALSE.

      IF (NSIGI_COP.EQ.0) RETURN
C
      IF (.NOT.ALLOCATED(IND)) THEN
        AllOCATE (IND(NRTAL,8))  
        AllOCATE (IIND(NRTAL)) 
        AllOCATE (INDSS(NRTAL,8))
        AllOCATE (VECTOR(MAX(NRTAL,NLMPGS)))
        AllOCATE (SD(0:(MAX(NRTAL,NLMPGS))))
        SD=0._DP
      END IF

C  FILL IIND, INDSS ARRAYS FOR THOSE "AVERAGE" CELLS, TO WHICH "REAL" 
C  CELL IR ALSO CONTRIBUTES
C  IIND: HOW MANY CELLS
C  INDSS: WHICH CELLS
      CALL INDTAL(IND,NRTAL,NR1TAL,NP2TAL,NT3TAL,NBMLT)
      DO IR=1,NSBOX_TAL
        IIND(IR)=0
        IIN=0
        DO J=1,8
          IF (IND(IR,J).NE.0) THEN
            IIND(IR)=IIND(IR)+1
            IIN=IIN+1
            INDSS(IR,IIN)=J
          ENDIF
        ENDDO
      ENDDO
C
      RETURN

C
      ENTRY STATS1_COP(NBIN,NRIN,NPIN,NTIN,NSIN,LP,LT)
C
      NSB=NBIN
      NR1=NRIN
      NP2=NPIN
      NT3=NTIN
      NRW=NSIN

      IF (NSIGI_COP == 0) RETURN

C  HISTORY HAS TOUCHED NCLMT CELLS.
C  IT CONTRIBUTES TO NCLMTS CELLS (AVERAGES)
C  THESE CELL NUMBERS ARE STORED HERE ON ICLMT ARRAY
        NCLMTS = MAX(NCLMT,NCLMTS)
        DO I=1,NCLMT
          IR = ICLMT(I)
          DO IIN=2,IIND(IR)
            J=INDSS(IR,IIN)
            IRU=IND(IR,J)
            IF (IMETCL(IRU) == 0) THEN
              NCLMTS = NCLMTS+1
              IMETCL(IRU) = NCLMTS
              ICLMT(NCLMTS) = IRU
            END IF
          END DO
        END DO
C
C  STANDARD DEVIATION FOR ALL TALLIES COPV(1:NCPVI)
C
      DO 1012 ICPV=1,NCPVI
C  ARE THERE CONTRIBUTIONS TO THE REQUESTED TALLY FROM THIS HISTORY?
        IGS=ICPV
        ITL=NTALM
        ISCO = 0
        IF (LMETSP(NSPAN(ITL)+IGS-1)) ISCO = 1
        IF (ISCO == 0) GOTO 1012
C
        IF (.NOT.LP.AND..NOT.LT) GOTO 1005
C  USE SYMMETRY IN POLOIDAL/Y AND/OR TOROIDAL/Z COORDINATE
        IGI=IGS
        IGE=IGS
        IF (LP) THEN
          NSYM=NP2
          NSYH=(NSYM-1)/2
          DO 1003 IG=IGI,IGE
          DO 1003 IR=1,NR1
          DO 1003 IT=1,NT3
          DO 1003 IP=1,NSYH
                J1=IR+((IT-1)*NP2+IP-1)*NR1
                J2=IR+((IT-1)*NP2+NSYM-IP-1)*NR1
                SAV=(COPV(ICPV,J1)+COPV(ICPV,J2))*0.5
                COPV(ICPV,J1)=SAV
                COPV(ICPV,J2)=SAV
1003      CONTINUE
        ENDIF
        IF (LT) THEN
          NSYM=NT3
          NSYH=(NSYM-1)/2
          DO 1004 IG=IGI,IGE
          DO 1004 IR=1,NR1
          DO 1004 IP=1,NP2
          DO 1004 IT=1,NSYH
                J1=IR+((IT-1)*NP2+IP-1)*NR1
                J2=IR+((NSYM-IT-1)*NP2+IP-1)*NR1
                SAV=(COPV(ICPV,J1)+COPV(ICPV,J2))*0.5
                COPV(ICPV,J1)=SAV
                COPV(ICPV,J2)=SAV
1004      CONTINUE
        ENDIF
1005    CONTINUE
C
C  FILL ARRAY VECTOR, FOR INDIVIDUAL COPV-TALLIES
C  VECTOR IS FILLED ONLY FOR THOSE CELLS, 
C  WHICH HAVE BEEN TOUCHED BY THIS HISTROY
C  VECTOR CONTAINS THE SUM FROM ALL HISTORIES IN THESE CELLS UP TO THE
C  PRESENT HISTORY
       DO ICO = 1,NCLMT
         IR = ICLMT(ICO)
         VECTOR(ICO)=COPV(ICPV,IR)
       END DO

        SD1S = 0.D0
C  FILL ARRAY SD WITH THE INDIVIDUAL CONTRIBUTION FROM THIS HISTROY,
C  IN EACH CELL THAT HAS BEEN TOUCHED BY THIS HISTORY
        DO ICO = 1,NCLMT
          IR = ICLMT(ICO)
          SD1 = VECTOR(ICO)-SDVIA_COP(ICPV,IR)
          SD1S=SD1S+SD1
          SDVIA_COP(ICPV,IR)=VECTOR(ICO)
          SD(IR) = SD1
C  FILL SD ALSO FOR OTHER "CELL", TO WHICH CELL IR CONTRIBUTES
C  I.E., AVERAGES OVER COORDINATES OR OVER THE ENTIRE COMPUTATIONAL DOMAIN
          DO IIN=2,IIND(IR)
            J=INDSS(IR,IIN)
            IRU=IND(IR,J)
            SD(IRU)=SD(IRU)+SD1
          END DO
        END DO

        DO ICO = 1,NCLMTS
          IR = ICLMT(ICO)
          SD1=SD(IR)
          SIGMA_COP(ICPV,IR)=SIGMA_COP(ICPV,IR)+SD1*SD1
          SD(IR)=0._DP
        END DO

        SGMS_COP(ICPV)=SGMS_COP(ICPV)+SD1S*SD1S
1012  CONTINUE
C

C  STATISTICS FOR ALGEBRAIC FUNCTION OF TALLIES, AS NEEDED FOR COUPLING
C  THIS PART IS SPECIFIC FOR THIS VERSION OF COUPLE_....
C  HERE:
C  STATISTICS FOR TOTAL SAMPLED EMISSION- TOTAL ABSORPTION
C   
      ICPV=NCPVI+1
      IF (LMETSP(NSPAN(NTALM)+2) .OR.
     .    LMETSP(NSPAN(NTALM)+3) .OR.
     .    LMETSP(NSPAN(NTALM)+4) .OR.
     .    LMETSP(NSPAN(NTALM)+5) ) THEN

C  FILL ARRAY VECTOR, FOR INDIVIDUAL COPV-TALLIES
C  VECTOR IS FILLED ONLY FOR THOSE CELLS, 
C  WHICH HAVE BEEN TOUCHED BY THIS HISTROY
C  VECTOR CONTAINS THE SUM FROM ALL HISTORIES IN THESE CELLS UP TO THE
C  PRESENT HISTORY
       DO ICO = 1,NCLMT
         IR = ICLMT(ICO)
         VECTOR(ICO)=COPV(3,IR)+COPV(4,IR)+COPV(5,IR)+COPV(6,IR)
       END DO

        SD1S = 0.D0
C  FILL ARRAY SD WITH THE INDIVIDUAL CONTRIBUTION FROM THIS HISTROY,
C  IN EACH CELL THAT HAS BEEN TOUCHED BY THIS HISTORY
        DO ICO = 1,NCLMT
          IR = ICLMT(ICO)
          SD1 = VECTOR(ICO)-SDVIA_COP(ICPV,IR)
          SD1S=SD1S+SD1
          SDVIA_COP(ICPV,IR)=VECTOR(ICO)
          SD(IR) = SD1
C  FILL SD ALSO FOR OTHER "CELL", TO WHICH CELL IR CONTRIBUTES
C  I.E., AVERAGES OVER COORDINATES OR OVER THE ENTIRE COMPUTATIONAL DOMAIN
          DO IIN=2,IIND(IR)
            J=INDSS(IR,IIN)
            IRU=IND(IR,J)
            SD(IRU)=SD(IRU)+SD1
          END DO
        END DO

        DO ICO = 1,NCLMTS
          IR = ICLMT(ICO)
          SD1=SD(IR)
          SIGMA_COP(ICPV,IR)=SIGMA_COP(ICPV,IR)+SD1*SD1
          SD(IR)=0._DP
        END DO

        SGMS_COP(ICPV)=SGMS_COP(ICPV)+SD1S*SD1S
      END IF

C
1020  CONTINUE
      RETURN
C
      ENTRY STATS2_COP(XN,FSIG,ZFLUX)
C
C  1. FALL  ALLE BEITRAEGE GLEICHES VORZEICHEN: SIG ZWISCHEN 0 UND 1
C           (=1, FALLS NUR EIN BEITRAG UNGLEICH 0, ODER (KUENSTLICH
C            ERZWUNGEN) FALLS GAR KEIN BEITRAG UNGLEICH NULL)
C  2. FALL  NEGATIVE UND POSITIVE BEITRAGE KOMMEN VOR:
C           LT. FORMEL SIND AUCH WERTE GROESSER 1  MOEGLICH.
C
      XNM=XN-1.
      IF (XNM.LE.0.D0) RETURN
      ZFLUXQ=ZFLUX*ZFLUX
C
      IF (NCPVI.EQ.0) GOTO 2200
C
      DO 2112 ICPV=1,NCPVI
C
        DS=0.
        DO 2011 IR=1,NSB
          SD1=COPV(ICPV,IR)
          DS=DS+SD1
          DO 2016 IIN=1,IIND(IR)
            J=INDSS(IR,IIN)
            IRU=IND(IR,J)
            SD(IRU)=SD(IRU)+SD1
2016      CONTINUE
2011    CONTINUE

        DO 2111 IR=1,NSB
          D=SD(IR)
          DD=D*D
          DA=ABS(D)
          SG2=MAX(0._DP,SIGMA_COP(ICPV,IR)-DD/XN)
C RELATIV STANDARD DEVIATION
          SG=SQRT(SG2)/(DA+EPS60)
          SIGMA_COP(ICPV,IR)=SG*FSIG
C CUMULATED VARIANCE FOR SUM OVER STRATA
          STV_COP(ICPV,IR)=STV_COP(ICPV,IR)+SG2*ZFLUXQ/XNM/XN
          EE_COP(ICPV,IR)=EE_COP(ICPV,IR)+D*ZFLUX/XN
          SD(IR)=0._DP
2111    CONTINUE
        D2S=DS*DS
        DSA=ABS(DS)
        SG2=MAX(0._DP,SGMS_COP(ICPV)-D2S/XN)
        SG=SQRT(SG2)/(DSA+EPS60)
        SGMS_COP(ICPV)=SG*FSIG
C
        STVS_COP(ICPV)=STVS_COP(ICPV)+SG2*ZFLUXQ/XNM/XN
        EES_COP(ICPV)=EES_COP(ICPV)+DS*ZFLUX/XN
2112  CONTINUE
C
C  STATISTICS FOR ALGEBRAIC FUNCTION OF TALLIES, AS NEEDED FOR COUPLING
C  HERE:
C  STATISTICS FOR TOTAL SAMPLED EMISSION- TOTAL ABSORPTION
C
      ICPV = NCPVI+1
      DS=0.
      DO IR=1,NSB
        SD1=SUM(COPV(3:6,IR))
        DS=DS+SD1
        DO IIN=1,IIND(IR)
          J=INDSS(IR,IIN)
          IRU=IND(IR,J)
          SD(IRU)=SD(IRU)+SD1
        ENDDO
      ENDDO
      DO IR=1,NSB
        D=SD(IR)
        DD=D*D
        DA=ABS(D)
        SG2=MAX(0._DP,SIGMA_COP(ICPV,IR)-DD/XN)
C RELATIV STANDARD DEVIATION
        SG=SQRT(SG2)/(DA+EPS60)
        SIGMA_COP(ICPV,IR)=SG*FSIG
C CUMULATED VARIANCE FOR SUM OVER STRATA
        STV_COP(ICPV,IR)=STV_COP(ICPV,IR)+SG2*ZFLUXQ/XNM/XN
        EE_COP(ICPV,IR)=EE_COP(ICPV,IR)+D*ZFLUX/XN
      END DO
      D2S=DS*DS
      DSA=ABS(DS)
      SG2=MAX(0._DP,SGMS_COP(ICPV)-D2S/XN)
      SG=SQRT(SG2)/(DSA+EPS60)
      SGMS_COP(ICPV)=SG*FSIG
C
      STVS_COP(ICPV)=STVS_COP(ICPV)+SG2*ZFLUXQ/XNM/XN
      EES_COP(ICPV)=EES_COP(ICPV)+DS*ZFLUX/XN
C
2200  CONTINUE
      RETURN
      END
