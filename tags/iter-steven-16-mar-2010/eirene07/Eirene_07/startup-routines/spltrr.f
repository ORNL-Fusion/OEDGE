C    AUG. 05: NCELL UPDATED FOR SPLITTING AND THEN RESET
!pb 08.11.06: definition of splitting arrays changed 
!             RSPLST(NLEVEL,1:NPARTT) --> RSPLST(1:NPARTT,NLEVEL)
C
      SUBROUTINE SPLTRR(IDIM,MS,NINC,*,*)
C
C  SPLITTING AND RUSSIAN ROULETTE SURFACE
C
C  IDIM  =1: RADIAL SURFACE
C        =2: POLOIDAL SURFACE
C        =3: TOROIDAL SURFACE
C        =4: ADDITIONAL SURFACE
C        =0: NOT ON ANY SURFACE
C  NINC  >0: RUSSIAN ROULETTE
C  NINC  <0: SPLITTING
C  RETURN 1: SPLIT AND CONTINUE FLIGHT
C  RETURN 2: STOP FLIGHT, BECAUSE OF RUSSIAN ROULETTE
C
C  SPLITTING AND RUSSIAN ROULETTE SURFACE MS
C
      USE PRECISION
      USE PARMMOD
      USE COMPRT
      USE COMSPL
      USE CGRID

      IMPLICIT NONE

      INTEGER, INTENT(IN) :: IDIM, MS, NINC
      REAL(DP) :: XNU, XM, YM, ZM, ZNU1, ZEP1, ZNU, X0S, Y0S, Z0S,
     .            TIMES
      REAL(DP), EXTERNAL :: RANF_EIRENE
      INTEGER :: IPOLGS, MASRFS, MSURFS, J, IADD, NU, IG, NCELLS

      IF (IDIM.EQ.1) THEN
        IADD=0
      ELSEIF (IDIM.EQ.2) THEN
        IADD=N1ST
      ELSEIF (IDIM.EQ.3) THEN
        IADD=N1ST+N2ND
      ELSEIF (IDIM.EQ.4) THEN
        IADD=N1ST+N2ND+N3RD
      ELSE
      ENDIF
C
      ZNU=ABS(RNUMB(IADD+MS))
      NU=ZNU
      IG=SIGN(1._DP,RNUMB(IADD+MS))
C
C  RUSSIAN ROULETTE?
      IF(NINC*IG.GT.0) GO TO 340
C
C  NO - SPLIT PARTICLE
      X0S=X0
      Y0S=Y0
      Z0S=Z0
      TIMES=TIME
      NCELLS=NCELL
C
      X0=X0+VELX*ZT
      Y0=Y0+VELY*ZT
      Z0=Z0+VELZ*ZT
      TIME=TIME+ZT/VEL
      NCELL=NRCELL+((NPCELL-1)+(NTCELL-1)*NP2T3)*NR1P2+NBLCKA
C
C SPECIFIC FOR SPLITTING AT RADIAL SURFACES:
C
      MASRFS=MASURF
      MSURFS=MSURF
      IPOLGS=IPOLG
C
      IPOLG=IPOLGN
      MASURF=0
      MSURF=0
C
      IF (NLTRC) CALL CHCTRC(X0,Y0,Z0,16,9)
      IF (NLSTOR) CALL STORE(200)
C
C
      NLEVEL=NLEVEL+1
C  IS SPLITTING PARAMETER AN INTEGER?
      IF (NU.NE.ZNU) THEN
        XNU=ZNU-DBLE(NU)
        IF (RANF_EIRENE( ).LT.XNU) NU=NU+1
      ENDIF
C
C   SPLIT INTO SEVERAL PARTICLES WITH REDUCED WEIGHT
C   SAVE LOCATION, WEIGHT AND OTHER PARAMETERS AT CURRENT LEVEL
      WEIGHT=WEIGHT/ZNU
      DO 333 J=1,NPARTC
        RSPLST(J,NLEVEL)=RPST(J)
333   CONTINUE
      DO 334 J=1,MPARTC
        ISPLST(J,NLEVEL)=IPST(J)
334   CONTINUE
C  NUMBER OF NODES AT THIS LEVEL
      NODES(NLEVEL)=NU
C  RESTORE SOME PARTICLE CO-ORDINATES
      X0=X0S
      Y0=Y0S
      Z0=Z0S
      TIME=TIMES
      NCELL=NCELLS
C
C  SPECIFIC FOR SPLITTING ON RADIAL SURFACES:
      MASURF=MASRFS
      MSURF=MSURFS
      IPOLG=IPOLGS
C
C  CONTINUE THE OLD TRACK WITH REDUCED WEIGHT
      RETURN 1
C
C   RUSSIAN ROULETTE
C
340   CONTINUE
C  PROBABLITY OF DEATH
      ZNU1=1.0/ZNU
      ZEP1=RANF_EIRENE( )
C  IS THIS PARTICLE TO BE KILLED
      IF (ZEP1.LT.ZNU1) THEN
C  NO   INCREASE WEIGHT AND CONTINUE TRACKING
        WEIGHT=WEIGHT*ZNU
        RETURN 1
      ENDIF
C
C  YES   KILL PARTICLE AND STOP FLIGHT
C
      IF (NLTRC) THEN
        XM=X0+VELX*ZT
        YM=Y0+VELY*ZT
        ZM=Z0+VELZ*ZT
        CALL CHCTRC(XM,YM,ZM,16,10)
      ENDIF
      IF (NLSTOR) CALL STORE(0)
      LGPART=.FALSE.
      RETURN 2
      END
