C
C
      SUBROUTINE CDEF(AL,JI,JE,K,COU,NTE,CF,LEXP,LTEST,LSUM,IFCLF)
C
C  EIRENE ATOMIC DATA , DEFAULT OR FROM FILE: POLYNOM FIT FORMAT
C
C  INPUT:
C    AL(J),J=1,NTE
C  OUTPUT:
C    MAXWELLIAN RATES, AL=LN(KT), KT IN (EV)
C  K
C       K>0: A&M DATA FROM FILES HYDHEL, METHANE OR AMJUEL
C           K:  NUMBER OF REACTION IN EIRENE "CREAC"-ARRAY
C
C       K<0: HARD WIRED EIRENE DEFAULT ATOMIC AND MOLECULAR DATA PACKAGE
C       EACH NUMBER ABS(K) CORRESPONDS TO ONE SPECIFIC REACTION DATA FIT
C       (SEE COMMENTS BELOW)
C       COEFFICIENTS FOR THESE DEFAULT REACTIONS ARE SPECIFIED IN
C       SUBROUTINE SETUP_DEFAULT_REACTIONS
C
C  JI,JE
C  FOR JI<=J<=JE RETURN:
C       1<=J<=9:  JTH ENERGY COEFFICIENT OF TWO PARAM. FITS
C                 AT TEMPERATUR KT (EV)
C                 (E.G.: RATES FOR HEAVY PARTICLE INTERACTIONS)
C                 IF ONLY ONE FIT AVAILABLE, ITS INDEX IS J=1
C                 E.Q. FOR ELECT. IMP. RATES AS FUNCTION OF TE
C  IFCLF 
C       FLAG DESCRIBING WHICH SORT OF REACTION DATA IS TO USED
C       = 1    POTENTIAL
C       = 2    CROSS SECTION
C       = 3    RATE COEFFICIENT
C       = 4    MOMENTUM-WEIGHTED RATE COEFFICIENT
C       = 5    ENERGY-WEIGHTES RATE COEFFICIENT
C       = 6    OTHER 
C
C  FIT FROM JANEV ET AL, PPPL-TM-368, 1985  (PREPRINT) OR:
C           SPRINGER SERIES ON ATOMS AND PLASMAS, VOL 4, 1987
C
      USE PRECISION
      USE PARMMOD
      USE CCONA
      USE COMXS
      USE COMPRT, ONLY: IUNOUT

      IMPLICIT NONE

      REAL(DP), INTENT(IN) :: AL(*)
      REAL(DP), INTENT(OUT) :: COU(0:9,*),CF(9,0:9)
      INTEGER, INTENT(IN) :: JI, JE, K, NTE, IFCLF
      LOGICAL, INTENT(IN) :: LEXP, LTEST, LSUM
      REAL(DP) :: CTEST
      INTEGER :: ICELL, J, II
C
C
C  DATA FROM A&M DATA FILES
      SELECT CASE (IFCLF)
      CASE (1)
        IF (.NOT.REACDAT(K)%LPOT) THEN
          WRITE (IUNOUT,*) ' NO DATA AVAILABLE FOR POTENTIAL',K
          CALL EXIT_OWN(1)
        END IF
        CF(1:9,1) = REACDAT(K)%POT%POLY%DBLPOL(1:9,1)
      CASE (2)
        IF (.NOT.REACDAT(K)%LCRS) THEN
          WRITE (IUNOUT,*) ' NO DATA AVAILABLE FOR CROSS SECTION',K
          CALL EXIT_OWN(1)
        END IF
        CF(1:9,1) = REACDAT(K)%CRS%POLY%DBLPOL(1:9,1)
      CASE (3)
        IF (.NOT.REACDAT(K)%LRTC) THEN
          WRITE (IUNOUT,*) ' NO DATA AVAILABLE FOR RATE COEFFICIENT',K
          CALL EXIT_OWN(1)
        END IF
        CF(1:9,JI:JE)=REACDAT(K)%RTC%POLY%DBLPOL(1:9,JI:JE)
      CASE (4)
        IF (.NOT.REACDAT(K)%LRTCMW) THEN
          WRITE (IUNOUT,*) ' NO DATA AVAILABLE FOR',
     .                     ' MOMENTUM-WEIGHTED RATE COEFFICIENT',K
          CALL EXIT_OWN(1)
        END IF
        CF(1:9,JI:JE)=REACDAT(K)%RTCMW%POLY%DBLPOL(1:9,JI:JE)
      CASE (5)
        IF (.NOT.REACDAT(K)%LRTCEW) THEN
          WRITE (IUNOUT,*) ' NO DATA AVAILABLE FOR',
     .                     ' ENERGY-WEIGHTED RATE COEFFICIENT',K
          CALL EXIT_OWN(1)
        END IF
        CF(1:9,JI:JE)=REACDAT(K)%RTCEW%POLY%DBLPOL(1:9,JI:JE)
      CASE (6)
        IF (.NOT.REACDAT(K)%LOTH) THEN
          WRITE (IUNOUT,*) ' NO DATA AVAILABLE FOR',
     .                     ' OTHER REACTION',K
          CALL EXIT_OWN(1)
        END IF
        CF(1:9,JI:JE)=REACDAT(K)%OTH%POLY%DBLPOL(1:9,JI:JE)
      CASE DEFAULT
        WRITE (IUNOUT,*) ' WRONG FLAG IFCLF SPECIFIED IN CDEF'
        WRITE (IUNOUT,*) ' IFCLF = ', IFCLF 
        CALL EXIT_OWN(1)
      END SELECT

      IF (LTEST) THEN
        DO 13 J=JI,JE
          CTEST=0.
          DO 14 II=1,9
            CTEST=CTEST+ABS(CF(II,J))
14        CONTINUE
          IF (CTEST.LE.EPS60) GOTO 990
13      CONTINUE
      ENDIF
C
      IF (LSUM) THEN
        DO J=JI,JE
          DO ICELL=1,NTE
            COU(J,ICELL)=CF(9,J)
          END DO
        END DO
C
        DO 21 J=JI,JE
          DO 22 II=8,1,-1
            DO 23 ICELL=1,NTE
              COU(J,ICELL)=COU(J,ICELL)*AL(ICELL)+CF(II,J)
23          CONTINUE
22        CONTINUE
21      CONTINUE
      ENDIF
C
      IF (LEXP) THEN
        DO 24 ICELL=1,NTE
          COU(JE,ICELL)=EXP(MAX(-100._DP,COU(JE,ICELL)))
24      CONTINUE
      ENDIF
C
      RETURN
C
C
990   CONTINUE
      WRITE (iunout,*) 'ERROR IN SUBROUTINE CDEF: ZERO FIT COEFFICIENTS'
      WRITE (iunout,*) 'J,K = ',J,K,'  EXIT CALLED!'
      CALL EXIT_OWN(1)
      END
