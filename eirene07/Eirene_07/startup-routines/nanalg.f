C
C   ****************************
C   *  NONANALOG METHODS, A.I. *
C   ****************************
C
C      SUBROUTINE NANALG
C
      SUBROUTINE NANALG
C
C   SET UP SPLITTING SURFACES
C
      USE PRECISION
      USE PARMMOD
      USE CADGEO
      USE CLOGAU
      USE CGRID
      USE CTRCEI
      USE COMPRT
      USE COMSPL

      IMPLICIT NONE

      REAL(DP) :: RSPLIT(N1ST)
      REAL(DP) :: XMAXR1, ZR1, ZR2
      INTEGER :: IR, IP, IT, IRT, IRP, IA, IRA, IRD, J, MMXRAD, MAXR1,
     .           JR, JS

C---------------------------------------------------------------------
      NODES=0
      RSPLIT=0.D0
      NLSPLT(1:N1ST+N2ND+N3RD+NLIM)=.FALSE.
C
C
100   CONTINUE
C  SET RADIAL SPLITTING SURFACES
      IF(NR1ST.LE.2) GOTO 200
C
C  DEFAULT MODEL: 101--119
C
C  HARD WIRED OPTIONS FOR RADIAL SPLITTING SURFACES
C  USES: MAXRAD, SPLPAR, AND RHOSRF-GRID
C  SETS: RSPLIT(ISURF) : SPLITTING SURFACE RADIA
C  FINDS: JS: RADIAL SURFACE NUMBER TO BE USED AS SPLITTING SURFACE
C
      IF (LEVGEO.LE.3.AND.MAXRAD.LT.0) THEN
        MMXRAD=ABS(MAXRAD)
        MAXR1=MMXRAD+1
        XMAXR1=MMXRAD+1
        DO 101 J=2,MAXR1
          RSPLIT(J-1)=RHOSRF(1)+(J-1)/XMAXR1*(RHOSRF(NR1ST)-RHOSRF(1))
101     CONTINUE
C
C
C
        DO 110 JS=2,MIN(NR1ST,N1ST+N2ND+N3RD+NLIM)
          ZR1=RHOSRF(JS-1)
          ZR2=RHOSRF(JS)
          DO 104 JR=1,MMXRAD
            IF(RSPLIT(JR).GE.ZR2.OR.RSPLIT(JR).LT.ZR1) GOTO 104
C  RADIAL SURFACE JS SHOULD BE USED AS SPLITTING SURFACE
            RNUMB(JS)=SPLPAR
            NLSPLT(JS)=.TRUE.
104       CONTINUE
110     CONTINUE
        GOTO 200
      ENDIF
C
C  NON DEFAULT MODEL: 121--199
C
      DO 121 IRD=1,MAXRAD
        DO IR=1,MIN(NR1ST,N1ST+N2ND+N3RD+NLIM)
          IF (IR.EQ.NSSPL(IRD)) THEN
            RNUMB(IR)=PRMSPL(IRD)
            NLSPLT(IR)=.TRUE.
          ENDIF
        ENDDO
121   CONTINUE
C
200   CONTINUE
C
C  SET POLOIDAL SPLITTING SURFACES
      IF(NP2ND.LE.2) GOTO 300
C
C  NON DEFAULT MODEL: 221--299
C
      DO 221 IRP=1,MAXPOL
        DO IP=1,NP2ND
          IF (IP.EQ.NSSPL(N1ST+IRP)) THEN
            RNUMB(N1ST+IP)=PRMSPL(N1ST+IRP)
            NLSPLT(N1ST+IP)=.TRUE.
          ENDIF
        ENDDO
221   CONTINUE
C
300   CONTINUE
C
C  SET TOROIDAL SPLITTING SURFACES
      IF(NT3RD.LE.2) GOTO 400
C
C  NON DEFAULT MODEL: 321--399
C
      DO 321 IRT=1,MAXTOR
        DO IT=1,NT3RD
          IF (IT.EQ.NSSPL(N1ST+N2ND+IRT)) THEN
            RNUMB(N1ST+N2ND+IT)=PRMSPL(N1ST+N2ND+IRT)
            NLSPLT(N1ST+N2ND+IT)=.TRUE.
          ENDIF
        ENDDO
321   CONTINUE
C
400   CONTINUE
C  SET ADDITIONAL SPLITTING SURFACES
      IF(NLIMI.LE.0) GOTO 500
C
C  NON DEFAULT MODEL: 421--499
C
      DO 421 IRA=1,MAXADD
        DO IA=1,NLIMI
          IF (IA.EQ.NSSPL(N1ST+N2ND+N3RD+IRA)) THEN
            RNUMB(N1ST+N2ND+N3RD+IA)=PRMSPL(N1ST+N2ND+N3RD+IRA)
            NLSPLT(N1ST+N2ND+N3RD+IA)=.TRUE.
          ENDIF
        ENDDO
421   CONTINUE
C
500   IF (.NOT.TRCNAL) RETURN
      WRITE (iunout,*) 
     .  'SPLITTING SURFACES, BELONGING TO THE STANDARD  MESH '
      CALL LEER(1)
      WRITE (iunout,*) 'NLSPLT(ISURF)=TRUE INDICATES THAT THE'
      WRITE (iunout,*) 'SURFACE WITH NUMBER ISURF IS SPLITTING'
      IF (NR1ST.GT.1) THEN
        WRITE (iunout,*) 'RADIAL SURFACES: '
        CALL MASAL1 ('NLSPLT',NLSPLT(1),MIN(NR1ST,N1ST+N2ND+N3RD+NLIM))
      ENDIF
      IF (NP2ND.GT.1) THEN
        WRITE (iunout,*) 'POLOIDAL SURFACES: '
        CALL MASAL1 ('NLSPLT',NLSPLT(N1ST+1),NP2ND)
      ENDIF
      IF (NT3RD.GT.1) THEN
        WRITE (iunout,*) 'TOROIDAL SURFACES: '
        CALL MASAL1 ('NLSPLT',NLSPLT(N1ST+N2ND+1),NT3RD)
      ENDIF
      IF (NLIMI.GE.1) THEN
        WRITE (iunout,*) 'ADDITIONAL SURFACES: '
        CALL MASAL1 ('NLSPLT',NLSPLT(N1ST+N2ND+N3RD+1),NLIMI)
      ENDIF
      CALL LEER(1)
      RETURN
      END
